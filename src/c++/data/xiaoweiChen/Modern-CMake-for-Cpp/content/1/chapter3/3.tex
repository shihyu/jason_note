
随着解决方案的行数和文件数的增长，慢慢地不可避免的事情即将到来:要么开始对项目进行区分，要么淹没在代码行和大量文件中。可以通过两种方式解决这个问题:分配CMake代码和将源文件移动到子目录中。这两种情况下，目标都是遵循称为关注点分离的设计原则，就是将代码分成块，将功能密切相关的代码分组，同时将其他代码分离，以创建强大的边界。

在第1章中讨论列表文件时，讨论了一些关于分区CMake代码的内容。讨论了include()指令，该指令允许CMake从外部文件执行代码。调用include()不会引入文件中没有定义的作用域(若所包含的文件包含函数，则在调用时将正确处理它们的作用域)。

这种方法有助于分离关注点，但是只有一些专门的代码提取到单独的文件中，甚至可以在不相关的项目之间共享，若作者不小心的话，仍然会用其内部逻辑污染全局变量作用域。编程中有一个古老的真理:最差的机制好过最好的意图。

稍后将学习如何处理这个问题，现在让我们将注意力转移到源代码上。

假设有一个支持小型汽车租赁公司的软件示例——将有许多源文件定义软件的不同方面:管理客户、汽车、停车位、长期合同、维护记录、员工记录等。若要把所有这些文件放在一个目录中，查找文件肯定会是一场噩梦。因此，在项目的主目录中创建了许多目录，并将相关文件移动到其中。我们的CMakeLists.txt文件看起来类似这样:

\begin{lstlisting}[style=styleCMake]
# chapter03/01-partition/CMakeLists.txt

cmake_minimum_required(VERSION 3.20.0)
project(Rental CXX)
add_executable(Rental
				main.cpp
				cars/car.cpp
				# more files in other directories
)
\end{lstlisting}

这一切都很好，但我们仍然在顶级文件中拥有来自嵌套目录的源文件列表!为了增加关注点的分离，可以将源列表放在另一个列表文件中，并使用前面提到的include()指令和cars\_sources变量:

\begin{lstlisting}[style=styleCMake]
# chapter03/02-include/CMakeLists.txt

cmake_minimum_required(VERSION 3.20.0)
project(Rental CXX)
include(cars/cars.cmake)
add_executable(Rental
				main.cpp
				${cars_sources}
				# ${more variables}
)
\end{lstlisting}

新的嵌套列表文件将包含源代码:

\begin{lstlisting}[style=styleCMake]
# chapter03/02-include/cars/cars.cmake

set(cars_sources
	cars/car.cpp
	# cars/car_maintenance.cpp
)
\end{lstlisting}

CMake可以有效地将cars\_sources设置在与add\_executable相同的范围内，用所有文件填充变量。但这个解决方案有一些缺陷:

\begin{itemize}
\item 
来自嵌套目录的变量会污染顶层作用域(反之亦然):

在简单的示例中这不是一个问题，但在更复杂的、在流程中使用多个变量的多级树中，其很快就会成为一个难以调试的问题。

\item 

所有的目录将共享相同的配置:

随着项目多年来的成熟，这个问题会更加的明显。若没有粒度度的存在，必须将每个翻译单元视为相同的，并且不能指定不同的编译标志，不能为代码的某些部分选择较新的语言版本，也不能在选定的代码区域设置静默警告。所有内容都是全局的，需要同时对所有源文件进行更改。

\item 
有一些共享编译触发器:

对配置的更改必须重新编译所有文件，即使更改对其中一些文件并没有意义。

\item 
所有的路径都相对于顶层:

注意cars.cmake中，必须提供cars/car.cpp文件的完整路径。这将导致大量重复的文本破坏可读性，并违背的\textit{不要自我重复}(DRY)原则。并且，重命名一个目录会很困难。
\end{itemize}

另一种方法是使用add\_subdirectory()指令，引入了一个变量范围等。

\subsubsubsection{3.3.1\hspace{0.2cm}作用域的子目录}

按照文件系统的自然结构来构造项目是一种常见的做法，其中嵌套的目录表示应用程序的离散元素:业务逻辑、GUI、API和报告，最后，用测试、外部依赖项、脚本和文档分隔目录。为了支持这个概念，CMake提供了以下指令:

\begin{lstlisting}[style=styleCMake]
add_subdirectory(source_dir [binary_dir]
  [EXCLUDE_FROM_ALL])
\end{lstlisting}

这将向构建添加一个源目录，也可以提供一个写入构建文件的路径(binary\_dir)。EXCLUDE\_FROM\_ALL关键字将禁用在子目录中定义的目标的默认构建(将在下一章讨论目标)。这对于分离核心功能不需要的项目部分(例如，示例和扩展)可能很有用。

这个指令将在source\_dir路径中查找CMakeLists.txt文件(相对于当前目录求值)。这个文件将在目录作用域中进行解析，所以前面方法中提到的所有缺陷都不存在:

\begin{itemize}
\item 
变量更改与嵌套作用域隔离。

\item 
可以随心所欲地配置嵌套工件。

\item 
更改嵌套的CMakeLists.txt文件中不需要构建和不相关的目标。

\item 
路径是目录的本地路径，若需要，可以添加到父include路径。
\end{itemize}

来看一个add\_subdirectory()的项目:

\begin{tcblisting}{commandshell={}}
chapter03/03-add_subdirectory# tree -A
.
├── CMakeLists.txt
├── cars
│     ├── CMakeLists.txt
│     ├── car.cpp
│     └── car.h
└── main.cpp
\end{tcblisting}

这里，有两个CMakeLists.txt文件。顶层文件将使用嵌套目录cars:

\begin{lstlisting}[style=styleCMake]
# chapter03/02-add_subdirectory/CMakeLists.txt

cmake_minimum_required(VERSION 3.20.0)
project(Rental CXX)

add_executable(Rental main.cpp)

add_subdirectory(cars)
target_link_libraries(Rental PRIVATE cars)
\end{lstlisting}

最后一行用于将cars目录中的构件链接到Rental可执行文件。这是一个特定于目标的指令，我们将在下一章深入讨论。来看看嵌套的列表文件是什么样的:

\begin{lstlisting}[style=styleCMake]
#chapter03/02-add_subdirectory/cars/CMakeLists.txt

add_library(cars OBJECT
	car.cpp
# car_maintenance.cpp
)
target_include_directories(cars PUBLIC .)
\end{lstlisting}

使用add\_library()生成了全局可见的目标cars，并使用target\_include\_ directories()将cars目录添加到其公共包括目录中。这允许main.cpp包含cars.h文件而不提供相对路径:

\begin{lstlisting}[style=styleCXX]
#include "car.h"
\end{lstlisting}

可以在嵌套的列表文件中看到add\_library()指令，示例中是否开始使用库了呢?事实上没有，因为我们使用了OBJECT关键字，这表示只对生成目标文件感兴趣(正如在前面的示例中所做的那样)，只是将它们归类到一个逻辑目标(汽车)下。

\subsubsubsection{3.3.2\hspace{0.2cm}嵌套项目}

上一节中，简要地提到了add\_subdirectory()中使用的EXCLUDE\_FROM\_ALL参数。CMake文档建议，若在源码树中有这样的部件，应该在它们的CMakeLists.txt文件中有自己的project()指令，这样就可以生成自己的构建系统，并且可以独立构建。

其他情况下这是否有用?当然。例如，正在处理在一个CI/CD流水中构建的多个C++项目(构建一个框架或一组库时)。或者，也许正在从遗留解决方案中移植构建系统，例如使用普通makefile的GNU Make，可能需要一个选项来将事情慢慢地分解为更独立的部分——将它们放在单独的构建流水中，或者只是在更小的范围上工作，可以由CLion等IDE加载。

可以通过向嵌套目录中的列表文件添加project()来实现，不要忘记在前面加上cmake\_minimum\_required()。

既然支持项目嵌套，是否可以以某种方式将一起构建的相关项目连接起来?

\subsubsubsection{3.3.3\hspace{0.2cm}外部项目}

从一个项目到另一个项目在技术上存在，CMake将在一定程度上支持这一点。甚至还有load\_cache()指令允许从另一个项目的缓存中加载值。这不是一个常规的或推荐的用例，其将导致周期性依赖和项目耦合的问题，最好避免使用这个指令。所以，相关项目是应该嵌套，通过库连接，还是合并到单个项目中?

我们可以使用这些工具进行的分区:包括列表文件、添加子目录和嵌套项目。但应该如何使用它们，使我们的项目保持可维护性、易于导航和扩展呢?要做到这一点，需要一个定义良好的项目结构。