
对于CMake项目，工具链包含构建和运行应用程序时使用的所有工具——例如，工作环境、生成器、CMake可执行程序本身和编译器。

当构建因一些编译和语法错误而停止时，缺乏经验的用户会有什么感受。他们必须深入研究源代码，并试图理解发生了什么。经过一个小时的调试后，发现正确的解决方案是更新编译器。我们能否为用户提供更好的体验，并在开始构建之前检查编译器中是否存在所有所需的函数?

当然!有几种方法可以指定这些需求。若工具链不支持所有必需的特性，CMake将提前停止并显示发生了什么事情的明确消息，要求用户介入。

\subsubsubsection{3.6.1\hspace{0.2cm}设定C++标准}

若用户想要构建我们的项目，可能要做的第一件事是设置编译器需要支持的C++标准。对于新项目，这应该至少是C++14，但最好是C++17或C++20。CMake还支持将标准设置为实验性的C++23，但这目前只是一个草案。

\begin{tcolorbox}[colback=blue!5!white,colframe=blue!75!black,title=Note]
从C++11正式发布到现在已经10年了，它不再认为是现代C++标准。不建议使用此版本启动项目，除非目标环境非常旧。

坚持旧标准的另一个原因是，若正在构建难以升级的历史项目。然而，C++委员会非常努力地保持向后兼容。通常，升级标准不会遇到任何问题。
\end{tcolorbox}

CMake支持在每个目标的基础上设置标准，所以可以根据偏好的粒度使用。我认为最好在整个项目中采用单一标准。这可以通过设置CMAKE\_CXX\_STANDARD变量为以下值之一来实现:98、11、14、17、20或23(自CMake 3.20以来)。这将是所有随后定义的目标的默认值(因此最好将其设置在根列表文件顶部附近)。可以在每个目标的基础上覆写:


\begin{lstlisting}[style=styleCMake]
set_property(TARGET <target> PROPERTY CXX_STANDARD <standard>)
\end{lstlisting}

\subsubsubsection{3.6.2\hspace{0.2cm}坚持支持标准}

上一节中提到的CXX\_STANDARD属性不会阻止CMake继续构建，即使编译器不支持所需的版本——视为首选项。CMake不知道代码是否真的使用了以前编译器中没有的全新功能，它将尝试使用现有的功能。

若确定这不会成功，可以设置另一个默认标志(它可以以与前一个相同的方式被覆盖)，并显式要求目标的标准:

\begin{lstlisting}[style=styleCMake]
set(CMAKE_CXX_STANDARD_REQUIRED ON)
\end{lstlisting}

若系统中没有最新的编译器(GNU GCC 11)，用户只会看到以下消息，构建将停止:

\begin{tcblisting}{commandshell={}}
Target "Standard" requires the language dialect "CXX23" (with
compiler extensions), but CMake does not know the compile flags
to use to enable it.
\end{tcblisting}

即使是在现代环境中，要求使用C++23也可能有点过分。但C++14应该没问题，因为自2015年以来GCC/Clang已经完全支持它了。

\subsubsubsection{3.6.3\hspace{0.2cm}特定于供应商的扩展}

根据组织中实现的策略，可能会对允许或禁用特定于供应商的扩展感兴趣。这些是什么?这么说吧，对于一些编译器生产者的需求，C++标准的发展有点慢，所以他们决定在语言中添加他们自己的增强——也可以叫插件。为了实现这一点，CMake会将-std=gnu++14，而不是-std=c++14添加到编译行。

一方面，因为它允许一些方便的功能，这可能是需要的。但另一方面，若切换到另一个编译器(或者您的用户切换到另一个编译器!)，代码可能无法构建。

这也是一个每个目标的属性，它有一个默认变量CMAKE\_CXX\_EXTENSIONS。CMake在这里更加自由，并且允许扩展，除非我们告诉它不要这样做:

\begin{lstlisting}[style=styleCMake]
set(CMAKE_CXX_EXTENSIONS OFF)
\end{lstlisting}

若可能的话，我建议这样做，因为这个选项将坚持使用与供应商无关的代码。这样的代码不会对用户施加任何不必要的要求。以类似的方式，可以使用set\_property()在每个目标的基础上更改这个值。

\subsubsubsection{3.6.4\hspace{0.2cm}过程间优化}

通常，编译器在单个翻译单元的级别上优化代码，所以.cpp文件会进行预处理、编译，然后进行优化。稍后，这些文件将传递给链接器以构建一个二进制文件。现代编译器可以在链接之后执行优化(这称为链接时优化)，以便所有编译单元都可以作为单个模块进行优化。若编译器支持过程间优化，那么这可能是个好主意。

我们将遵循与前面相同的方法。需要设置的默认变量叫做CMAKE\_INTERPROCEDURAL\_OPTIMIZATION。在设置它之前，需要确保支持，以避免错误:

\begin{lstlisting}[style=styleCMake]
include(CheckIPOSupported)
check_ipo_supported(RESULT ipo_supported)
if(ipo_supported)
	set(CMAKE_INTERPROCEDURAL_OPTIMIZATION True)
endif()
\end{lstlisting}

必须包含一个内置模块来访问check\_ipo\_supported()指令。

\subsubsubsection{3.6.5\hspace{0.2cm}检查支持的编译器特性}

若构建失败，最好尽早失败，这样就可以向用户提供明确的反馈消息。我们特别感兴趣的是衡量支持哪些C++特性(哪些不支持)。CMake会在配置阶段询问编译器，并在CMAKE\_CXX\_COMPILE\_FEATURES变量中存储可用特性的列表。可以写一个非常具体的检查脚本，询问是否有某种特性可用:

\begin{lstlisting}[style=styleCMake]
# chapter03/07-features/CMakeLists.txt

list(FIND CMAKE_CXX_COMPILE_FEATURES
	cxx_variable_templates result)
if(result EQUAL -1)
	message(FATAL_ERROR "I really need variable templates.")
endif()
\end{lstlisting}

使用的每个功能编写一个文档是一项艰巨的任务。甚至CMake的作者也建议只检查是否存在某些高级元特性:cxx\_std\_98、cxx\_std\_11、cxx\_std\_14、cxx\_std\_17、cxx\_std\_20和cxx\_std\_23，每个元特性都表明编译器支持特定的C++标准。

CMake功能的完整列表可以在文档中找到:\url{https://cmake.org/cmake/help/latest/prop_gbl/CMAKE_CXX_KNOWN_ FEATURES.html}。

\subsubsubsection{3.6.6\hspace{0.2cm}编译测试文件}

用GCC 4.7.x编译一个应用程序时，我想到了一个特别有趣的场景。我已经在编译器的参考中手动确认，使用的所有C++11特性都得到了支持。然而，解决方案仍然不能正常工作。原因：因为代码默认使用了标准头文件<regex>，而在GCC 4.7.x因为某个bug，使得正则表达式库还没有实现。

没有检查可以保护您不受这种错误的影响，但是可以通过创建一个测试文件来减少这种行为，该文件可以填充您想要检查的所有特性。CMake提供了两个配置时间命令，try\_compile()和try\_run()，以验证目标平台上支持所需的一切。

第二个命令提供了更多的自由，因为可以确保代码不仅在编译，而且在正确地执行(可以测试regex是否在工作)。当然，这不适用于交叉编译场景(因为主机无法运行为不同目标构建的可执行文件)。这个检查的目的是在编译工作时向用户提供一个快速反馈，所以并不需要运行单元测试或其他复杂的东西。

像这样:

\begin{lstlisting}[style=styleCXX]
//chapter03/08-test_run/main.cpp

#include <iostream>
int main()
{
	std::cout << "Quick check if things work." << std::endl;
}
\end{lstlisting}

使用test\_run()一点也不复杂。首先设置所需的标准，然后调用test\_run()并将收集到的信息打印给用户:

\begin{lstlisting}[style=styleCMake]
# chapter03/08-test_run/CMakeLists.txt

set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS OFF)

try_run(run_result compile_result
		${CMAKE_BINARY_DIR}/test_output
		${CMAKE_SOURCE_DIR}/main.cpp
		RUN_OUTPUT_VARIABLE output)
		
message("run_result: ${run_result}")
message("compile_result: ${compile_result}")
message("output:\n" ${output})
\end{lstlisting}

这个指令有很多可选的字段要设置，乍一看可能会让人不知所措，但当我们阅读并将其与示例中的调用进行比较时，就会发现所有内容都在一起:

\begin{lstlisting}[style=styleCMake]
try_run(<runResultVar> <compileResultVar>
		<bindir> <srcfile> [CMAKE_FLAGS <flags>...]
		[COMPILE_DEFINITIONS <defs>...]
		[LINK_OPTIONS <options>...]
		[LINK_LIBRARIES <libs>...]
		[COMPILE_OUTPUT_VARIABLE <var>]
		[RUN_OUTPUT_VARIABLE <var>]
		[OUTPUT_VARIABLE <var>]
		[WORKING_DIRECTORY <var>]
		[ARGS <args>...])
\end{lstlisting}

编译和运行一个非常基本的测试文件只需要几个字段，还使用可选的RUN\_OUTPUT\_VARIABLE关键字从stdout收集输出。

下一步是使用一些更现代的C++特性来扩展这个简单的文件，将在整个实际项目中使用这些特性——可能是添加一个可变变量模板，看看目标机器上的编译器是否能够消化它。每次实际项目中引入一个新特性时，可以在测试文件中放入一个相同特性的小样本，确定检查编译是否在尽可能短的时间内工作。

最后，可以检入条件块，若收集的输出满足期望，并且当有些东西不正确时打印消息(SEND\_ERROR)。记住SEND\_ERROR将继续通过配置阶段，但不会生成。这有助于在中止构建之前显示遇到的所有错误。



















