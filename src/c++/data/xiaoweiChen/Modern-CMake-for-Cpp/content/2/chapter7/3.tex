管理依赖关系和发现所需的所有编译标志的问题和C++库本身一样古老，有许多工具可以处理它。其中一个(曾经非常流行)工具叫做PkgConfig(\url{freedesktop.org/wiki/Software/pkg-config/})。通常在类Unix系统上可用(也可以在macOS和Windows上工作)。

pkg-config正慢慢被其他更现代的解决方案淘汰。这里出现了一个问题——是否应该投入时间来支持它?答案——视情况而定:

\begin{itemize}
\item 
若一个库非常流行，可能已经在CMake中有它的查找模块。这种情况下，就不需要它。

\item 
若没有查找模块(或者它对库不起作用)，并且该库只提供PkgConfig.pc，那么使用现成的即可。
\end{itemize}

许多(若不是大多数的话)库都采用了CMake，并在当前版本中提供了一个包配置文件。若不需要发布解决方案，并且可以控制环境，请使用find\_package()，不要担心遗留版本的问题。

遗憾的是，并不是所有的环境都可以快速更新到库的最新版本。许多公司仍然在生产中使用遗留系统，这些系统不再获得最新的软件包，用户可能只能使用旧版本(但希望兼容)。通常，会提供一个.pc文件。

此外，若项目能够为大多数用户提供开箱即用的服务，支持较老的PkgConfig格式可能值得。

开始使用find\_package()，若\_FOUND为false，退回到PkgConfig。通过这种方式，可以使用一种环境升级的方式，可以只使用主方法，而不更改代码。

这个助手工具的概念非常简单——库的作者提供了一个包含编译和链接所需细节的小的.pc文件:

\begin{lstlisting}[style=styleCMake]
prefix=/usr/local
exec_prefix=${prefix}
includedir=${prefix}/include
libdir=${exec_prefix}/lib

Name: foobar
Description: A foobar library
Version: 1.0.0
Cflags: -I${includedir}/foobar
Libs: -L${libdir} -lfoobar
\end{lstlisting}

该格式非常简单、轻量级，甚至支持基本的变量展开。这就是为什么许多开发人员更喜欢它，而不是像CMake这样复杂、健壮的解决方案。虽然PkgConfig非常容易使用，但功能非常有限:

\begin{itemize}
\item 
检查系统中是否存在库，以及是否提供了.pc文件

\item 
检查是否有足够的库版本可用

\item 
通过运行pkg-config -{}-libs libfoo获取库的链接器标志

\item 
获取库的包含目录(该字段在技术上可以包含其他编译器标志) ——  pkg-config -{}-cflags libfoo
\end{itemize}

要在构建场景中正确使用PkgConfig，构建系统必须在操作系统中找到pkg-config可执行文件，运行几次并提供适当的参数，并将响应存储在变量中，以便稍后将它们传递给编译器。已经知道如何在CMake中做到这一点——扫描已知的用于存储帮助工具的路径来检查PkgConfig是否已安装，然后使用一些exec\_program()指令来发现如何链接依赖项。尽管这些步骤是有限的，但当想要使用PkgConfig时，每次都这样做似乎有点不妥。

CMake提供了一个方便的内置查找模块——FindPkgConfig。它遵循了常规查找模块的大多数规则，但没有提供PKG\_CONFIG\_INCLUDE\_DIRS或PKG\_CONFIG\_LIBS变量，而是设置了一个直接指向二进制文件的变量——PKG\_CONFIG\_EXECUTABLE。意料之中的是，PKG\_CONFIG\_FOUND变量也设置了——我们将使用它来确认该工具在系统中是可用的，然后用模块中定义的pkg\_check\_modules()帮助命令扫描包。

让我们在实践中看看。一个提供.pc文件的流行库的例子是PostgreSQL数据库的客户端——libpqxx。

要在Debian上安装它，可以使用libpqxx-dev包(不同的操作系统可能需要不同的包):

\begin{tcblisting}{commandshell={}}
apt-get install libpqxx-dev
\end{tcblisting}

创建main.cpp文件，使用虚拟连接类:

\begin{lstlisting}[style=styleCXX]
// chapter07/02-find-pkg-config/main.cpp

#include <pqxx/pqxx>
int main()
{
	// We're not actually connecting, but
	// just proving that pqxx is available.
	pqxx::nullconnection connection;
}
\end{lstlisting} 

现在可以使用PkgConfig查找模块为前面的代码提供必要的依赖项:

\begin{lstlisting}[style=styleCMake]
# chapter07/03-find-pkg-config/CMakeLists.txt

cmake_minimum_required(VERSION 3.20.0)
project(FindPkgConfig CXX)

find_package(PkgConfig REQUIRED)
pkg_check_modules(PQXX REQUIRED IMPORTED_TARGET libpqxx)
message("PQXX_FOUND: ${PQXX_FOUND}")

add_executable(main main.cpp)
target_link_libraries(main PRIVATE PkgConfig::PQXX)
\end{lstlisting}

让我们分析一波：

\begin{itemize}
\item 
要求CMake使用find\_package()指令查找PkgConfig可执行文件。若由于REQUIRED关键字而不存在pkg-config，则会失败。

\item 
调用FindPkgConfig查找模块中定义的pkg\_check\_modules()自定义宏，以PQXX作为所选名称创建一个新的导入目标。查找模块将搜索名为libpxx的依赖项，若库因为REQUIRED关键字而不可用，将再次失败。注意IMPORTED\_TARGET关键字——若没有它，就不会自动创建目标，必须用宏手动定义它。


\item 
通过打印PQXX\_FOUND确认诊断消息中的一切都是正确的。若没有在前面的命令中指定REQUIRED，可以在这里检查这个变量是否设置(可能是为了允许其他回退机制启动)。

\item 
创建main可执行文件。

\item 
链接由pkg\_check\_modules()创建的PkgConfig::PQXX导入目标。注意，PkgConfig::是一个常量前缀，PQXX来自传递给该命令的第一个参数。
\end{itemize}

这是引入尚不支持CMake的依赖项的一种相当方便的方法。这个查找模块有一些其他的方法和选项，若有兴趣了解更多，建议参考官方文档:\url{https://cmake.org/ cmake/help/latest/module/FindPkgConfig.html}。

查找模块是为CMake提供关于已安装依赖项的一种方式。CMake在所有主要平台上都广泛支持大多数流行的库，但当我们想要使用一个还没有专门的查找模块的第三方库时，该怎么办呢?



















