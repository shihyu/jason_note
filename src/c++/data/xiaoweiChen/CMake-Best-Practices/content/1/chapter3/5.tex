我们已经创建了三个不同的库——一个静态或动态库，一个接口或头文件库，以及一个预编译但没有链接的对象库。

了解如何在共享项目的可执行文件中使用它们。将它们安装为系统库或使用它们作为外部依赖，将在第5章中介绍。

所以，可以把\texttt{add\_library}放在同一个CMakeLists.txt文件中，或者使用\texttt{add\_subdirectory}将其整合起来。两者都是有效的选项，并取决于项目的设置方式。

下面的例子中，假设在hello\_lib、hello\_header\_only和hello\_object目录中已经用CMakeLists.txt文件定义了三个库。可以使用\texttt{add\_subdirectory}包含这些库。这里，创建了一个名为chapter3的新目标，它是可执行文件。然后，将这些库用\texttt{target\_link\_libraries}添加到可执行文件中:

\begin{lstlisting}[style=styleCMake]
add_subdirectory(hello_lib)
add_subdirectory(hello_header_only)
add_subdirectory(hello_object)

add_executable(chapter3)
target_sources(chapter3 PRIVATE src/main.cpp)
target_link_libraries(chapter3 PRIVATE hello_header_only hello
	hello_object)
\end{lstlisting}

\texttt{target\_link\_libraries}的目标也可以是另一个库。同样，库的链接说明符，可以是以下任意一个:

\begin{itemize}
\item 
PRIVATE: 用于链接库，但不是公共接口的一部分。只有在构建目标时才需要链接库。

\item 
INTERFACE: 没有链接到库，但是公共接口的一部分。当在其他地方使用目标时，链接库是必需的。这通常仅限头文件库时使用。

\item 
PUBLIC: 链接到库，是公共接口的一部分。因此，该库既是构建依赖项，也是使用依赖项。

\begin{tcolorbox}[colback=red!5!white,colframe=red!75!black,title=不推荐的做法]
本书的作者极力反对下列实践，因为它们会创建难以维护的项目，使得在不同的构建环境之间难于移植。但为了完整起见，我们将它们包含进来。

除了传递PUBLIC、PRIVATE或INTERFACE之后的另一个目标，还可以传递库的完整路径或库的文件名，例如/usr/share/lib/mylib.so，或者只是mylib.so。这是实践可行的，但不鼓励这样做，因为这会降低CMake项目的可移植性。另外，也可以通过传递诸如-nolibc之类的链接器标志，还是不建议这么做。若所有目标都需要特殊的链接器标志，使用命令行传递是首选的方式。若单个库需要特殊标志，使用\texttt{target\_link\_options}则是首选法，最好与命令行上设置的选项结合使用。
\end{tcolorbox}
\end{itemize}

下一节中，我们将研究如何设置编译器和链接器选项。

\subsubsubsection{3.5.1\hspace{0.2cm}设置编译器和链接器选项}

C++编译器有很多选项来设置一些常见的标志，从外部设置预处理器定义也是一种常见的做法。CMake中，这些是使用\texttt{target\_compile\_options}传递，使用\texttt{target\_link\_options}更改链接器行为，但编译器和链接器可能有不同的设置标志的方法。例如，在GCC和Clang中，选项用减号(-)传递，而Microsoft编译器将斜杠(/)作为选项的前缀。但是通过第1章中介绍的生成器表达式，可以很容易地在CMake中处理这个问题:

\begin{lstlisting}[style=styleCMake]
target_compile_options(
	hello
	PRIVATE $<$<CXX_COMPILER_ID:MSVC>:/SomeOption>
			$<$<CXX_COMPILER_ID:GNU,Clang,AppleClang>:-someOption>
)
\end{lstlisting}

让我们详细了解一下生成器表达式。

\$<\$<CXX\_COMPILER\_ID:MSVC>:/SomeOption>是一个嵌套的生成器表达式，由内而外求值。生成器表达式在生成阶段进行计算。首先，当C++编译器等于MSVC时，\$<CXX\_COMPILER\_ID:MSVC>为true。若是这种情况，那么外部表达式将返回/SomeOption，然后传递给编译器。若内部表达式的计算结果为false，则不传递。

\$<\$<CXX\_COMPILER\_ID:GNU,Clang,AppleClang>:-fopenmp>的工作原理类似，但不是只检查单个值，而是传递一个包含GNU，Clang，AppleClang的列表。若\texttt{CXX\_COMPILER\_ID}匹配其中任何一个，内部表达式计算为true，someOption会传递给编译器。

将编译器或链接器选项传递为PRIVATE，将其标记为与库接口不需要的此目标的构建需求。若使用PUBLIC，那么编译选项也成为构建需求，所有依赖于原始目标的目标将使用相同的编译选项。将编译器选项暴露给依赖的目标是需要谨慎做的事情。若编译器选项只用于使用目标而不用于构建目标，则可以使用关键字INTERFACE。在构建纯头文件库时，这是最常见的情况。

编译器选项的特殊情况是编译定义，其会传递给底层程序。这通过\texttt{target\_compile\_definitions}进行传递。

\hspace*{\fill} \\ %插入空行
\noindent
\textbf{调试编译器选项}

要查看所有编译选项，可以查看生成的构建文件，例如Makefiles 或Visual Studio项目。更方便的方法是让CMake将所有编译命令导出为JSON。

通过使用CMAKE\_EXPORT\_COMPILE\_COMMANDS，将生成一个名为compile\_commands.json的文件，其包含用于编译的完整命令。

启用此选项并运行CMake将产生如下结果:

\begin{lstlisting}[style=styleCMake]
{
	"directory": "/workspaces/CMake-Best-Practices/build",
	"command": "/usr/bin/g++ -I/workspaces/CMake-Best-Practices/
	chapter03/hello_header_only/include
    -I/workspaces/CMake-Best-Practices/chapter03/hello_lib/include
    -I/workspaces/CMake-Best-Practices/chapter03/hello_object_lib/include
    -g -fopenmp -o
	chapter03/CMakeFiles/chapter03.dir/src/main.cpp.o -c /
	workspaces/CMake-Best-Practices/chapter03/src/main.cpp",
	"file": "/workspaces/CMake-Best-Practices/chapter03/src/main.cpp"
},
\end{lstlisting}

注意，上一个示例中添加了手动指定的-fopenMP标志。compile\_commands.json可以用作构建系统无关的方式来加载命令。一些IDE，如VS Code和CLion，可以解释JSON文件并生成项目信息。也常用于调试编译器选项，以防某些事情没有按预期工作。编译命令数据库的完整规范可以在\url{https://clang.llvm.org/docs/JSONCompilationDatabase.html}找到。

\subsubsubsection{3.5.2\hspace{0.2cm}库别名}

库别名是在不创建新的构建目标的情况下引用库的一种方法，有时称为命名空间。常见的模式是为从项目中安装的每个库以MyProject::library的形式创建一个库别名，可以用于对多个目标进行语义分组。有助于避免命名方面的冲突，特别是当项目包含公共目标时，比如名为utils的库、helper和类似的库。在相同的命名空间下收集相同项目的所有目标是一个很好的实践。在链接来自其他项目的库时，包含名称空间可以避免意外包含错误的库。本章中所有的库目标都将使用一个命名空间作为别名来分组它们，以便它们可以使用命名空间引用:

\begin{lstlisting}[style=styleCMake]
add_library(Chapter3::hello ALIAS hello)
...
target_link_libraries(SomeLibrary PRIVATE Chapter3::hello)
\end{lstlisting}

除了帮助确定目标的来源，CMake使用命名空间来识别导入的目标，并提供更好的诊断消息。

\begin{tcolorbox}[colback=blue!5!white,colframe=blue!75!black,title=使用命名空间]
作为一种良好的实践，始终使用名称空间对目标进行别名，并使用”命名空间::前缀”进行引用。
\end{tcolorbox}

命名空间是组织构建构件的一种很好的方式。但有时这还不够，我们希望看到更大的视野，了解“什么正在使用什么”，以及哪个工件依赖于哪个库。CMake可以帮助创建提供这样深刻见解的依赖图，我们将在下一章中看到。














