简单地说，系统根目录(或简称sysroot)是构建系统认为的根目录，可以从中查找头文件和库。这个目录包含用于编译软件的平台的根文件系统的精简版本，经常用于为其他平台交叉编译软件时。若构建容器不是无法完成，sysroot可以作为提供已定义构建环境的替代方案。

要在CMake中使用sysroot，需要一个工具链文件，这些文件定义了用来编译和链接软件的工具，以及在哪里可以找到库。正常的构建中，CMake通过自动检测系统检测工具链。工具链文件通过CMAKE\_TOOLCHAIN\_FILE传递给CMake:

\begin{tcblisting}{commandshell={}}
cmake -S <source_dir> -B <binary_dir> -DCMAKE_TOOLCHAIN_FILE=
  <path/to/toolchain.cmake>
\end{tcblisting}

从3.21版本起，CMake支持-{}-toolchain选项来传递工具链文件，这相当于使用CMAKE\_TOOLCHAIN\_FILE缓存变量。

或者，可以使用CMake预设作为缓存变量来传递工具链文件。至少，与sysroot一起使用的工具链文件将定义CMAKE\_SYSROOT变量，以指向sysroot，并定义CMAKE\_<LANG>\_COMPILER变量，以指向与sysroot中库兼容的编译器。为了避免将来自sysroot外部的依赖项与安装在主机系统上的文件混淆在一起，通常还设置了用于控制\texttt{find\_}指令查找位置的变量。最小的工具链文件可能是这样的:

\begin{lstlisting}[style=styleCMake]
set(CMAKE_SYSTEM_NAME Linux)

set(CMAKE_SYSROOT /path/to/sysroot/)
set(CMAKE_STAGING_PREFIX path/to/staging/directory)

set(CMAKE_C_COMPILER /path/to/sysroot/usr/bin/gcc-10)
set(CMAKE_CXX_COMPILER /path/to/sysroot/usr/bin/g++-10)

set(CMAKE_FIND_ROOT_PATH_MODE_PROGRAM NEVER)
set(CMAKE_FIND_ROOT_PATH_MODE_LIBRARY ONLY)
set(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE ONLY)
set(CMAKE_FIND_ROOT_PATH_MODE_PACKAGE BOTH)
\end{lstlisting}

\begin{enumerate}
\item 
使用CMAKE\_SYSTEM\_NAME变量来设置目标系统的系统名称，在sysroot中为其编译文件的系统。

\item 
通过设置CMAKE\_SYSROOT变量来设置sysroot的路径。CMAKE\_STAGING\_PREFIX可选，用于指定在主机上安装项目构件的位置。指定暂存前缀有助于保持sysroot和主机文件系统的整洁；否则，构件的安装都将发生在主机文件系统上。

\item 
设置CMAKE\_C\_COMPILER和CMAKE\_CXX\_COMPILER变量，将编译器设置为sysroot附带的编译器。

\item 
设置CMake中\texttt{find\_}指令的搜索行为。CMAKE\_FIND\_ROOT\_PATH\_MODE\_*变量可以为ONLY、NEVER和BOTH。若设置为ONLY，CMake将只搜索sysroot中的文件类型;若设置为NEVER，则搜索将只考虑主机文件结构。若设置为BOTH，则将搜索主机系统路径和sysroot路径。注意，CMAKE\_STAGING\_PREFIX认为是系统路径，因此为了搜索sysroot和暂存目录，必须同时选中两者。本例中，配置的方式是将所有头文件和库限制在sysroot中，而\texttt{find\_program}将只查找主机系统，而\texttt{find\_package}将在两个文件系统内查找。
\end{enumerate}

设置CMAKE\_SYSROOT不会自动设置安装构建构件的位置。对于生成的二进制文件与主机系统兼容的情况，这可能是预期的行为。但比如交叉编译，这就不是我们想要的结果，所以建议设置CMAKE\_STAGING\_PREFIX。设置暂存目录有两个效果:第一，暂存目录中进行安装；第二，暂存目录将添加到\texttt{find\_}指令的搜索前缀中。需要注意的是，暂存目录将添加到CMAKE\_SYSTEM\_PREFIX\_PATH中，这有一个问题，即来自前面示例的CMAKE\_FIND\_ROOT\_PATH\_MODE\_XXX变量必须设置为BOTH，这样才能找到安装在暂存区域中的包、库和程序。

\begin{tcolorbox}[colback=blue!5!white,colframe=blue!75!black,title=CMAKE\_STAGING\_PREFIX和CMAKE\_INSTALL\_PREFIX]
若同时设置了CMAKE\_STAGING\_PREFIX和CMAKE\_INSTALL\_PREFIX，则暂存前缀优先。根据经验，只要工具链与主机系统兼容，就可以省略暂存，否则需要进行定义。
\end{tcolorbox}

与容器相比，sysroot的一个缺点是不能启动，也不能在其中执行命令。因此，若工具链和sysroot与主机平台不兼容，产生的文件在不移动到目标平台或使用模拟器的情况下，无法执行。





























