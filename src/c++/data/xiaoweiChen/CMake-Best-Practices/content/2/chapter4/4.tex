已经看到了CMake是如何构建软件项目的，是时候向大家介绍CMake的打包工具CPack了。它默认随CMake安装一起提供，可以利用现有的CMake代码来生成特定于平台的安装和包。CPack在概念上类似于CMake，基于生成包而不是构建系统文件的生成器。下表显示了从3.21.3版本起可用的CPack生成器类型:

\begin{table}[H]
	\centering
	\begin{tabular}{|l|l|}
		\hline
		\textbf{生成器}    & \textbf{描述}             \\ \hline
		7Z                 & 7-zip压缩包                    \\ \hline
		DEB                & Debian包                   \\ \hline
		External           & CPack外部包           \\ \hline
		IFW                & Qt安装程序框架           \\ \hline
		NSIS               & Null软安装程序              \\ \hline
		NSIS64             & Null软安装程序 (64位)      \\ \hline
		NuGet              & NuGet包                   \\ \hline
		RPM                & RPM包                      \\ \hline
		STGZ               & 自解压TAR gzip压缩包 \\ \hline
		TBZ2               & Tar BZip2压缩包                \\ \hline
		TGZ                & Tar GZIP压缩包                 \\ \hline
		TXZ                & Tar XZ压缩包                   \\ \hline
		TZ                 & Tar 压缩包             \\ \hline
		TZST               & Tar Zstandard压缩包           \\ \hline
		ZIP                & Zip压缩包                      \\ \hline
	\end{tabular}
\end{table}

CPack使用CMake的安装机制来填充包的内容，CPack使用CPackConfig.cmake中的配置细节， CPackSourceConfig.cmake文件生成包。这些文件可以手动填写，也可以由CMake在CPack模块的帮助下自动生成。在已有的CMake项目上使用CPack，就像包含CPack模块一样简单，前提是该项目已有\texttt{install(…)}。包含CPack模块会导致CMake生成CPackConfig.cmake和CPackSourceConfig.cmake文件，这是打包项目所需的CPack配置。另外，附加包目标将用于构建步骤。这一步将构建项目并运行CPack，以便开始打包。当CMake或用户正确填充了CPack配置文件，就可以使用CPack。CPack模块允许定制包装过程，从而可以设置大量的CPack变量。这些变量分为两组——普通变量和生成器特定变量。公共变量影响所有包生成器，而生成器特定的变量只影响特定类型的生成器。下表显示了我们在示例中最常使用的CPack变量:

\begin{table}[H]
	\centering
	\begin{tabular}{|l|l|l|}
		\hline
		\textbf{变量名}         & \textbf{描述}            & \textbf{默认值} \\ \hline
		CPACK\_PACKAGE\_NAME           & 包名                    & 项目名           \\ \hline
		CPACK\_PACKAGE\_VENDOR         & 包供应商名             & "Humanity"             \\ \hline
		CPACK\_PACKAGE\_VERSION\_MAJOR & 包主要版本           & 项目主要版本  \\ \hline
		CPACK\_PACKAGE\_VERSION\_MINOR & 包次要版本           & 项目次要版本  \\ \hline
		CPACK\_PACKAGE\_VERSION\_PATCH & 包补丁版本           & 项目补丁版本  \\ \hline
		CPACK\_GENERATOR               & 使用的CPack生成器列表 & N/A                    \\ \hline
		CPACK\_THREADS & 支持并行的线程数 & 1 \\ \hline
	\end{tabular}
\end{table}

包含CPack模块之前，必须对变量进行更改，否则使用默认值。这里通过一个示例来了解CPack的实际应用，将使用第4章，例6(chapter04/ex06\_pack)的示例。此示例构造为一个独立的项目，并不是根示例项目的一部分。它是一个常规项目，有两个子目录，分别名为executable和library。可执行目录的CMakeLists.txt文件如下所示:

\begin{lstlisting}[style=styleCMake]
add_executable(ch4_ex06_executable src/main.cpp)
target_compile_features(ch4_ex06_executable PRIVATE cxx_std_11)
target_link_libraries(
	ch4_ex06_executable PRIVATE ch4_ex06_library)
install(TARGETS ch4_ex06_executable)
\end{lstlisting}

库目录的CMakeLists.txt文件如下所示:

\begin{lstlisting}[style=styleCMake]
add_library(ch4_ex06_library STATIC src/lib.cpp)
target_compile_features(ch4_ex06_library PRIVATE cxx_std_11)
target_include_directories(ch4_ex06_library PUBLIC include)
set_target_properties(ch4_ex06_library PROPERTIES PUBLIC_HEADER
	include/chapter04/ex06/lib.hpp)
include(GNUInstallDirs)
# Defines the ${CMAKE_INSTALL_INCLUDEDIR} variable.
install(TARGETS ch4_ex06_library)
install (
	DIRECTORY ${PROJECT_SOURCE_DIR}/include/
	DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}
)
\end{lstlisting}

这些文件夹的CMakeLists.txt文件包含常规的、可安装的CMake目标，并没有声明关于CPack的内容。让我们看看顶层的CMakeLists.txt文件:

\begin{lstlisting}[style=styleCMake]
cmake_minimum_required(VERSION 3.21)
project(
	ch4_ex06_pack
	VERSION 1.0
	DESCRIPTION "Chapter 4 Example 06, Packaging with CPack"
	LANGUAGES CXX)
if(NOT PROJECT_IS_TOP_LEVEL)
	message(FATAL_ERROR "The chapter 4, ex06_pack project is
		intended to be a standalone, top-level project.
		Do not include this directory.")
endif()

add_subdirectory(executable)
add_subdirectory(library)

set(CPACK_PACKAGE_VENDOR "CBP Authors")
set(CPACK_GENERATOR "DEB;RPM;TBZ2")
set(CPACK_THREADS 0)
set(CPACK_DEBIAN_PACKAGE_MAINTAINER "CBP Authors")
include(CPack)
\end{lstlisting}

顶层CMakeLists.txt文件除了最后四行之外，几乎和一个普通的顶层CMakeLists.txt文件无异。它设置了三个与CPack相关的变量，然后包含了CPack模块，这四行足以提供基本的CPack支持。CPACK\_PACKAGE\_NAME和CPACK\_PACKAGE\_VERSION\_*变量没有让CPACK从顶层项目的名称和版本参数中进行推断。让我们配置这个项目，看看是否工作正常:

\begin{tcblisting}{commandshell={}}
cd chapter04/ex06_pack
cmake –S . -B build/
\end{tcblisting}

项目配置完成后，CpackConfig.cmake和CpackConfigSource.cmake文件将生成到build/CPack*目录下。看看他们是否存在:

\begin{tcblisting}{commandshell={}}
$ ls build/CPack*
build/CPackConfig.cmake build/CPackSourceConfig.cmake
\end{tcblisting}

这里，可以看到CPack配置文件是自动生成的。构建它，并尝试用CPack打包项目:

\begin{tcblisting}{commandshell={}}
cmake --build build/
cpack --config build/CPackConfig.cmake -B build/
\end{tcblisting}

参数\texttt{-{}-config}是CPack命令的主要输入。参数\texttt{-B}修改了默认的包目录，CPack将把它的工件写入该目录。看看CPack的输出:

\begin{tcblisting}{commandshell={}}
CPack: Create package using DEB
/*…*/
CPack: - package: /home/user/workspace/personal/CMake-Best-Practices/
chapter04/ex06_pack/build/ch4_ex06_pack-1.0-Linux.deb
generated.
CPack: Create package using RPM
/*…*/
CPack: - package: /home/user/workspace/personal/CMake-Best-Practices/
chapter04/ex06_pack/build/ch4_ex06_pack-1.0-Linux.rpm
generated.
CPack: Create package using TBZ2
/*…*/
CPack: - package: /home/user/workspace/personal/CMake-Best-Practices/
chapter04/ex06_pack/build/ch4_ex06_pack-1.0-Linux.tar.bz2 
generated.
\end{tcblisting}

这里，可以看到CPack使用DEB、RPM和TBZ2生成器生成了ch4\_ex06\_pack-1.0-Linux.deb, ch4\_ex06\_pack-1.0-Linux.rpm和cch4\_ex06\_pack-1.0-Linux.tar.bz2。尝试在Debian环境中安装生成的Debian包:

\begin{tcblisting}{commandshell={}}
sudo dpkg -i build/ch4_ex06_pack-1.0-Linux.deb
\end{tcblisting}

若安装无误，应该能够在命令行中直接使用ch4\_ex06\_executable:

\begin{tcblisting}{commandshell={}}
13:38 $ ch4_ex06_executable
Hello, world!
\end{tcblisting}

没问题!作为练习，请尝试安装RPM和tar.bz2包。

本节中，学习了如何使用CPack来打包我们的项目，这绝不是一个详尽的指南，CPack本身值得用几章来详细介绍。








