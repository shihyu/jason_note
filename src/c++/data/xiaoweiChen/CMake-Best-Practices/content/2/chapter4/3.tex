
上一节中，学习了如何使项目可安装，以便其他人可以通过在他们的系统上安装使用。但是有时候，交付工件是不够的。例如，若交付一个库，也必须很容易导入到一个项——特别是CMake项目。本节中，我们将学习如何使这个导入过程更容易(为其他CMake项目)。

若要导入的项目具有正确的配置文件，有很多导入库的方便方法。其中一个是使用\texttt{find\_package()}(将在第5章中讨论)。若有使用者在工作中使用CMake，只需要\texttt{find\_package(your\_project\_name)}就可以使用你的代码，他们肯定会很高兴。本节中，将学习如何生成所需的配置文件，使\texttt{find\_package()}在项目中工作。

CMake使用依赖的首选方式是通过包。包为基于CMake的构建系统传递依赖信息，包可以是Config-file包、Find-module包或pkg-config包。所有的包类型都可以通过\texttt{find\_package()}找到并使用。出于对篇幅和简单性的考虑，本节只介绍Config-file包。其余的方法大多是缺少配置文件的变通方法，所以最好将它们留在过去。让我们了解一下Config-file包。

\subsubsubsection{4.3.1\hspace{0.2cm}进入CMake包世界——Config-file包}

Config-file包基于包含包内容信息的配置文件。该信息指示包的内容位置，因此CMake读取该文件并使用该包。因此，只使用包配置文件就可以使用该包。

有两种类型的配置文件——包配置文件和可选的包版本文件，两个文件都必须有一个特定的命名。包配置文件可以命名为<ProjectName>Config.cmake或<projectname>-config.cmake，这取决于个人喜好。这两个符号将由CMake在\texttt{find\_package(ProjectName)}/\texttt{find\_package(projectname)}调用中选择。包配置文件的内容如下所示:

\begin{lstlisting}[style=styleCMake]
set(Foo_INCLUDE_DIRS ${PREFIX}/include/foo-1.2)
set(Foo_LIBRARIES ${PREFIX}/lib/foo-1.2/libfoo.a)
\end{lstlisting}

这里，\$\{PREFIX\}是项目的安装前缀，是一个变量，因为安装前缀可以根据系统的类型更改，也可以由用户修改。

与包配置文件类似，包版本文件可以命名为<ProjectName>ConfigVersion.cmake或<projectname>-config-version.cmake。CMake期望包配置和包版本文件出现在\texttt{find\_package(…)}的搜索路径中，可以在CMake的帮助下创建这些文件。搜索包时，\texttt{find\_package(…)}会查找<CMAKE\_PREFIX\_PATH>/cmake目录。我们的示例中，将把Config-file包配置文件放到这个文件夹中。

要创建config-file包，需要学习一些东西，比如导出目标和CmakePackageConfigHelpers模块。为了了解它，这里需要一个实际的例子。我们将使用第4章示例5，使其成为具有config-file包，这个例子位于chapter04/ex05\_config\_file\_package文件夹。来看看chapter04/ex05\_config\_file\_package目录下的CMakeLists.txt文件(注释和项目命令在空格处被省略;另外，与主题无关的行这里不会再提及):

\begin{lstlisting}[style=styleCMake]
include(GNUInstallDirs)
set(ch4_ex05_lib_INSTALL_CMAKEDIR cmake CACHE PATH
	"Installation directory for config-file package cmake files")
/*…*/
\end{lstlisting}

CMakeLists.txt文件非常类似于chapter4/ex02\_static，并支持config-file打包。第一行\texttt{include(GNUInstallDirs)}用于包含GNUInstallDirs模块。这提供了CMAKE\_INSTALL\_INCLUDEDIR变量，将在稍后使用。\texttt{set(ch4\_ex05\_lib\_INSTALL\_CMAKEDIR…)}是一个用户定义的变量，用于设置config-file打包配置文件的安装目录。这是一个相对路径，应该在\texttt{install(…)}指令中使用:

\begin{lstlisting}[style=styleCMake]
/*…*/
target_include_directories(ch4_ex05_lib PUBLIC
	$<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include>
)
target_compile_features(ch4_ex05_lib PUBLIC cxx_std_11)
/*…*/
\end{lstlisting}

\texttt{target\_include\_directories(…)}与通常使用的方式不同。因为在将目标导入到另一个项目时，不存在构建时包含路径，其使用生成器表达式来区分构建时包含目录和安装时包含目录。下面的一组命令使目标可安装:

\begin{lstlisting}[style=styleCMake]
/*…*/
install(TARGETS ch4_ex05_lib
	EXPORT ch4_ex05_lib_export
	INCLUDES DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}
)
install (
	DIRECTORY ${PROJECT_SOURCE_DIR}/include/
	DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}
)
/*…*/
\end{lstlisting}

\texttt{install(TARGETS...)}也和平常有一点不同，包含EXPORT参数，用于从给定的\texttt{install(…)}目标创建一个导出名称。然后，可以使用此导出名称导出这些目标。使用INCLUDES DESTINATION参数指定的路径，将用于填充导出目标的INTERFACE\_INCLUDE\_DIRECTORIES属性，并自动使用安装前缀路径作为前缀。这里，\texttt{install(DIRECTORY…)}用于安装目标的头文件，位于\$\{PROJECT\_SOURCE\_DIR\}/include/，在\$\{CMAKE\_INSTALL\_PREFIX\}/\$\{CMAKE\_INSTALL\_INCLUDEDIR\}目录中。\$\{CMAKE\_INSTALL\_INCLUDEDIR\}可以让使用者覆盖此安装的include目录。现在，可以根据前面例子中创建的导出名称创建一个导出文件:

\begin{lstlisting}[style=styleCMake]
/*…*/
install(EXPORT ch4_ex05_lib_export
	FILE ch4_ex05_lib-config.cmake
	NAMESPACE ch4_ex05_lib::
	DESTINATION ${ch4_ex05_lib_INSTALL_CMAKEDIR}
)
/*…*/
\end{lstlisting}

\texttt{install(EXPORT…)}是这个文件中最重要的一段代码，是执行实际目标导出的代码。其生成一个CMake文件，其中包含给定导出名称中的所有导出目标。\texttt{EXPOR}T参数接受现有的导出名称来进行导出，它引用的是ch4\_ex05\_lib\_export导出名称，我们在之前的\texttt{install(TARGETS…)}中创建的。FILE用于确定导出的文件名，并设置为ch4\_ex05\_lib-config.cmake。NAMESPACE用于为所有导出的目标添加命名空间前缀。这允许将所有导出的目标连接到通用的命名空间下，并避免与具有相似目标名称的包发生冲突。最后，DESTINATION确定生成导出文件的安装路径。设置为\$\{ch4\_ex05\_lib\_INSTALL\_CMAKEDIR\}以便\texttt{find\_package()}发现它。

\begin{tcolorbox}[colback=webgreen!5!white,colframe=webgreen!75!black,title=Note]
除了导出的目标之外，没有提供额外的内容，所以导出文件的名称是ch4\_ex05\_lib-config.cmake。它是此包所需的包配置文件名称。这样做是因为示例项目不需要满足任何依赖关系，可以按原样直接导入。若需要额外的操作，建议使用中间包配置文件来满足这些依赖关系，然后包含导出的文件。
\end{tcolorbox}

使用\texttt{install(EXPORT…)}，获得了ch4\_ex05\_lib-config.cmake文件。从而目标可以通过\texttt{find\_package(...)}来使用，要实现对\texttt{find\_package(…)}的完全支持，还需要一个步骤，即获取ch4\_ex05\_lib-config-version.cmake文件:

\begin{lstlisting}[style=styleCMake]
/*…*/
include(CMakePackageConfigHelpers)
write_basic_package_version_file(
	"ch4_ex05_lib-config-version.cmake"
	# Package compatibility strategy. SameMajorVersion is
	#	essentially 'semantic versioning'.
	COMPATIBILITY SameMajorVersion
)
install(FILES
	"${CMAKE_CURRENT_BINARY_DIR}/ch4_ex05_lib-config-version.cmake"
	DESTINATION "${ch4_ex05_lib_INSTALL_CMAKEDIR}"
)
/* end of the file */
\end{lstlisting}

最后几行中，可以找到生成和安装ch4\_ex05\_lib-config-version.cmake文件所需的代码。使用\texttt{include(CMakePackageConfigHelpers)}，导入CMakePackageConfigHelpers模块。这个模块提供了write\_basic\_package\_version\_file(…)函数，用于根据给定的参数自动生成包版本文件。第一个位置参数是输出的文件名。VERSION用于以major.minor.patch形式指定生成包的版本，允许write\_basic\_package\_version\_file从项目版本自动获取。COMPATIBILITY可以根据版本的值指定兼容性策略。SameMajorVersion表示此包与具有此包相同主版本值的任何版本兼容，其他可能的值为AnyNewerVersion、SameMinorVersion和ExactVersion。

现在，来测试一下这个方法是否有效。为了测试包配置，我们必须定期安装项目:

\begin{tcblisting}{commandshell={}}
cmake –S . -B ./build
cmake --build ./build
cmake --install ./build --prefix /tmp/install-test
\end{tcblisting}

\texttt{cmake -{}-install}的输出如下所示:

\begin{tcblisting}{commandshell={}}
/* … */
-- Installing: /tmp/install-test/cmake/ch4_ex05_lib-config.cmake
-- Installing: /tmp/install-test/cmake/ch4_ex05_lib-confignoconfig.
cmake
-- Installing: /tmp/install-test/cmake/ch4_ex05_lib-configversion.
cmake
/*…*/
\end{tcblisting}

可以看到包配置文件已经成功安装在/tmp/install-test/cmake目录下。我将检查这些文件内容的工作留给读者作为练习。为了使用这个包，再来看下chapter04/ex05\_consumer示例中的CMakeLists.txt文件:

\begin{lstlisting}[style=styleCMake]
if(NOT PROJECT_IS_TOP_LEVEL)
	message(FATAL_ERROR "The chapter 4, ex05_consumer project is
		intended to be a standalone, top-level project. Do not
			include this directory.")
endif()
find_package(ch4_ex05_lib 1 CONFIG REQUIRED)
add_executable(ch4_ex05_consumer src/main.cpp)
target_compile_features(ch4_ex05_consumer PRIVATE cxx_std_11)
target_link_libraries(
	ch4_ex05_consumer ch4_ex05_lib::ch4_ex05_lib)
\end{lstlisting}

前几行中，可以看到关于项目是否是顶层项目的验证。这个示例是一个外部应用程序，所以不应该是根示例项目的一部分。因此，可以保证用包导出的目标，而不是根项目的目标，根项目也不包括ex05\_consumer文件夹。接下来，有一个\texttt{find\_package(…)}，其中ch4\_ex05\_lib作为包名给出。还明确要求包的主版本为1；\texttt{find\_package(…)}只考虑\texttt{CONFIG}包和\texttt{find\_package(…)}中指定的包是必需的。在随后的行中，一个常规的可执行文件定义为ch4\_ex05\_consumer，它在ch4\_ex05\_lib命名空间(ch4\_ex05\_lib::ch4\_ex05\_lib)中链接到ch4\_ex05\_lib。ch4\_ex05\_lib::ch4\_ex05\_lib是我们在包中定义的实际目标。来看看源文件src/main.cpp:

\begin{lstlisting}[style=styleCXX]
#include <chapter04/ex05/lib.hpp>
int main(void){
	chapter04::ex05::greeter g;
	g.greet();
}
\end{lstlisting}

这是一个包含chapter04/ex05/lib.hpp的简单应用程序，创建了一个greeter类的实例，并调用了greet()函数。试着编译并运行这个应用程序:

\begin{tcblisting}{commandshell={}}
cd chapter04/ex05_consumer
cmake -S . -B build/ -DCMAKE_PREFIX_PATH:STRING=/tmp/install-test
cmake --build build/
./build/ch4_ex05_consumer
\end{tcblisting}

已经使用自定义前缀(/tmp/install-test)安装了包，所以可以通过设置CMAKE\_PREFIX\_PATH变量来表明这一点。这会导致\texttt{find\_package(…)}也会在/tmp/install-test中搜索包。对于默认的前缀安装，不需要此参数设置。若一切顺利的话，我们应该看到输出为Hello, world!: 

\begin{tcblisting}{commandshell={}}
./build/ch4_ex05_consumer
Hello, world!
\end{tcblisting}

这样，使用者就可以使用我们的配置文件。接下来，通过学习如何使用CPack进行打包来结束本节。



























