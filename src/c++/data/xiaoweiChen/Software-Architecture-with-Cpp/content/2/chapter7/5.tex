
CMake有内置程序，在分发构建的结果时，这些程序可以大有作为。本节将描述安装和导出实用程序，以及它们之间的区别。后面的小节将展示如何使用CPack打包代码，以及如何使用Conan进行打包。

安装和导出对于微服务本身并不是那么重要，但交付库供他人使用，就就有用了。

\subsubsubsection{7.5.1\hspace{0.2cm}安装}

如果编写或使用过Makefile，那么应该用过\texttt{make install}，并看到过项目的可交付成果是如何安装在OS目录或选择的另一个目录中的。如果正在使用make与CMake，使用本节的步骤将可以以相同的方式安装可交付成果。如果没有，仍然可以使用安装目标。除此之外，将有一种简单的方法来使用CPack根据安装命令来创建安装包。

如果在Linux系统上，通过调用下面的命令根据操作系统的惯例预先设置一些安装目录，可能是个不错的方式:

\begin{lstlisting}[style=styleCMake]
include(GNUInstallDirs)
\end{lstlisting}

这将使安装程序使用由bin、lib和其他类似目录组成的目录结构。这样的目录也可以手动设置一些CMake变量。

创建安装目标还包括几个步骤。首先，也是最重要的是定义想要安装的目标，在例子中如下所示:

\begin{lstlisting}[style=styleCMake]
install(
	TARGETS libcustomer customer
	EXPORT CustomerTargets
	LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
	ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}
	RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR})
\end{lstlisting}

这告诉CMake将本章前面定义的库和可执行文件公开为\texttt{CustomerTargets}，使用前面设置的目录。

如果计划将库的不同配置安装到不同的文件夹中，可以这样:

\begin{lstlisting}[style=styleCMake]
install(TARGETS libcustomer customer
	CONFIGURATIONS Debug
	# destinations for other components go here...
	RUNTIME DESTINATION Debug/bin)
	
install(TARGETS libcustomer customer
	CONFIGURATIONS Release
	# destinations for other components go here...
	RUNTIME DESTINATION Release/bin)
\end{lstlisting}

可以注意到，这里为可执行文件和库指定了目录，但没有为包含文件指定目录。这需要在另一个命令中提供:

\begin{lstlisting}[style=styleCMake]
install(DIRECTORY ${PROJECT_SOURCE_DIR}/include/
	DESTINATION include)
\end{lstlisting}

这意味着顶层\texttt{include}目录的内容将安装在安装根目录下的\texttt{include}目录中。第一个路径后的斜杠修复了一些路径问题，所以需要留意一下它。

现在有一系列的目标，需要生成一个文件，让另一个CMake项目可以了解其意图。这可以通过以下方式实现:

\begin{lstlisting}[style=styleCMake]
install(
	EXPORT CustomerTargets
	FILE CustomerTargets.cmake
	NAMESPACE domifair::
	DESTINATION ${CMAKE_INSTALL_LIBDIR}/cmake/Customer)
\end{lstlisting}

这个命令接受目标集，并创建CustomerTargets.cmake文件，将包含关于目标和要求的信息。每个目标都有一个名称空间前缀，例如：\texttt{customer}将变成\texttt{domifair::customer}。生成的文件将安装在安装树中的库文件夹子目录中。

为了让依赖项目使用CMake的\texttt{find\_package}找到相应的目标，需要提供一个CustomerConfig.cmake文件。如果目标没有依赖项，可以直接将前面的目标导出到该文件，而不是目标文件。否则，应该编写包含上述目标文件的配置文件。

例子中，想要重用一些CMake变量，所以需要创建一个模板，并使用\texttt{configure\_file}对其进行填充:

\begin{lstlisting}[style=styleCMake]
configure_file(${PROJECT_SOURCE_DIR}/cmake/CustomerConfig.cmake.in
	CustomerConfig.cmake @ONLY)
\end{lstlisting}

CustomerConfig.cmake.in文件将从处理依赖关系开始:

\begin{lstlisting}[style=styleCMake]
include(CMakeFindDependencyMacro)
find_dependency(cpprestsdk 2.10.18 REQUIRED)
\end{lstlisting}

\texttt{find\_dependency}宏是用于配置文件中的\texttt{find\_package}的包装器。虽然使用在conanfile.txt中定义C++ REST SDK 2.10.18的方式让Conan提供了相应的依赖，但这里需要再次指定这些依赖。生成的包可以在另一台机器上使用，因此也需要在那里安装依赖。如果想在目标机上使用Conan，可以按照如下步骤安装C++ REST SDK:

\begin{tcblisting}{commandshell={}}
conan install cpprestsdk/2.10.18
\end{tcblisting}

处理了依赖关系后，配置文件模板将包括之前创建的目标文件:

\begin{lstlisting}[style=styleCMake]
if(NOT TARGET domifair::@PROJECT_NAME@)
	include("${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@Targets.cmake")
endif()
\end{lstlisting}

\texttt{configure\_file}执行时，将用项目中定义的与\texttt{\$\{VARIABLES\}}匹配的内容替换所有的\texttt{@VARIABLES@}。这样，基于CustomerConfig.cmake.in文件模板，将创建一个CustomerConfig.cmake文件。

使用\texttt{find\_package}查找依赖项时，通常需要指定要查找的包的版本。为了在包中支持版本信息，必须创建CustomerConfigVersion.cmake文件。CMake提供了一个帮助函数，可以创建这个文件。可以这样使用:

\begin{lstlisting}[style=styleCMake]
include(CMakePackageConfigHelpers)
write_basic_package_version_file(
CustomerConfigVersion.cmake
VERSION ${PACKAGE_VERSION}
COMPATIBILITY AnyNewerVersion)
\end{lstlisting}

\texttt{PACKAGE\_VERSION}将根据在顶层CMakeLists.txt文件顶部调用\texttt{project}时，传递的\texttt{VERSION}参数来对其进行填充。

若是新的或相同的版本请求，\texttt{AnyNewerVersion COMPATIBILITY}表示包可以兼容。其他选项包括\texttt{SameMajorVersion}，\texttt{SameMinorVersion}和\texttt{ExactVersion}。

在创建了配置文件和配置版本文件后，就可以告诉CMake它们应该与二进制文件和目标文件一起进行安装:

\begin{lstlisting}[style=styleCMake]
install(FILES ${CMAKE_CURRENT_BINARY_DIR}/CustomerConfig.cmake
		${CMAKE_CURRENT_BINARY_DIR}/CustomerConfigVersion.cmake
	DESTINATION ${CMAKE_INSTALL_LIBDIR}/cmake/Customer)
\end{lstlisting}

最后一件事，这里应该为项目安装许可证。可以利用CMake的命令来安装文件，把它们放在文档目录中:

\begin{lstlisting}[style=styleCMake]
install(
	FILES ${PROJECT_SOURCE_DIR}/LICENSE
	DESTINATION ${CMAKE_INSTALL_DOCDIR})
\end{lstlisting}

要成功地在操作系统的根目录中创建安装目标，只需要知道这些。这里可能想了解如何将包安装到另一个目录，例如仅为当前用户安装。为此，需要在生成构建系统时，设置\texttt{CMAKE\_INSTALL\_PREFIX}。

注意，若不想安装到Unix树的根目录中，将需要为依赖项目提供一个安装目录的路径，可以通过\texttt{CMAKE\_PREFIX\_PATH}设置。

现在来了解一下重用构建内容的另一种方法。

\subsubsubsection{7.5.2\hspace{0.2cm}导出}

导出是一种将关于本地构建包的信息添加到CMake包注册表的技术。当目标在构建目录中直接可见时，即使没有安装这也很有用的。导出的一个常见的场景是在在本地进行构建，但在开发机器上使用它们。

从CMakeLists.txt文件中添加对这种机制的支持非常容易:

\begin{lstlisting}[style=styleCMake]
export(
	TARGETS libcustomer customer
	NAMESPACE domifair::
	FILE CustomerTargets.cmake)

set(CMAKE_EXPORT_PACKAGE_REGISTRY ON)
export(PACKAGE domifair)
\end{lstlisting}

CMake将创建类似于安装部分的目标文件，在提供的命名空间中定义库和可执行目标。自CMake 3.15起，包注册表默认禁用，所以需要通过设置适当的前置变量来启用。然后，可以通过导出包将关于目标的信息直接放入注册表中。

注意，现在有了一个目标文件，但没有匹配的配置文件。若目标依赖于外部库，必须在找到包之前找到它们。在我们的例子中，调用必须按以下方式排序:

\begin{lstlisting}[style=styleCMake]
find_package(cpprestsdk 2.10.18)
find_package(domifair)
\end{lstlisting}

首先，找到C++ REST SDK，然后寻找依赖于它的包。这就是开始输出目标文件所需要知道的全部信息。这比安装它们容易多了，不是吗？

现在，来了解第三种将目标暴露在外部的方法。

\subsubsubsection{7.5.3\hspace{0.2cm}CPack}

本节中，将描述如何使用CMake自带的打包工具CPack。CPack可以轻松地创建各种格式的包，从ZIP和TGZ压缩包到DEB和RPM包，甚至包括NSIS或一些OS X-specific的安装向导。当安装逻辑就位，集成该工具就不难了。现在就来展示如何使用CPack来打包项目。

首先，指定CPack在创建包时使用的变量:

\begin{lstlisting}[style=styleCMake]
set(CPACK_PACKAGE_VENDOR "Authors")
set(CPACK_PACKAGE_CONTACT "author@example.com")
set(CPACK_PACKAGE_DESCRIPTION_SUMMARY
	"Library and app for the Customer microservice")
\end{lstlisting}

需要手工提供一些信息，但是可以根据定义项目时指定的项目版本填充变量。还有更多的CPack变量，可以在本章末尾的扩展阅读部分的CPack链接中进行了解。其中一些是所有包生成器通用的，而一些是特定于相应生成器的。例如，若计划使用安装程序，可以设置以下两个变量:

\begin{lstlisting}[style=styleCMake]
set(CPACK_RESOURCE_FILE_LICENSE "${PROJECT_SOURCE_DIR}/LICENSE")
set(CPACK_RESOURCE_FILE_README "${PROJECT_SOURCE_DIR}/README.md")
\end{lstlisting}

设置好的变量后，就可以选择CPack使用的生成器了。先在\texttt{CPACK\_GENERATOR}中放入一些基本的代码，CPACK依赖于一个变量:

\begin{lstlisting}[style=styleCMake]
list(APPEND CPACK_GENERATOR TGZ ZIP)
\end{lstlisting}

这将使CPack根据本章前面定义的安装步骤，生成两种类型的压缩文件。

可以根据许多东西选择不同的包生成器，比如正在运行的机器上可用的工具。在Windows上构建时创建Windows安装程序，在Linux上构建时安装了适当的工具，则创建DEB或RPM包。如果是在Linux上运行，可以检查dpkg是否安装，如果安装了，则创建DEB包:

\begin{lstlisting}[style=styleCMake]
if(UNIX)
	find_program(DPKG_PROGRAM dpkg)
	if(DPKG_PROGRAM)
		list(APPEND CPACK_GENERATOR DEB)
		set(CPACK_DEBIAN_PACKAGE_DEPENDS "${CPACK_DEBIAN_PACKAGE_DEPENDS}
		libcpprest2.10 (>= 2.10.2-6)")
		set(CPACK_DEBIAN_PACKAGE_SHLIBDEPS ON)
	else()
		message(STATUS "dpkg not found - won't be able to create DEB
		packages")
	endif()
\end{lstlisting}

使用\texttt{CPACK\_DEBIAN\_PACKAGE\_DEPENDS}变量使DEB包要求首先安装C++ REST SDK。

对于RPM包，可以手动检查rpmbuild:

\begin{lstlisting}[style=styleCMake]
	find_program(RPMBUILD_PROGRAM rpmbuild)
	if(RPMBUILD_PROGRAM)
		list(APPEND CPACK_GENERATOR RPM)
		set(CPACK_RPM_PACKAGE_REQUIRES "${CPACK_RPM_PACKAGE_REQUIRES} 
		cpprest >= 2.10.2-6")
	else()
		message(STATUS "rpmbuild not found -
		 won't be able to create RPM packages")
	endif()
endif()
\end{lstlisting}

很优雅，对吧？

这些生成器提供了很多变量，如果需要比这里描述的这些基本需求更多的东西，可以查阅CMake文档。

最后，当涉及变量时——还可以使用它们来避免打包不需要的文件。可以通过以下方式实现:

\begin{lstlisting}[style=styleCMake]
set(CPACK_SOURCE_IGNORE_FILES /.git /dist /.*build.* /\\\\.DS_Store)
\end{lstlisting}

当所有的工具都准备好，就可以在CMakeLists中包括CPack了:

\begin{lstlisting}[style=styleCMake]
include(CPack)
\end{lstlisting}

记住，在最后一步总是这样做，因为CMake不会传播后续使用的变量到CPack中。

可以直接调用cpack或更长的形式运行，其会检查是否需要重新构建:\texttt{cmake -\,-build . -\,-target package}。如果只需要使用\texttt{-G}标志重新构建一种类型的包，则可以覆盖生成器。例如，\texttt{-G DEB}只构建DEB包，\texttt{-G WIX -C Release}打包一个发行版MSI可执行文件，或\texttt{-G DragNDrop}可以得到一个DMG安装程序。

接下来，让我们讨论一种更野蛮的方法(进行包的构建)。
