# 9.2 使用Fortran库构建C/C++项目

**NOTE**:*此示例代码可以在 https://github.com/dev-cafe/cmake-cookbook/tree/v1.0/chapter-9/recipe-02 中找到，其中有一个示例：一个是C++、C和Fortran的混例。该示例在CMake 3.5版(或更高版本)中是有效的，并且已经在GNU/Linux、macOS和Windows上进行过测试。*

第3章第4节，展示了如何检测Fortran编写的BLAS和LAPACK线性代数库，以及如何在C++代码中使用它们。这里，将重新讨论这个方式，但这次的角度有所不同：较少地关注检测外部库，会更深入地讨论混合C++和Fortran的方面，以及名称混乱的问题。

## 准备工作

本示例中，我们将重用第3章第4节源代码。虽然，我们不会修改源码或头文件，但我们会按照第7章“结构化项目”中，讨论的建议修改项目树结构，并得到以下源代码结构:

```shell
.
├── CMakeLists.txt
├── README.md
└── src
      ├── CMakeLists.txt
      ├── linear-algebra.cpp
      └── math
            ├── CMakeLists.txt
            ├── CxxBLAS.cpp
            ├── CxxBLAS.hpp
            ├── CxxLAPACK.cpp
            └── CxxLAPACK.hpp
```

这里，收集了BLAS和LAPACK的所有包装器，它们提供了`src/math`下的数学库了，主要程序为` linear-algebra.cpp`。因此，所有源都在`src`子目录下。我们还将CMake代码分割为三个`CMakeLists.txt`文件，现在来讨论这些文件。

## 具体实施

这个项目混合了C++(作为该示例的主程序语言)和C(封装Fortran子例程所需的语言)。在根目录下的`CMakeLists.txt`文件中，我们需要做以下操作:

1. 声明一个混合语言项目，并选择C++标准：

   ```cmake
   cmake_minimum_required(VERSION 3.5 FATAL_ERROR)
   
   project(recipe-02 LANGUAGES CXX C Fortran)
   
   set(CMAKE_CXX_STANDARD 11)
   set(CMAKE_CXX_EXTENSIONS OFF)
   set(CMAKE_CXX_STANDARD_REQUIRED ON)
   ```

2. 使用`GNUInstallDirs`模块来设置CMake将静态和动态库，以及可执行文件保存的标准目录。我们还指示CMake将Fortran编译的模块文件放在`modules`目录下:

   ```cmake
   include(GNUInstallDirs)
   set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY
   	${CMAKE_BINARY_DIR}/${CMAKE_INSTALL_LIBDIR})
   set(CMAKE_LIBRARY_OUTPUT_DIRECTORY
   	${CMAKE_BINARY_DIR}/${CMAKE_INSTALL_LIBDIR})
   set(CMAKE_RUNTIME_OUTPUT_DIRECTORY
   	${CMAKE_BINARY_DIR}/${CMAKE_INSTALL_BINDIR})
   set(CMAKE_Fortran_MODULE_DIRECTORY ${PROJECT_BINARY_DIR}/modules)
   ```

3. 然后，进入下一个子目录:

   ```cmake
   add_subdirectory(src)
   ```

子文件`src/CMakeLists.txt`添加了另一个目录`math`，其中包含线性代数包装器。在`src/math/CMakeLists.txt`中，我们需要以下操作:

1. 调用`find_package`来获取BLAS和LAPACK库的位置:

   ```cmake
   find_package(BLAS REQUIRED)
   find_package(LAPACK REQUIRED)
   ```

2. 包含`FortranCInterface.cmake`模块，并验证Fortran、C和C++编译器是否兼容:

   ```cmake
   include(FortranCInterface)
   FortranCInterface_VERIFY(CXX)
   ```

3. 我们还需要生成预处理器宏来处理BLAS和LAPACK子例程的名称问题。同样，`FortranCInterface`通过在当前构建目录中生成一个名为`fc_mangl.h`的头文件来提供协助:

   ```cmake
   FortranCInterface_HEADER(
     fc_mangle.h
     MACRO_NAMESPACE "FC_"
     SYMBOLS DSCAL DGESV
     )
   ```

4. 接下来，添加了一个库，其中包含BLAS和LAPACK包装器的源代码。我们还指定要找到头文件和库的目录。注意`PUBLIC`属性，它允许其他依赖于`math`的目标正确地获得它们的依赖关系:

   ```cmake
   add_library(math "")
   
   target_sources(math
     PRIVATE
       CxxBLAS.cpp
       CxxLAPACK.cpp
     )
     
   target_include_directories(math
     PUBLIC
       ${CMAKE_CURRENT_SOURCE_DIR}
       ${CMAKE_CURRENT_BINARY_DIR}
     )
   target_link_libraries(math
     PUBLIC
     	${LAPACK_LIBRARIES}
     )
   ```

回到`src/CMakeLists.txt`，我们最终添加了一个可执行目标，并将其链接到BLAS/LAPACK包装器的数学库:

```cmake
add_executable(linear-algebra "")

target_sources(linear-algebra
  PRIVATE
  	linear-algebra.cpp
  )

target_link_libraries(linear- algebra
  PRIVATE
  	math
  )
```

## 工作原理

使用`find_package`确定了要链接到的库。方法和之前一样，需要确保程序能够正确地调用它们定义的函数。第3章第4节中，我们面临的问题是编译器的名称符号混乱。我们使用`FortranCInterface`模块来检查所选的C和C++编译器与Fortran编译器的兼容性。我们还使用`FortranCInterface_HEADER`函数生成带有宏的头文件，以处理Fortran子例程的名称混乱。并通过以下代码实现:

```cmake
FortranCInterface_HEADER(
  fc_mangle.h
  MACRO_NAMESPACE "FC_"
  SYMBOLS DSCAL DGESV
)
```

这个命令将生成`fc_mangl.h`头文件，其中包含从Fortran编译器推断的名称混乱宏，并将其保存到当前二进制目录`CMAKE_CURRENT_BINARY_DIR`中。我们小心地将`CMAKE_CURRENT_BINARY_DIR`设置为数学目标的包含路径。生成的`fc_mangle.h`如下:

```c++
#ifndef FC_HEADER_INCLUDED
#define FC_HEADER_INCLUDED

/* Mangling for Fortran global symbols without underscores. */
#define FC_GLOBAL(name,NAME) name##_

/* Mangling for Fortran global symbols with underscores. */
#define FC_GLOBAL_(name,NAME) name##_

/* Mangling for Fortran module symbols without underscores. */
#define FC_MODULE(mod_name,name, mod_NAME,NAME) __##mod_name##_MOD_##name

/* Mangling for Fortran module symbols with underscores. */
#define FC_MODULE_(mod_name,name, mod_NAME,NAME) __##mod_name##_MOD_##name

/* Mangle some symbols automatically. */
#define DSCAL FC_GLOBAL(dscal, DSCAL)
#define DGESV FC_GLOBAL(dgesv, DGESV)
#endif
```

本例中的编译器使用下划线进行错误处理。由于Fortran不区分大小写，子例程可能以小写或大写出现，这就说明将这两种情况传递给宏的必要性。注意，CMake还将为隐藏在Fortran模块后面的符号生成宏。

**NOTE**:*现在，BLAS和LAPACK的许多实现都在Fortran子例程附带了一个C的包装层。这些包装器已经标准化，分别称为CBLAS和LAPACKE。*

由于已经将源组织成库目标和可执行目标，所以我们应该对目标的`PUBLIC`、`INTERFACE`和`PRIVATE`可见性属性的使用进行评论。与源文件一样，包括目录、编译定义和选项，当与`target_link_libraries`一起使用时，这些属性的含义是相同的:

* 使用`PRIVATE`属性，库将只链接到当前目标，而不链接到使用它的任何其他目标。
* 使用`INTERFACE`属性，库将只链接到使用当前目标作为依赖项的目标。
* 使用`PUBLIC`属性，库将被链接到当前目标，以及将其作为依赖项使用的任何其他目标。