# 10.4 安裝超級構建

**NOTE**:*此示例代碼可以在 https://github.com/dev-cafe/cmake-cookbook/tree/v1.0/chapter-10/recipe-04 中找到，其中有一個C++示例。該示例在CMake 3.6版(或更高版本)中是有效的，並且已經在GNU/Linux、macOS和Windows上進行過測試。*

我們的消息庫取得了巨大的成功，許多其他程序員都使用它，並且非常滿意。也希望在自己的項目中使用它，但是不確定如何正確地管理依賴關係。可以用自己的代碼附帶消息庫的源代碼，但是如果該庫已經安裝在系統上了應該怎麼做呢？第8章，展示了超級構建的場景，但是不確定如何安裝這樣的項目。本示例將帶您瞭解安裝超級構建的安裝細節。

## 準備工作

此示例將針對消息庫，構建一個簡單的可執行鏈接。項目佈局如下:

```shell
├── cmake
│    ├── install_hook.cmake.in
│    └── print_rpath.py
├── CMakeLists.txt
├── external
│    └── upstream
│        ├── CMakeLists.txt
│        └── message
│            └── CMakeLists.txt
└── src
    ├── CMakeLists.txt
    └── use_message.cpp
```

主`CMakeLists.txt`文件配合超級構建，`external`子目錄包含處理依賴項的CMake指令。`cmake`子目錄包含一個Python腳本和一個模板CMake腳本。這些將用於安裝方面的微調，CMake腳本首先進行配置，然後調用Python腳本打印`use_message`可執行文件的`RPATH`:

```python
import shlex
import subprocess
import sys

def main():
  patcher = sys.argv[1]
  elfobj = sys.argv[2]
  tools = {'patchelf': '--print-rpath', 'chrpath': '--list', 'otool': '-L'}
  if patcher not in tools.keys():
  raise RuntimeError('Unknown tool {}'.format(patcher))
  cmd = shlex.split('{:s} {:s} {:s}'.format(patcher, tools[patcher], elfobj))
  rpath = subprocess.run(
      cmd,
      bufsize=1,
      stdout=subprocess.PIPE,
      stderr=subprocess.PIPE,
      universal_newlines=True)
  print(rpath.stdout)

if __name__ == "__main__":
  main()
```

使用平臺原生工具可以輕鬆地打印`RPATH`，稍後我們將在本示例中討論這些工具。

最後，`src`子目錄包含項目的`CMakeLists.txt`和源文件。`use_message.cpp`源文件包含以下內容:

```c++
#include <cstdlib>
#include <iostream>

#ifdef USING_message
#include <message/Message.hpp>
void messaging()
{
  Message say_hello("Hello, World! From a client of yours!");
  std::cout << say_hello << std::endl;
  Message say_goodbye("Goodbye, World! From a client of yours!");
  std::cout << say_goodbye << std::endl;
}
#else
void messaging()
{
  std::cout << "Hello, World! From a client of yours!" << std::endl;
  std::cout << "Goodbye, World! From a client of yours!" << std::endl;
}
#endif

int main()
{
  messaging();
  return EXIT_SUCCESS;
}
```

## 具體實施

我們將從主`CMakeLists.txt`文件開始，它用來協調超級構建:

1. 與之前的示例相同。首先聲明一個C++11項目，設置了默認安裝路徑、構建類型、目標的輸出目錄，以及安裝樹中組件的佈局:

   ```cmake
   cmake_minimum_required(VERSION 3.6 FATAL_ERROR)
   
   project(recipe-04
     LANGUAGES CXX
     VERSION 1.0.0
     )
   
   # <<< General set up >>>
   
   set(CMAKE_CXX_STANDARD 11)
   set(CMAKE_CXX_EXTENSIONS OFF)
   set(CMAKE_CXX_STANDARD_REQUIRED ON)
   
   if(NOT CMAKE_BUILD_TYPE)
     set(CMAKE_BUILD_TYPE Release CACHE STRING "Build type" FORCE)
   endif()
   
   message(STATUS "Build type set to ${CMAKE_BUILD_TYPE}")
   
   message(STATUS "Project will be installed to ${CMAKE_INSTALL_PREFIX}")
   
   include(GNUInstallDirs)
   
   set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY
     ${PROJECT_BINARY_DIR}/${CMAKE_INSTALL_LIBDIR})
   set(CMAKE_LIBRARY_OUTPUT_DIRECTORY
     ${PROJECT_BINARY_DIR}/${CMAKE_INSTALL_LIBDIR})
   set(CMAKE_RUNTIME_OUTPUT_DIRECTORY
     ${PROJECT_BINARY_DIR}/${CMAKE_INSTALL_BINDIR})
   
   # Offer the user the choice of overriding the installation directories
   set(INSTALL_LIBDIR ${CMAKE_INSTALL_LIBDIR} CACHE PATH "Installation directory for libraries")
   set(INSTALL_BINDIR ${CMAKE_INSTALL_BINDIR} CACHE PATH "Installation directory for executables")
   set(INSTALL_INCLUDEDIR ${CMAKE_INSTALL_INCLUDEDIR} CACHE PATH "Installation directory for header files")
   if(WIN32 AND NOT CYGWIN)
     set(DEF_INSTALL_CMAKEDIR CMake)
   else()
     set(DEF_INSTALL_CMAKEDIR share/cmake/${PROJECT_NAME})
   endif()
   set(INSTALL_CMAKEDIR ${DEF_INSTALL_CMAKEDIR} CACHE PATH "Installation directory for CMake files")
   
   # Report to user
   foreach(p LIB BIN INCLUDE CMAKE)
     file(TO_NATIVE_PATH ${CMAKE_INSTALL_PREFIX}/${INSTALL_${p}DIR} _path )
     message(STATUS "Installing ${p} components to ${_path}")
     unset(_path)
   endforeach()
   ```

2. 設置了`EP_BASE`目錄屬性，這將為超構建中的子項目設置佈局。所有子項目都將在`CMAKE_BINARY_DIR`的子項目文件夾下生成:

   ```cmake
   set_property(DIRECTORY PROPERTY EP_BASE ${CMAKE_BINARY_DIR}/subprojects)
   ```

3. 然後，聲明`STAGED_INSTALL_PREFIX`變量。這個變量指向構建目錄下的`stage`子目錄，項目將在構建期間安裝在這裡。這是一種沙箱安裝過程，讓我們有機會檢查整個超級構建的佈局:

   ```cmake
   set(STAGED_INSTALL_PREFIX ${CMAKE_BINARY_DIR}/stage)
   message(STATUS "${PROJECT_NAME} staged install: ${STAGED_INSTALL_PREFIX}")
   ```

4. 添加`external/upstream`子目錄。其中包括使用CMake指令來管理我們的上游依賴關係，在我們的例子中，就是消息庫:

   ```cmake
   add_subdirectory(external/upstream)
   ```

5. 然後，包含` ExternalProject.cmake`標準模塊:

   ```cmake
   include(ExternalProject)
   ```

6. 將自己的項目作為外部項目添加，調用`ExternalProject_Add`命令。`SOURCE_DIR`用於指定源位於`src`子目錄中。我們會選擇適當的CMake參數來配置我們的項目。這裡，使用`STAGED_INSTALL_PREFIX`作為子項目的安裝目錄:

   ```cmake
   ExternalProject_Add(${PROJECT_NAME}_core
     DEPENDS
       message_external
     SOURCE_DIR
       ${CMAKE_CURRENT_SOURCE_DIR}/src
     CMAKE_ARGS
       -DCMAKE_INSTALL_PREFIX=${STAGED_INSTALL_PREFIX}
       -DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE}
       -DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}
       -DCMAKE_CXX_FLAGS=${CMAKE_CXX_FLAGS}
       -DCMAKE_CXX_STANDARD=${CMAKE_CXX_STANDARD}
       -DCMAKE_CXX_EXTENSIONS=${CMAKE_CXX_EXTENSIONS}
       -DCMAKE_CXX_STANDARD_REQUIRED=${CMAKE_CXX_STANDARD_REQUIRED}
       -Dmessage_DIR=${message_DIR}
     CMAKE_CACHE_ARGS
       -DCMAKE_PREFIX_PATH:PATH=${CMAKE_PREFIX_PATH}
     BUILD_ALWAYS
       1
     )
   ```

7. 現在，為`use_message`添加一個測試，並由`recipe-04_core`構建。這將運行`use_message`可執行文件的安裝，即位於構建樹中的安裝:

   ```cmake
   enable_testing()
   
   add_test(
     NAME
     	check_use_message
     COMMAND
     	${STAGED_INSTALL_PREFIX}/${INSTALL_BINDIR}/use_message
     )
   ```

8. 最後，可以聲明安裝規則。因為所需要的東西都已經安裝在暫存區域中，我們只要將暫存區域的內容複製到安裝目錄即可:

   ```cmake
   install(
     DIRECTORY
     	${STAGED_INSTALL_PREFIX}/
     DESTINATION
     	.
     USE_SOURCE_PERMISSIONS
     )
   ```

9. 使用`SCRIPT`參數聲明一個附加的安裝規則。CMake腳本的` install_hook.cmake `將被執行，但只在GNU/Linux和macOS上執行。這個腳本將打印已安裝的可執行文件的`RPATH`，並運行它。我們將在下一節詳細地討論這個問題：

   ```cmake
   if(UNIX)
     set(PRINT_SCRIPT "${CMAKE_CURRENT_LIST_DIR}/cmake/print_rpath.py")
     configure_file(cmake/install_hook.cmake.in install_hook.cmake @ONLY)
     install(
       SCRIPT
         ${CMAKE_CURRENT_BINARY_DIR}/install_hook.cmake
       )
   endif()
   ```

`-Dmessage_DIR=${message_DIR}`已作為CMake參數傳遞給項目，這將正確設置消息庫依賴項的位置。`message_DIR`的值在`external/upstream/message`目錄下的`CMakeLists.txt`文件中定義。這個文件處理依賴於消息庫，讓我們看看是如何處理的:

1. 首先，搜索並找到包。用戶可能已經在系統的某個地方安裝了，並在配置時傳遞了`message_DIR`:

   ```cmake
   find_package(message 1 CONFIG QUIET)
   ```

2. 如果找到了消息庫，我們將向用戶報告目標的位置和版本，並添加一個虛擬的`message_external`目標。這裡，需要虛擬目標來正確處理超構建的依賴關係:

   ```cmake
   if(message_FOUND)
     get_property(_loc TARGET message::message-shared PROPERTY LOCATION)
     message(STATUS "Found message: ${_loc} (found version ${message_VERSION})")
     add_library(message_external INTERFACE) # dummy
   ```

3. 如果沒有找到這個庫，我們將把它添加為一個外部項目，從在線Git存儲庫下載它，然後編譯它。安裝路徑、構建類型和安裝目錄佈局都是由主`CMakeLists.txt`文件設置，C++編譯器和標誌也是如此。項目將安裝到`STAGED_INSTALL_PREFIX`下，然後進行測試:

   ```cmake
   else()
     include(ExternalProject)
     message(STATUS "Suitable message could not be located, Building message instead.")
     ExternalProject_Add(message_external
       GIT_REPOSITORY
         https://github.com/dev-cafe/message.git
       GIT_TAG
         master
       UPDATE_COMMAND
         ""
       CMAKE_ARGS
         -DCMAKE_INSTALL_PREFIX=${STAGED_INSTALL_PREFIX}
         -DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE}
         -DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}
       CMAKE_CACHE_ARGS
         -DCMAKE_CXX_FLAGS:STRING=${CMAKE_CXX_FLAGS}
       TEST_AFTER_INSTALL
         1
       DOWNLOAD_NO_PROGRESS
         1
       LOG_CONFIGURE
         1
       LOG_BUILD
         1
       LOG_INSTALL
         1
     )
   ```

4. 最後，將`message_DIR`目錄進行設置，為指向新構建的` messageConfig.cmake`文件指明安裝路徑。注意，這些路徑被保存到`CMakeCache`中:

   ```cmake
     if(WIN32 AND NOT CYGWIN)
       set(DEF_message_DIR ${STAGED_INSTALL_PREFIX}/CMake)
     else()
       set(DEF_message_DIR ${STAGED_INSTALL_PREFIX}/share/cmake/message)
     endif()
     file(TO_NATIVE_PATH "${DEF_message_DIR}" DEF_message_DIR)
     set(message_DIR ${DEF_message_DIR}
       CACHE PATH "Path to internally built messageConfig.cmake" FORCE)
   endif()
   ```

我們終於準備好編譯我們自己的項目，併成功地將其鏈接到消息庫(無論是系統上已有的消息庫，還是新構建的消息庫)。由於這是一個超級構建，`src`子目錄下的代碼是一個完全獨立的CMake項目:

1. 聲明一個C++11項目：

   ```cmake
   cmake_minimum_required(VERSION 3.6 FATAL_ERROR)
   
   project(recipe-04_core
     LANGUAGES CXX
     )
   
   set(CMAKE_CXX_STANDARD 11)
   set(CMAKE_CXX_EXTENSIONS OFF)
   set(CMAKE_CXX_STANDARD_REQUIRED ON)
   
   include(GNUInstallDirs)
   
   set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY
     ${CMAKE_BINARY_DIR}/${CMAKE_INSTALL_LIBDIR})
   set(CMAKE_LIBRARY_OUTPUT_DIRECTORY
     ${CMAKE_BINARY_DIR}/${CMAKE_INSTALL_LIBDIR})
   set(CMAKE_RUNTIME_OUTPUT_DIRECTORY
     ${CMAKE_BINARY_DIR}/${CMAKE_INSTALL_BINDIR})
   ```

2. 嘗試找到消息庫。超級構建中，正確設置`message_DIR`:

   ```cmake
   find_package(message 1 CONFIG REQUIRED)
   get_property(_loc TARGET message::message-shared PROPERTY LOCATION)
   message(STATUS "Found message: ${_loc} (found version ${message_VERSION})")
   ```

3. 添加可執行目標`use_message`，該目標由`use_message.cpp`源文件創建，並連接到`message::message-shared`目標:

   ```cmake
   add_executable(use_message use_message.cpp)
   
   target_link_libraries(use_message
     PUBLIC
     	message::message-shared
     )
   ```

4. 為`use_message`設置目標屬性。再次對`RPATH`進行設置:

   ```cmake
   # Prepare RPATH
   file(RELATIVE_PATH _rel ${CMAKE_INSTALL_PREFIX}/${CMAKE_INSTALL_BINDIR} ${CMAKE_INSTALL_PREFIX})
   if(APPLE)
     set(_rpath "@loader_path/${_rel}")
   else()
     set(_rpath "\$ORIGIN/${_rel}")
   endif()
   file(TO_NATIVE_PATH "${_rpath}/${CMAKE_INSTALL_LIBDIR}" use_message_RPATH)
   
   set_target_properties(use_message
     PROPERTIES
       MACOSX_RPATH ON
       SKIP_BUILD_RPATH OFF
       BUILD_WITH_INSTALL_RPATH OFF
       INSTALL_RPATH "${use_message_RPATH}"
       INSTALL_RPATH_USE_LINK_PATH ON
     )
   ```

5. 最後，為`use_message`目標設置了安裝規則:

   ```cmake
   install(
     TARGETS
     	use_message
     RUNTIME
       DESTINATION ${CMAKE_INSTALL_BINDIR}
       COMPONENT bin
     )
   ```

現在瞧瞧CMake腳本模板`install_hook.cmake.in`的內容：

1. CMake腳本在我們的主項目範圍之外執行，因此沒有定義變量或目標的概念。因此，需要設置變量來保存已安裝的`use_message`可執行文件的完整路徑。注意使用`@INSTALL_BINDIR@`，它將由`configure_file`解析：

   ```cmake
   set(_executable ${CMAKE_INSTALL_PREFIX}/@INSTALL_BINDIR@/use_message)
   ```

2. 需要找到平臺本機可執行工具，使用該工具打印已安裝的可執行文件的`RPATH`。我們將搜索`chrpath`、`patchelf`和`otool`。當找到已安裝的程序時，向用戶提供有用的狀態信息，並且退出搜索：

   ```cmake
   set(_patcher)
   list(APPEND _patchers chrpath patchelf otool)
   foreach(p IN LISTS _patchers)
     find_program(${p}_FOUND
       NAMES
         ${p}
       )
     if(${p}_FOUND)
       set(_patcher ${p})
       message(STATUS "ELF patching tool ${_patcher} FOUND")
       break()
     endif()
   endforeach()
   ```

3. 檢查`_patcher`變量是否為空，這意味著PatchELF工具是否可用。當為空時，我們要進行的操作將會失敗，所以會發出一個致命錯誤，提醒用戶需要安裝PatchELF工具:

   ```cmake
   if(NOT _patcher)
   	message(FATAL_ERROR "ELF patching tool NOT FOUND!\nPlease install one of chrpath, patchelf or otool")
   ```

4. 當PatchELF工具找到了，則繼續。我們調用Python腳本`print_rpath.py`，將`_executable`變量作為參數傳遞給`execute_process`：

   ```cmake
     find_package(PythonInterp REQUIRED QUIET)
     execute_process(
       COMMAND
         ${PYTHON_EXECUTABLE} @PRINT_SCRIPT@ "${_patcher}"
       "${_executable}"
       RESULT_VARIABLE _res
       OUTPUT_VARIABLE _out
       ERROR_VARIABLE _err
       OUTPUT_STRIP_TRAILING_WHITESPACE
       )
   ```

5. 檢查`_res`變量的返回代碼。如果執行成功，將打印`_out`變量中捕獲的標準輸出流。否則，打印退出前捕獲的標準輸出和錯誤流:

   ```cmake
     if(_res EQUAL 0)
       message(STATUS "RPATH for ${_executable} is ${_out}")
     else()
       message(STATUS "Something went wrong!")
       message(STATUS "Standard output from print_rpath.py: ${_out}")
       message(STATUS "Standard error from print_rpath.py: ${_err}")
       message(FATAL_ERROR "${_patcher} could NOT obtain RPATH for ${_executable}")
     endif()
   endif()
   ```

6. 再使用`execute_process`來運行已安裝的`use_message`可執行目標:

   ```cmake
   execute_process(
     COMMAND ${_executable}
     RESULT_VARIABLE _res
     OUTPUT_VARIABLE _out
     ERROR_VARIABLE _err
     OUTPUT_STRIP_TRAILING_WHITESPACE
     )
   ```

7. 最後，向用戶報告`execute_process`的結果:

   ```cmake
   if(_res EQUAL 0)
     message(STATUS "Running ${_executable}:\n ${_out}")
   else()
     message(STATUS "Something went wrong!")
     message(STATUS "Standard output from running ${_executable}:\n ${_out}")
     message(STATUS "Standard error from running ${_executable}:\n ${_err}")
     message(FATAL_ERROR "Something went wrong with ${_executable}")
   endif()
   ```

## 工作原理

CMake工具箱中，超級構建是非常有用的模式。它通過將複雜的項目劃分為更小、更容易管理的子項目來管理它們。此外，可以使用CMake作為構建項目的包管理器。CMake可以搜索依賴項，如果在系統上找不到依賴項，則重新構建它們。這裡需要三個`CMakeLists.txt`文件：

* 主`CMakeLists.txt`文件包含項目和依賴項共享的設置，還包括我們自己的項目(作為外部項目)。本例中，我們選擇的名稱為`${PROJECT_NAME}_core`；也就是`recipe-04_core`，因為項目名稱`recipe-04`用於超級構建。
* 外部`CMakeLists.txt`文件將嘗試查找上游依賴項，並在導入目標和構建目標之間進行切換，這取決於是否找到了依賴項。對於每個依賴項，最好有單獨的子目錄，其中包含一個`CMakeLists.txt`文件。
* 最後，我們項目的`CMakeLists.txt`文件，可以構建一個獨立的CMake項目。在原則上，我們可以自己配置和構建它，而不需要超級構建提供的依賴關係管理工具。

當對消息庫的依賴關係未得到滿足時，將首先考慮超級構建:

```shell
$ mkdir -p build
$ cd build
$ cmake -DCMAKE_INSTALL_PREFIX=$HOME/Software/recipe-04 ..
```

讓CMake查找庫，這是我們得到的輸出:

```shell
-- The CXX compiler identification is GNU 7.3.0
-- Check for working CXX compiler: /nix/store/gqg2vrcq7krqi9rrl6pphvsg81sb8pjw-gcc-wrapper-7.3.0/bin/g++
-- Check for working CXX compiler: /nix/store/gqg2vrcq7krqi9rrl6pphvsg81sb8pjw-gcc-wrapper-7.3.0/bin/g++ -- works
-- Detecting CXX compiler ABI info
-- Detecting CXX compiler ABI info - done
-- Detecting CXX compile features
-- Detecting CXX compile features - done
-- Project will be installed to /home/roberto/Software/recipe-04
-- Build type set to Release
-- Installing LIB components to /home/roberto/Software/recipe-04/lib64
-- Installing BIN components to /home/roberto/Software/recipe-04/bin
-- Installing INCLUDE components to /home/roberto/Software/recipe-04/include
-- Installing CMAKE components to /home/roberto/Software/recipe-04/share/cmake/recipe-04
-- recipe-04 staged install: /home/roberto/Workspace/robertodr/cmake-cookbook/chapter-10/recipe-04/cxx-example/build/stage
-- Suitable message could not be located, Building message instead.
-- Configuring done
-- Generating done
-- Build files have been written to: /home/roberto/Workspace/robertodr/cmake-cookbook/chapter-10/recipe-04/cxx-example/build
```

根據指令，CMake報告如下:

* 安裝將分階段進入構建樹。分階段安裝是對實際安裝過程進行沙箱化的一種方法。作為開發人員，這對於在運行安裝命令之前檢查所有庫、可執行程序和文件是否安裝在正確的位置非常有用。對於用戶來說，可在構建目錄中給出了相同的結構。這樣，即使沒有運行正確的安裝，我們的項目也可以立即使用。
* 系統上沒有找到合適的消息庫。然後，CMake將運行在構建項目之前構建庫所提供的命令，以滿足這種依賴性。

如果庫已經位於系統的已知位置，我們可以將`-Dmessage_DIR`選項傳遞給CMake:

```shell
$ cmake -DCMAKE_INSTALL_PREFIX=$HOME/Software/use_message -Dmessage_DIR=$HOME/Software/message/share/cmake/message ..
```

事實上，這個庫已經找到並導入。我們對自己的項目進行建造操作:

```shell
-- The CXX compiler identification is GNU 7.3.0
-- Check for working CXX compiler: /nix/store/gqg2vrcq7krqi9rrl6pphvsg81sb8pjw-gcc-wrapper-7.3.0/bin/g++
-- Check for working CXX compiler: /nix/store/gqg2vrcq7krqi9rrl6pphvsg81sb8pjw-gcc-wrapper-7.3.0/bin/g++ -- works
-- Detecting CXX compiler ABI info
-- Detecting CXX compiler ABI info - done
-- Detecting CXX compile features
-- Detecting CXX compile features - done
-- Project will be installed to /home/roberto/Software/recipe-04
-- Build type set to Release
-- Installing LIB components to /home/roberto/Software/recipe-04/lib64
-- Installing BIN components to /home/roberto/Software/recipe-04/bin
-- Installing INCLUDE components to /home/roberto/Software/recipe-04/include
-- Installing CMAKE components to /home/roberto/Software/recipe-04/share/cmake/recipe-04
-- recipe-04 staged install: /home/roberto/Workspace/robertodr/cmake-cookbook/chapter-10/recipe-04/cxx-example/build/stage
-- Checking for one of the modules 'uuid'
-- Found message: /home/roberto/Software/message/lib64/libmessage.so.1 (found version 1.0.0)
-- Configuring done
-- Generating done
-- Build files have been written to: /home/roberto/Workspace/robertodr/cmake-cookbook/chapter-10/recipe-04/cxx-example/build
```

項目的最終安裝規則是，將安裝文件複製到`CMAKE_INSTALL_PREFIX`:

```cmake
install(
  DIRECTORY
  	${STAGED_INSTALL_PREFIX}/
  DESTINATION
  	.
  USE_SOURCE_PERMISSIONS
  )
```

注意使用`.`而不是絕對路徑`${CMAKE_INSTALL_PREFIX}`，這樣CPack工具就可以正確理解該規則。CPack的用法將在第11章中介紹。

`recipe-04_core`項目構建一個簡單的可執行目標，該目標鏈接到消息動態庫。正如本章前幾節所討論，為了讓可執行文件正確運行，需要正確設置`RPATH`。本章的第1節展示了，如何在CMake的幫助下實現這一點，同樣的模式在`CMakeLists.txt`中被重用，用於創建`use_message`的可執行目標:

```cmake
file(RELATIVE_PATH _rel ${CMAKE_INSTALL_PREFIX}/${CMAKE_INSTALL_BINDIR} ${CMAKE_INSTALL_PREFIX})
if(APPLE)
  set(_rpath "@loader_path/${_rel}")
else()
  set(_rpath "\$ORIGIN/${_rel}")
endif()
file(TO_NATIVE_PATH "${_rpath}/${CMAKE_INSTALL_LIBDIR}" use_message_RPATH)

set_target_properties(use_message
  PROPERTIES
    MACOSX_RPATH ON
    SKIP_BUILD_RPATH OFF
    BUILD_WITH_INSTALL_RPATH OFF
    INSTALL_RPATH "${use_message_RPATH}"
    INSTALL_RPATH_USE_LINK_PATH ON
  )
```

為了檢查這是否合適，可以使用本機工具打印已安裝的可執行文件的`RPATH`。我們將對該工具的調用，封裝到Python腳本中，並將其進一步封裝到CMake腳本中。最後，使用`SCRIPT`關鍵字將CMake腳本作為安裝規則調用:

```cmake
if(UNIX)
  set(PRINT_SCRIPT "${CMAKE_CURRENT_LIST_DIR}/cmake/print_rpath.py")
  configure_file(cmake/install_hook.cmake.in install_hook.cmake @ONLY)
  install(
    SCRIPT
      ${CMAKE_CURRENT_BINARY_DIR}/install_hook.cmake
    )
endif()
```

腳本是在安裝最後進行執行:

```shell
$ cmake --build build --target install
```

GNU/Linux系統上，我們將看到以下輸出:

```shell
Install the project...
-- Install configuration: "Release"
-- Installing: /home/roberto/Software/recipe-04/.
-- Installing: /home/roberto/Software/recipe-04/./lib64
-- Installing: /home/roberto/Software/recipe-04/./lib64/libmessage.so
-- Installing: /home/roberto/Software/recipe-04/./lib64/libmessage_s.a
-- Installing: /home/roberto/Software/recipe-04/./lib64/libmessage.so.1
-- Installing: /home/roberto/Software/recipe-04/./include
-- Installing: /home/roberto/Software/recipe-04/./include/message
-- Installing: /home/roberto/Software/recipe-04/./include/message/Message.hpp
-- Installing: /home/roberto/Software/recipe-04/./include/message/messageExport.h
-- Installing: /home/roberto/Software/recipe-04/./share
-- Installing: /home/roberto/Software/recipe-04/./share/cmake
-- Installing: /home/roberto/Software/recipe-04/./share/cmake/message
-- Installing: /home/roberto/Software/recipe-04/./share/cmake/message/messageTargets-release.cmake
-- Installing: /home/roberto/Software/recipe-04/./share/cmake/message/messageConfigVersion.cmake
-- Installing: /home/roberto/Software/recipe-04/./share/cmake/message/messageConfig.cmake
-- Installing: /home/roberto/Software/recipe-04/./share/cmake/message/messageTargets.cmake
-- Installing: /home/roberto/Software/recipe-04/./bin
-- Installing: /home/roberto/Software/recipe-04/./bin/hello-world_wAR
-- Installing: /home/roberto/Software/recipe-04/./bin/use_message
-- Installing: /home/roberto/Software/recipe-04/./bin/hello-world_wDSO
-- ELF patching tool chrpath FOUND
-- RPATH for /home/roberto/Software/recipe-04/bin/use_message is /home/roberto/Software/recipe-04/bin/use_message: RUNPATH=$ORIGIN/../lib64:/home/roberto/Workspace/robertodr/cmake-cookbook/chapter-10/recipe-04/cxx-example/build/stage/lib64:/nix/store/di389pfcw2krnmh8nmkn55d1rnzmba37-CMake-Cookbook/lib64:/nix/store/di389pfcw2krnmh8nmkn55d1rnzmba37-CMake-Cookbook/lib:/nix/store/mjs2b8mmid86lvbzibzdlz8w5yrjgcnf-util-linux-2.31.1/lib:/nix/store/2kcrj1ksd2a14bm5sky182fv2xwfhfap-glibc-2.26-131/lib:/nix/store/4zd34747fz0ggzzasy4icgn3lmy89pra-gcc-7.3.0-lib/lib
-- Running /home/roberto/Software/recipe-04/bin/use_message:
This is my very nice message:
Hello, World! From a client of yours!
...and here is its UUID: a8014bf7-5dfa-45e2-8408-12e9a5941825
This is my very nice message:
Goodbye, World! From a client of yours!
...and here is its UUID: ac971ef4-7606-460f-9144-1ad96f713647
```

**NOTE**:*我們建議使用的工具是PatchELF (https://nixos.org/patchelf.html )、chrpath (https://linux.die.net/man/1/chrpath )和otool (http://www.manpagez.com/man/1/otool/ )。第一種方法適用於GNU/Linux和macOS，而chrpath和otool分別適用於GNU/Linux和macOS。*



