# 7.9 組織Fortran項目

**NOTE**:*此示例代碼可以在 https://github.com/dev-cafe/cmake-cookbook/tree/v1.0/chapter-7/recipe-09 中找到，其中有一個Fortran示例。該示例在CMake 3.5版(或更高版本)中是有效的，並且已經在GNU/Linux、macOS和Windows上進行過測試。*

我們來討論如何構造和組織Fortran項目，原因有二:

1. 現在，仍然有很多Fortran項目，特別是在數字軟件中(有關通用Fortran軟件項目的更全面列表，請參見http://fortranwiki.org/fortran/show/Libraries )。
2. 對於不使用CMake的項目，Fortran 90(以及更高版本)可能更難構建，因為Fortran模塊強制執行編譯順序。換句話說，對於手工編寫的Makefile，通常需要為Fortran模塊文件編寫依賴掃描程序。

正如我們在本示例中所示，現代CMake允許我們以非常緊湊和模塊化的方式配置和構建項目。作為一個例子，我們將使用前兩個示例中的基本元胞自動機，現在將其移植到Fortran。

## 準備工作

文件樹結構與前兩個示例非常相似。我們用Fortran源代碼替換了C++，現在就沒有頭文件了:

```shell
.
├── CMakeLists.txt
├── external
│    ├── CMakeLists.txt
│    ├── conversion.f90
│    └── README.md
├── src
│    ├── CMakeLists.txt
│    ├── evolution
│    │    ├── ancestors.f90
│    │    ├── CMakeLists.txt
│    │    ├── empty.f90
│    │    └── evolution.f90
│    ├── initial
│    │    ├── CMakeLists.txt
│    │    └── initial.f90
│    ├── io
│    │    ├── CMakeLists.txt
│    │    └── io.f90
│    ├── main.f90
│    └── parser
│        ├── CMakeLists.txt
│        └── parser.f90
└── tests
    ├── CMakeLists.txt
    └── test.f90
```

主程序在` src/main.f90`中:

```fortran
program example

  use parser, only: get_arg_as_int
  use conversion, only: binary_representation
  use initial, only: initial_distribution
  use io, only: print_row
  use evolution, only: evolve
  
  implicit none
  
  integer :: num_steps
  integer :: length
  integer :: rule_decimal
  integer :: rule_binary(8)
  integer, allocatable :: row(:)
  integer :: step
  
  ! parse arguments
  num_steps = get_arg_as_int(1)
  length = get_arg_as_int(2)
  rule_decimal = get_arg_as_int(3)
  
  ! print information about parameters
  print *, "number of steps: ", num_steps
  print *, "length: ", length
  print *, "rule: ", rule_decimal
  
  ! obtain binary representation for the rule
  rule_binary = binary_representation(rule_decimal)
  
  ! create initial distribution
  allocate(row(length))
  call initial_distribution(row)
  
  ! print initial configuration
  call print_row(row)
  
  ! the system evolves, print each step
  do step = 1, num_steps
    call evolve(row, rule_binary)
    call print_row(row)
  end do
  
  deallocate(row)
end program
```

與前面的示例一樣，我們已經將conversion模塊放入`external/conversion.f90`中：

```fortran
module conversion

  implicit none
  public binary_representation
  private
  
contains

  pure function binary_representation(n_decimal)
    integer, intent(in) :: n_decimal
    integer :: binary_representation(8)
    integer :: pos
    integer :: n
    
    binary_representation = 0
    pos = 8
    n = n_decimal
    do while (n > 0)
      binary_representation(pos) = mod(n, 2)
      n = (n - binary_representation(pos))/2
      pos = pos - 1
    end do
  end function
  
end module
```

evolution庫分成三個文件，大部分在`src/evolution/evolution.f90`中:

```fortran
module evolution

  implicit none
  public evolve
  private
  
contains

  subroutine not_visible()
    ! no-op call to demonstrate private/public visibility
    call empty_subroutine_no_interface()
  end subroutine
  
  pure subroutine evolve(row, rule_binary)
    use ancestors, only: compute_ancestors
    
    integer, intent(inout) :: row(:)
    integer, intent(in) :: rule_binary(8)
    integer :: i
    integer :: left, center, right
    integer :: ancestry
    integer, allocatable :: new_row(:)
    
    allocate(new_row(size(row)))
    
    do i = 1, size(row)
      left = i - 1
      center = i
      right = i + 1
      
      if (left < 1) left = left + size(row)
      if (right > size(row)) right = right - size(row)
      
      ancestry = compute_ancestors(row, left, center, right)
      new_row(i) = rule_binary(ancestry)
    end do
    
    row = new_row
    deallocate(new_row)
    
  end subroutine
  
end module
```

祖先計算是在`src/evolution/ancestors.f90 `：

```fortran
module ancestors

  implicit none
  public compute_ancestors
  private
  
  contains
  pure integer function compute_ancestors(row, left, center, right) result(i)
    integer, intent(in) :: row(:)
    integer, intent(in) :: left, center, right
    
    i = 4*row(left) + 2*row(center) + 1*row(right)
    i = 8 - i
  end function
end module
```

還有一個“空”模塊在` src/evolution/empty.f90 `中：

```fortran
module empty

  implicit none
  public empty_subroutine
  private
  
contains

  subroutine empty_subroutine()
  end subroutine
  
end module

subroutine 
empty_subroutine_no_interface()
  use empty, only: empty_subroutine
  call empty_subroutine()
end subroutine
```

啟動條件的代碼位於`src/initial/initial.f90`：

```fortran
module initial

  implicit none
  public initial_distribution
  private
  
contains

	pure subroutine initial_distribution(row)
    integer, intent(out) :: row(:)
    
    row = 0
    row(size(row)/2) = 1
	end subroutine
	
end module
```

` src/io/io.f90`包含一個打印輸出：

```fortran
module io

  implicit none
  public print_row
  private
  
  contains
  
  subroutine print_row(row)
    integer, intent(in) :: row(:)
    character(size(row)) :: line
    integer :: i
    
    do i = 1, size(row)
      if (row(i) == 1) then
      	line(i:i) = '*'
      else
      	line(i:i) = ' '
      end if
    end do
    
    print *, line
  end subroutine
  
end module
```

`src/parser/parser.f90`用於解析命令行參數：

```fortran
module parser

  implicit none
  public get_arg_as_int
  private
  
  contains
  
  integer function get_arg_as_int(n) result(i)
    integer, intent(in) :: n
    character(len=32) :: arg
    
    call get_command_argument(n, arg)
    read(arg , *) i
  end function
end module
```

最後，使用`tests/test.f90`對上面的實現進行測試：

```fortran
program test

  use evolution, only: evolve
  
  implicit none
  
  integer :: row(9)
  integer :: expected_result(9)
  integer :: rule_binary(8)
  integer :: i
  
  ! test rule 90
  row = (/0, 1, 0, 1, 0, 1, 0, 1, 0/)
  rule_binary = (/0, 1, 0, 1, 1, 0, 1, 0/)
  call evolve(row, rule_binary)
  expected_result = (/1, 0, 0, 0, 0, 0, 0, 0, 1/)
  do i = 1, 9
  	if (row(i) /= expected_result(i)) then
  		print *, 'ERROR: test for rule 90 failed'
  		call exit(1)
  	end if
  end do
  
  ! test rule 222
  row = (/0, 0, 0, 0, 1, 0, 0, 0, 0/)
  rule_binary = (/1, 1, 0, 1, 1, 1, 1, 0/)
  call evolve(row, rule_binary)
  expected_result = (/0, 0, 0, 1, 1, 1, 0, 0, 0/)
  do i = 1, 9
  	if (row(i) /= expected_result(i)) then
  		print *, 'ERROR: test for rule 222 failed'
  		call exit(1)
  	end if
  end do
end program
```

## 具體實施

1. 主`CMakeLists.txt`類似於第7節，我們只是將CXX換成Fortran，去掉C++11的要求:

   ```cmake
   cmake_minimum_required(VERSION 3.5 FATAL_ERROR)
   
   project(recipe-09 LANGUAGES Fortran)
   
   include(GNUInstallDirs)
   set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY
   ${CMAKE_BINARY_DIR}/${CMAKE_INSTALL_LIBDIR})
   set(CMAKE_LIBRARY_OUTPUT_DIRECTORY
   ${CMAKE_BINARY_DIR}/${CMAKE_INSTALL_LIBDIR})
   set(CMAKE_RUNTIME_OUTPUT_DIRECTORY
   ${CMAKE_BINARY_DIR}/${CMAKE_INSTALL_BINDIR})
   
   # defines targets and sources
   add_subdirectory(src)
   
   # contains an "external" library we will link to
   add_subdirectory(external)
   
   # enable testing and define tests
   enable_testing()
   add_subdirectory(tests)
   ```

2. 目標和源在`src/CMakeLists.txt`中定義(conversion目標除外):

   ```cmake
   add_executable(automata main.f90)
   
   add_subdirectory(evolution)
   add_subdirectory(initial)
   add_subdirectory(io)
   add_subdirectory(parser)
   
   target_link_libraries(automata
     PRIVATE
       conversion
       evolution
       initial
       io
       parser
     )
   ```

3. conversion庫在`external/CMakeLists.txt`中定義:

   ```cmake
   add_library(conversion "")
   
   target_sources(conversion
     PUBLIC
     	${CMAKE_CURRENT_LIST_DIR}/conversion.f90
     )
   ```

4. `src/CMakeLists.txt`文件添加了更多的子目錄，這些子目錄又包含`CMakeLists.txt`文件。它們在結構上都是相似的，例如：`src/initial/CMakeLists.txt`包含以下內容:

   ```cmake
   add_library(initial "")
   
   target_sources(initial
     PUBLIC
     	${CMAKE_CURRENT_LIST_DIR}/initial.f90
     )
   ```

5. 有個例外的是`src/evolution/CMakeLists.txt`中的evolution庫，我們將其分為三個源文件:

   ```cmake
   add_library(evolution "")
   
   target_sources(evolution
     PRIVATE
     	empty.f90
     PUBLIC
     	${CMAKE_CURRENT_LIST_DIR}/ancestors.f90
     	${CMAKE_CURRENT_LIST_DIR}/evolution.f90
     )
   ```

6. 單元測試在`tests/CMakeLists.txt`中註冊:

   ```cmake
   add_executable(fortran_test test.f90)
   
   target_link_libraries(fortran_test evolution)
   
   add_test(
     NAME
     	test_evolution
     COMMAND
     	$<TARGET_FILE:fortran_test>
     )
   ```

7. 配置和構建項目，將產生以下輸出:

   ```shell
   $ mkdir -p build
   $ cd build
   $ cmake ..
   $ cmake --build .
   Scanning dependencies of target conversion
   [ 4%] Building Fortran object external/CMakeFiles/conversion.dir/conversion.f90.o
   [ 8%] Linking Fortran static library ../lib64/libconversion.a
   [ 8%] Built target conversion
   Scanning dependencies of target evolution
   [ 12%] Building Fortran object src/evolution/CMakeFiles/evolution.dir/ancestors.f90.o
   [ 16%] Building Fortran object src/evolution/CMakeFiles/evolution.dir/empty.f90.o
   [ 20%] Building Fortran object src/evolution/CMakeFiles/evolution.dir/evolution.f90.o
   [ 25%] Linking Fortran static library ../../lib64/libevolution.a
   [ 25%] Built target evolution
   Scanning dependencies of target initial
   [ 29%] Building Fortran object src/initial/CMakeFiles/initial.dir/initial.f90.o
   [ 33%] Linking Fortran static library ../../lib64/libinitial.a
   [ 33%] Built target initial
   Scanning dependencies of target io
   [ 37%] Building Fortran object src/io/CMakeFiles/io.dir/io.f90.o
   [ 41%] Linking Fortran static library ../../lib64/libio.a
   [ 41%] Built target io
   Scanning dependencies of target parser
   [ 45%] Building Fortran object src/parser/CMakeFiles/parser.dir/parser.f90.o
   [ 50%] Linking Fortran static library ../../lib64/libparser.a
   [ 50%] Built target parser
   Scanning dependencies of target example
   [ 54%] Building Fortran object src/CMakeFiles/example.dir/__/external/conversion.f90.o
   [ 58%] Building Fortran object src/CMakeFiles/example.dir/evolution/ancestors.f90.o
   [ 62%] Building Fortran object src/CMakeFiles/example.dir/evolution/evolution.f90.o
   [ 66%] Building Fortran object src/CMakeFiles/example.dir/initial/initial.f90.o
   [ 70%] Building Fortran object src/CMakeFiles/example.dir/io/io.f90.o
   [ 75%] Building Fortran object src/CMakeFiles/example.dir/parser/parser.f90.o
   [ 79%] Building Fortran object src/CMakeFiles/example.dir/main.f90.o
   [ 83%] Linking Fortran executable ../bin/example
   [ 83%] Built target example
   Scanning dependencies of target fortran_test
   [ 87%] Building Fortran object tests/CMakeFiles/fortran_test.dir/__/src/evolution/ancestors.f90.o
   [ 91%] Building Fortran object tests/CMakeFiles/fortran_test.dir/__/src/evolution/evolution.f90.o
   [ 95%] Building Fortran object tests/CMakeFiles/fortran_test.dir/test.f90.o
   [100%] Linking Fortran executable
   ```

8. 最後，運行單元測試：

   ```shell
   $ ctest
   
   Running tests...
   Start 1: test_evolution
   1/1 Test #1: test_evolution ................... Passed 0.00 sec
   
   100% tests passed, 0 tests failed out of 1
   ```

## 工作原理

第7節中使用`add_subdirectory`限制範圍，將從下往上討論CMake結構，從定義每個庫的單個`CMakeLists.txt`文件開始，比如`src/evolution/CMakeLists.txt`:

```cmake
add_library(evolution "")
target_sources(evolution
  PRIVATE
  	empty.f90
  PUBLIC
    ${CMAKE_CURRENT_LIST_DIR}/ancestors.f90
    ${CMAKE_CURRENT_LIST_DIR}/evolution.f90
  )
```

這些獨立的`CMakeLists.txt`文件定義了源文件的庫，遵循與前兩個示例相同的方式：開發或維護人員可以對其中文件分而治之。

首先用`add_library`定義庫名，然後定義它的源和包含目錄，以及它們的目標可見性。這種情況下，因為它們的模塊接口是在庫之外訪問，所以`ancestors.f90`和` evolution.f90`都是`PUBLIC`，而模塊接口` empty.f90 `不能在文件之外訪問，因此將其標記為`PRIVATE`。

向上移動一層，庫在`src/CMakeLists.txt`中封裝：

```cmake
add_executable(automata main.f90)

add_subdirectory(evolution)
add_subdirectory(initial)
add_subdirectory(io)
add_subdirectory(parser)

target_link_libraries(automata
  PRIVATE
    conversion
    evolution
    initial
    io
    parser
  )
```

這個文件在主`CMakeLists.txt`中被引用。這意味著我們使用`CMakeLists.txt`文件(使用`add_subdirectory`添加)構建項目。正如第7節中討論的，使用`add_subdirectory`限制範圍，這種方法可以擴展到更大型的項目，而不需要在多個目錄之間的全局變量中攜帶源文件列表，還可以隔離範圍和名稱空間。

將這個Fortran示例與C++版本(第7節)進行比較，我們可以注意到，在Fortran的情況下，相對的CMake工作量比較小；我們不需要使用`target_include_directory`，因為沒有頭文件，接口是通過生成的Fortran模塊文件進行通信。另外，我們既不需要擔心`target_sources`中列出的源文件的順序，也不需要在庫之間強制執行任何顯式依賴關係。CMake能夠從源文件依賴項推斷Fortran模塊依賴項。使用`target_sources`與`PRIVATE`和`PUBLIC`資源結合使用，以緊湊和健壯的方式表示接口。

## 更多信息

這個示例中，我們沒有指定應該放置Fortran模塊文件的目錄，並且保持了這個透明。模塊文件的位置可以通過設置`CMAKE_Fortran_MODULE_DIRECTORY`變量來指定。注意，也可以將其設置為`Fortran_MODULE_DIRECTORY`，從而實現更好的控制。詳細可見：https://cmake.org/cmake/help/v3.5/prop_tgt/Fortran_MODULE_DIRECTORY.html 