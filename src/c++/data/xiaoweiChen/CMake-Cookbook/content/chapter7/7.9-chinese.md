# 7.9 组织Fortran项目

**NOTE**:*此示例代码可以在 https://github.com/dev-cafe/cmake-cookbook/tree/v1.0/chapter-7/recipe-09 中找到，其中有一个Fortran示例。该示例在CMake 3.5版(或更高版本)中是有效的，并且已经在GNU/Linux、macOS和Windows上进行过测试。*

我们来讨论如何构造和组织Fortran项目，原因有二:

1. 现在，仍然有很多Fortran项目，特别是在数字软件中(有关通用Fortran软件项目的更全面列表，请参见http://fortranwiki.org/fortran/show/Libraries )。
2. 对于不使用CMake的项目，Fortran 90(以及更高版本)可能更难构建，因为Fortran模块强制执行编译顺序。换句话说，对于手工编写的Makefile，通常需要为Fortran模块文件编写依赖扫描程序。

正如我们在本示例中所示，现代CMake允许我们以非常紧凑和模块化的方式配置和构建项目。作为一个例子，我们将使用前两个示例中的基本元胞自动机，现在将其移植到Fortran。

## 准备工作

文件树结构与前两个示例非常相似。我们用Fortran源代码替换了C++，现在就没有头文件了:

```shell
.
├── CMakeLists.txt
├── external
│    ├── CMakeLists.txt
│    ├── conversion.f90
│    └── README.md
├── src
│    ├── CMakeLists.txt
│    ├── evolution
│    │    ├── ancestors.f90
│    │    ├── CMakeLists.txt
│    │    ├── empty.f90
│    │    └── evolution.f90
│    ├── initial
│    │    ├── CMakeLists.txt
│    │    └── initial.f90
│    ├── io
│    │    ├── CMakeLists.txt
│    │    └── io.f90
│    ├── main.f90
│    └── parser
│        ├── CMakeLists.txt
│        └── parser.f90
└── tests
    ├── CMakeLists.txt
    └── test.f90
```

主程序在` src/main.f90`中:

```fortran
program example

  use parser, only: get_arg_as_int
  use conversion, only: binary_representation
  use initial, only: initial_distribution
  use io, only: print_row
  use evolution, only: evolve
  
  implicit none
  
  integer :: num_steps
  integer :: length
  integer :: rule_decimal
  integer :: rule_binary(8)
  integer, allocatable :: row(:)
  integer :: step
  
  ! parse arguments
  num_steps = get_arg_as_int(1)
  length = get_arg_as_int(2)
  rule_decimal = get_arg_as_int(3)
  
  ! print information about parameters
  print *, "number of steps: ", num_steps
  print *, "length: ", length
  print *, "rule: ", rule_decimal
  
  ! obtain binary representation for the rule
  rule_binary = binary_representation(rule_decimal)
  
  ! create initial distribution
  allocate(row(length))
  call initial_distribution(row)
  
  ! print initial configuration
  call print_row(row)
  
  ! the system evolves, print each step
  do step = 1, num_steps
    call evolve(row, rule_binary)
    call print_row(row)
  end do
  
  deallocate(row)
end program
```

与前面的示例一样，我们已经将conversion模块放入`external/conversion.f90`中：

```fortran
module conversion

  implicit none
  public binary_representation
  private
  
contains

  pure function binary_representation(n_decimal)
    integer, intent(in) :: n_decimal
    integer :: binary_representation(8)
    integer :: pos
    integer :: n
    
    binary_representation = 0
    pos = 8
    n = n_decimal
    do while (n > 0)
      binary_representation(pos) = mod(n, 2)
      n = (n - binary_representation(pos))/2
      pos = pos - 1
    end do
  end function
  
end module
```

evolution库分成三个文件，大部分在`src/evolution/evolution.f90`中:

```fortran
module evolution

  implicit none
  public evolve
  private
  
contains

  subroutine not_visible()
    ! no-op call to demonstrate private/public visibility
    call empty_subroutine_no_interface()
  end subroutine
  
  pure subroutine evolve(row, rule_binary)
    use ancestors, only: compute_ancestors
    
    integer, intent(inout) :: row(:)
    integer, intent(in) :: rule_binary(8)
    integer :: i
    integer :: left, center, right
    integer :: ancestry
    integer, allocatable :: new_row(:)
    
    allocate(new_row(size(row)))
    
    do i = 1, size(row)
      left = i - 1
      center = i
      right = i + 1
      
      if (left < 1) left = left + size(row)
      if (right > size(row)) right = right - size(row)
      
      ancestry = compute_ancestors(row, left, center, right)
      new_row(i) = rule_binary(ancestry)
    end do
    
    row = new_row
    deallocate(new_row)
    
  end subroutine
  
end module
```

祖先计算是在`src/evolution/ancestors.f90 `：

```fortran
module ancestors

  implicit none
  public compute_ancestors
  private
  
  contains
  pure integer function compute_ancestors(row, left, center, right) result(i)
    integer, intent(in) :: row(:)
    integer, intent(in) :: left, center, right
    
    i = 4*row(left) + 2*row(center) + 1*row(right)
    i = 8 - i
  end function
end module
```

还有一个“空”模块在` src/evolution/empty.f90 `中：

```fortran
module empty

  implicit none
  public empty_subroutine
  private
  
contains

  subroutine empty_subroutine()
  end subroutine
  
end module

subroutine 
empty_subroutine_no_interface()
  use empty, only: empty_subroutine
  call empty_subroutine()
end subroutine
```

启动条件的代码位于`src/initial/initial.f90`：

```fortran
module initial

  implicit none
  public initial_distribution
  private
  
contains

	pure subroutine initial_distribution(row)
    integer, intent(out) :: row(:)
    
    row = 0
    row(size(row)/2) = 1
	end subroutine
	
end module
```

` src/io/io.f90`包含一个打印输出：

```fortran
module io

  implicit none
  public print_row
  private
  
  contains
  
  subroutine print_row(row)
    integer, intent(in) :: row(:)
    character(size(row)) :: line
    integer :: i
    
    do i = 1, size(row)
      if (row(i) == 1) then
      	line(i:i) = '*'
      else
      	line(i:i) = ' '
      end if
    end do
    
    print *, line
  end subroutine
  
end module
```

`src/parser/parser.f90`用于解析命令行参数：

```fortran
module parser

  implicit none
  public get_arg_as_int
  private
  
  contains
  
  integer function get_arg_as_int(n) result(i)
    integer, intent(in) :: n
    character(len=32) :: arg
    
    call get_command_argument(n, arg)
    read(arg , *) i
  end function
end module
```

最后，使用`tests/test.f90`对上面的实现进行测试：

```fortran
program test

  use evolution, only: evolve
  
  implicit none
  
  integer :: row(9)
  integer :: expected_result(9)
  integer :: rule_binary(8)
  integer :: i
  
  ! test rule 90
  row = (/0, 1, 0, 1, 0, 1, 0, 1, 0/)
  rule_binary = (/0, 1, 0, 1, 1, 0, 1, 0/)
  call evolve(row, rule_binary)
  expected_result = (/1, 0, 0, 0, 0, 0, 0, 0, 1/)
  do i = 1, 9
  	if (row(i) /= expected_result(i)) then
  		print *, 'ERROR: test for rule 90 failed'
  		call exit(1)
  	end if
  end do
  
  ! test rule 222
  row = (/0, 0, 0, 0, 1, 0, 0, 0, 0/)
  rule_binary = (/1, 1, 0, 1, 1, 1, 1, 0/)
  call evolve(row, rule_binary)
  expected_result = (/0, 0, 0, 1, 1, 1, 0, 0, 0/)
  do i = 1, 9
  	if (row(i) /= expected_result(i)) then
  		print *, 'ERROR: test for rule 222 failed'
  		call exit(1)
  	end if
  end do
end program
```

## 具体实施

1. 主`CMakeLists.txt`类似于第7节，我们只是将CXX换成Fortran，去掉C++11的要求:

   ```cmake
   cmake_minimum_required(VERSION 3.5 FATAL_ERROR)
   
   project(recipe-09 LANGUAGES Fortran)
   
   include(GNUInstallDirs)
   set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY
   ${CMAKE_BINARY_DIR}/${CMAKE_INSTALL_LIBDIR})
   set(CMAKE_LIBRARY_OUTPUT_DIRECTORY
   ${CMAKE_BINARY_DIR}/${CMAKE_INSTALL_LIBDIR})
   set(CMAKE_RUNTIME_OUTPUT_DIRECTORY
   ${CMAKE_BINARY_DIR}/${CMAKE_INSTALL_BINDIR})
   
   # defines targets and sources
   add_subdirectory(src)
   
   # contains an "external" library we will link to
   add_subdirectory(external)
   
   # enable testing and define tests
   enable_testing()
   add_subdirectory(tests)
   ```

2. 目标和源在`src/CMakeLists.txt`中定义(conversion目标除外):

   ```cmake
   add_executable(automata main.f90)
   
   add_subdirectory(evolution)
   add_subdirectory(initial)
   add_subdirectory(io)
   add_subdirectory(parser)
   
   target_link_libraries(automata
     PRIVATE
       conversion
       evolution
       initial
       io
       parser
     )
   ```

3. conversion库在`external/CMakeLists.txt`中定义:

   ```cmake
   add_library(conversion "")
   
   target_sources(conversion
     PUBLIC
     	${CMAKE_CURRENT_LIST_DIR}/conversion.f90
     )
   ```

4. `src/CMakeLists.txt`文件添加了更多的子目录，这些子目录又包含`CMakeLists.txt`文件。它们在结构上都是相似的，例如：`src/initial/CMakeLists.txt`包含以下内容:

   ```cmake
   add_library(initial "")
   
   target_sources(initial
     PUBLIC
     	${CMAKE_CURRENT_LIST_DIR}/initial.f90
     )
   ```

5. 有个例外的是`src/evolution/CMakeLists.txt`中的evolution库，我们将其分为三个源文件:

   ```cmake
   add_library(evolution "")
   
   target_sources(evolution
     PRIVATE
     	empty.f90
     PUBLIC
     	${CMAKE_CURRENT_LIST_DIR}/ancestors.f90
     	${CMAKE_CURRENT_LIST_DIR}/evolution.f90
     )
   ```

6. 单元测试在`tests/CMakeLists.txt`中注册:

   ```cmake
   add_executable(fortran_test test.f90)
   
   target_link_libraries(fortran_test evolution)
   
   add_test(
     NAME
     	test_evolution
     COMMAND
     	$<TARGET_FILE:fortran_test>
     )
   ```

7. 配置和构建项目，将产生以下输出:

   ```shell
   $ mkdir -p build
   $ cd build
   $ cmake ..
   $ cmake --build .
   Scanning dependencies of target conversion
   [ 4%] Building Fortran object external/CMakeFiles/conversion.dir/conversion.f90.o
   [ 8%] Linking Fortran static library ../lib64/libconversion.a
   [ 8%] Built target conversion
   Scanning dependencies of target evolution
   [ 12%] Building Fortran object src/evolution/CMakeFiles/evolution.dir/ancestors.f90.o
   [ 16%] Building Fortran object src/evolution/CMakeFiles/evolution.dir/empty.f90.o
   [ 20%] Building Fortran object src/evolution/CMakeFiles/evolution.dir/evolution.f90.o
   [ 25%] Linking Fortran static library ../../lib64/libevolution.a
   [ 25%] Built target evolution
   Scanning dependencies of target initial
   [ 29%] Building Fortran object src/initial/CMakeFiles/initial.dir/initial.f90.o
   [ 33%] Linking Fortran static library ../../lib64/libinitial.a
   [ 33%] Built target initial
   Scanning dependencies of target io
   [ 37%] Building Fortran object src/io/CMakeFiles/io.dir/io.f90.o
   [ 41%] Linking Fortran static library ../../lib64/libio.a
   [ 41%] Built target io
   Scanning dependencies of target parser
   [ 45%] Building Fortran object src/parser/CMakeFiles/parser.dir/parser.f90.o
   [ 50%] Linking Fortran static library ../../lib64/libparser.a
   [ 50%] Built target parser
   Scanning dependencies of target example
   [ 54%] Building Fortran object src/CMakeFiles/example.dir/__/external/conversion.f90.o
   [ 58%] Building Fortran object src/CMakeFiles/example.dir/evolution/ancestors.f90.o
   [ 62%] Building Fortran object src/CMakeFiles/example.dir/evolution/evolution.f90.o
   [ 66%] Building Fortran object src/CMakeFiles/example.dir/initial/initial.f90.o
   [ 70%] Building Fortran object src/CMakeFiles/example.dir/io/io.f90.o
   [ 75%] Building Fortran object src/CMakeFiles/example.dir/parser/parser.f90.o
   [ 79%] Building Fortran object src/CMakeFiles/example.dir/main.f90.o
   [ 83%] Linking Fortran executable ../bin/example
   [ 83%] Built target example
   Scanning dependencies of target fortran_test
   [ 87%] Building Fortran object tests/CMakeFiles/fortran_test.dir/__/src/evolution/ancestors.f90.o
   [ 91%] Building Fortran object tests/CMakeFiles/fortran_test.dir/__/src/evolution/evolution.f90.o
   [ 95%] Building Fortran object tests/CMakeFiles/fortran_test.dir/test.f90.o
   [100%] Linking Fortran executable
   ```

8. 最后，运行单元测试：

   ```shell
   $ ctest
   
   Running tests...
   Start 1: test_evolution
   1/1 Test #1: test_evolution ................... Passed 0.00 sec
   
   100% tests passed, 0 tests failed out of 1
   ```

## 工作原理

第7节中使用`add_subdirectory`限制范围，将从下往上讨论CMake结构，从定义每个库的单个`CMakeLists.txt`文件开始，比如`src/evolution/CMakeLists.txt`:

```cmake
add_library(evolution "")
target_sources(evolution
  PRIVATE
  	empty.f90
  PUBLIC
    ${CMAKE_CURRENT_LIST_DIR}/ancestors.f90
    ${CMAKE_CURRENT_LIST_DIR}/evolution.f90
  )
```

这些独立的`CMakeLists.txt`文件定义了源文件的库，遵循与前两个示例相同的方式：开发或维护人员可以对其中文件分而治之。

首先用`add_library`定义库名，然后定义它的源和包含目录，以及它们的目标可见性。这种情况下，因为它们的模块接口是在库之外访问，所以`ancestors.f90`和` evolution.f90`都是`PUBLIC`，而模块接口` empty.f90 `不能在文件之外访问，因此将其标记为`PRIVATE`。

向上移动一层，库在`src/CMakeLists.txt`中封装：

```cmake
add_executable(automata main.f90)

add_subdirectory(evolution)
add_subdirectory(initial)
add_subdirectory(io)
add_subdirectory(parser)

target_link_libraries(automata
  PRIVATE
    conversion
    evolution
    initial
    io
    parser
  )
```

这个文件在主`CMakeLists.txt`中被引用。这意味着我们使用`CMakeLists.txt`文件(使用`add_subdirectory`添加)构建项目。正如第7节中讨论的，使用`add_subdirectory`限制范围，这种方法可以扩展到更大型的项目，而不需要在多个目录之间的全局变量中携带源文件列表，还可以隔离范围和名称空间。

将这个Fortran示例与C++版本(第7节)进行比较，我们可以注意到，在Fortran的情况下，相对的CMake工作量比较小；我们不需要使用`target_include_directory`，因为没有头文件，接口是通过生成的Fortran模块文件进行通信。另外，我们既不需要担心`target_sources`中列出的源文件的顺序，也不需要在库之间强制执行任何显式依赖关系。CMake能够从源文件依赖项推断Fortran模块依赖项。使用`target_sources`与`PRIVATE`和`PUBLIC`资源结合使用，以紧凑和健壮的方式表示接口。

## 更多信息

这个示例中，我们没有指定应该放置Fortran模块文件的目录，并且保持了这个透明。模块文件的位置可以通过设置`CMAKE_Fortran_MODULE_DIRECTORY`变量来指定。注意，也可以将其设置为`Fortran_MODULE_DIRECTORY`，从而实现更好的控制。详细可见：https://cmake.org/cmake/help/v3.5/prop_tgt/Fortran_MODULE_DIRECTORY.html 