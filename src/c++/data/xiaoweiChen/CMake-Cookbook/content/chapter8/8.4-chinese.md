# 8.4 使用超級構建管理依賴項:Ⅲ.Google Test框架

**NOTE**:*此示例代碼可以在 https://github.com/dev-cafe/cmake-cookbook/tree/v1.0/chapter-8/recipe-04 中找到，其中有一個C++示例。該示例在CMake 3.11版(或更高版本)中是有效的，並且已經在GNU/Linux、macOS和Windows上進行過測試。在庫中也有一個例子可以在CMake 3.5下使用。*

第4章第3節中，我們使用Google Test框架實現單元測試，並在配置時使用`FetchContent`模塊獲取Google Test源(自CMake 3.11開始可用)。本章中，我們將重新討論這個方法，較少關注測試方面，並更深入地研究`FetchContent`。它提供了一個簡單通用的模塊，可以在配置時組裝項目依賴項。對於3.11以下的CMake，我們還將討論如何在配置時使用`ExternalProject_Add`模擬`FetchContent`。

## 準備工作

這個示例中，我們將複用第4章第3節的源碼，構建`main.cpp`、`sum_integer.cpp`和`sum_integers.hpp`和`test.cpp`。我們將在配置時使用`FetchContent`或`ExternalProject_Add`下載所有必需的Google Test源，在此示例中，只關注在配置時獲取依賴項，而不是實際的源代碼及其單元測試。

## 具體實施

這個示例中，我們只關注如何獲取Google Test源來構建`gtest_main`，並鏈接到Google Test庫。關於這個目標如何用於測試示例源的討論，請讀者參考第4章第3節:

1. 首先包括`FetchContent`模塊，它將提供需要的聲明、查詢和填充依賴項函數:

   ```cmake
   include(FetchContent)
   ```

2. 然後，聲明內容——名稱、存儲庫位置和要獲取的精確版本:

   ```cmake
   FetchContent_Declare(
     googletest
     GIT_REPOSITORY https://github.com/google/googletest.git
     GIT_TAG release-1.8.0
   )
   ```

3. 查詢內容是否已經被獲取/填充:

   ```cmake
   FetchContent_GetProperties(googletest)
   ```

4. 前面的函數定義了`googletest_POPULATED`。如果內容還沒有填充，我們獲取內容並配置子項目:

   ```cmake
   if(NOT googletest_POPULATED)
     FetchContent_Populate(googletest)
     
     # ...
     
     # adds the targets: gtest, gtest_main, gmock, gmock_main
     add_subdirectory(
       ${googletest_SOURCE_DIR}
       ${googletest_BINARY_DIR}
       )
       
     # ...
     
   endif()
   ```

5. 注意配置時獲取內容的方式:

   ```shell
   $ mkdir -p build
   $ cd build
   $ cmake ..
   ```

6. 這將生成以下構建目錄樹。Google Test源現已就緒，剩下的就交由CMake處理，並提供所需的目標:

   ```shell
   build/
   ├── ...
   ├── _deps
   │    ├── googletest-build
   │    │    ├── ...
   │    │    └── ...
   │    ├── googletest-src
   │    │    ├── ...
   │    │    └── ...
   │    └── googletest-subbuild
   │         ├── ...
   │         └── ...
   └── ...
   ```

## 工作原理

`FetchContent`模塊支持在配置時填充內容。例子中，獲取了一個Git庫，其中有一個Git標籤:

```cmake
FetchContent_Declare(
  googletest
  GIT_REPOSITORY https://github.com/google/googletest.git
  GIT_TAG release-1.8.0
)
```

CMake的3.11版本中，`FetchContent`已經成為CMake的標準部分。下面的代碼中，將嘗試在配置時使用`ExternalProject_Add`模擬`FetchContent`。這不僅適用於較老的CMake版本，而且可以讓我們更深入地瞭解`FetchContent`層下面發生了什麼，併為使用`ExternalProject_Add`在構建時獲取項目，提供一個有趣的替代方法。我們的目標是編寫一個`fetch_git_repo`宏，並將它放在`fetch_git_repo`中。這樣就可以獲取相應的內容了:

```cmake
include(fetch_git_repo.cmake)

fetch_git_repo(
  googletest
  ${CMAKE_BINARY_DIR}/_deps
  https://github.com/google/googletest.git
  release-1.8.0
)

# ...

# adds the targets: gtest, gtest_main, gmock, gmock_main
add_subdirectory(
  ${googletest_SOURCE_DIR}
  ${googletest_BINARY_DIR}
  )

# ...
```

這類似於`FetchContent`的使用。在底層實現中，我們將使用`ExternalProject_Add`。現在打開模塊，檢查`fetch_git_repo.cmake `中定義的`fetch_git_repo`:

```cmake
macro(fetch_git_repo _project_name _download_root _git_url _git_tag)

  set(${_project_name}_SOURCE_DIR ${_download_root}/${_project_name}-src)
  set(${_project_name}_BINARY_DIR ${_download_root}/${_project_name}-build)
  
  # variables used configuring fetch_git_repo_sub.cmake
  set(FETCH_PROJECT_NAME ${_project_name})
  set(FETCH_SOURCE_DIR ${${_project_name}_SOURCE_DIR})
  set(FETCH_BINARY_DIR ${${_project_name}_BINARY_DIR})
  set(FETCH_GIT_REPOSITORY ${_git_url})
  set(FETCH_GIT_TAG ${_git_tag})
  
  configure_file(
    ${CMAKE_CURRENT_LIST_DIR}/fetch_at_configure_step.in
    ${_download_root}/CMakeLists.txt
    @ONLY
    )
    
  # undefine them again
  unset(FETCH_PROJECT_NAME)
  unset(FETCH_SOURCE_DIR)
  unset(FETCH_BINARY_DIR)
  unset(FETCH_GIT_REPOSITORY)
  unset(FETCH_GIT_TAG)
  
  # configure sub-project
  execute_process(
    COMMAND
    "${CMAKE_COMMAND}" -G "${CMAKE_GENERATOR}" .
    WORKING_DIRECTORY
    ${_download_root}
    )
    
  # build sub-project which triggers ExternalProject_Add
  execute_process(
    COMMAND
    "${CMAKE_COMMAND}" --build .
    WORKING_DIRECTORY
    ${_download_root}
    )
endmacro()
```

宏接收項目名稱、下載根目錄、Git存儲庫URL和一個Git標記。宏定義了`${_project_name}_SOURCE_DIR `和`${_project_name}_BINARY_DIR`，我們需要在`fetch_git_repo`生命週期範圍內使用定義的`${_project_name}_SOURCE_DIR `和` ${_project_name}_BINARY_DIR`，因為要使用它們對子目錄進行配置:

```cmake
add_subdirectory(
  ${googletest_SOURCE_DIR}
  ${googletest_BINARY_DIR}
  )
```

`fetch_git_repo`宏中，我們希望使用`ExternalProject_Add`在配置時獲取外部項目，通過三個步驟實現了這一點:

1. 首先，配置` fetch_at_configure_step.in `:

   ```cmake
   cmake_minimum_required(VERSION 3.5 FATAL_ERROR)
   
   project(fetch_git_repo_sub LANGUAGES NONE)
   
   include(ExternalProject)
   
   ExternalProject_Add(
     @FETCH_PROJECT_NAME@
     SOURCE_DIR "@FETCH_SOURCE_DIR@"
     BINARY_DIR "@FETCH_BINARY_DIR@"
     GIT_REPOSITORY
     @FETCH_GIT_REPOSITORY@
     GIT_TAG
     @FETCH_GIT_TAG@
     CONFIGURE_COMMAND ""
     BUILD_COMMAND ""
     INSTALL_COMMAND ""
     TEST_COMMAND ""
     )
   ```

   使用`configure_file`，可以生成一個`CMakeLists.txt`文件，前面的佔位符被`fetch_git_repo.cmake`中的值替換。注意，前面的`ExternalProject_Add`命令僅用於獲取，而不僅是配置、構建、安裝或測試。

2. 其次，使用配置步驟在配置時觸發`ExternalProject_Add`(從主項目的角度):

   ```cmake
   # configure sub-project
   execute_process(
     COMMAND
     	"${CMAKE_COMMAND}" -G "${CMAKE_GENERATOR}" .
     WORKING_DIRECTORY
     	${_download_root}
     )
   ```

3. 最後在`fetch_git_repo.cmake `中觸發配置時構建步驟:

   ```cmake
   # build sub-project which triggers ExternalProject_Add
   execute_process(
     COMMAND
     	"${CMAKE_COMMAND}" --build .
     WORKING_DIRECTORY
     	${_download_root}
     )
   ```

這個解決方案的一個優點是，由於外部依賴項不是由`ExternalProject_Add`配置的，所以不需要通過`ExternalProject_Add`調用任何配置，將其引導至項目。我們可以使用`add_subdirectory`配置和構建模塊，就像外部依賴項是項目源代碼樹的一部分一樣。聰明的偽裝!

## 更多信息

有關`FetchContent`選項的詳細討論，請參考https://cmake.org/cmake/help/v3.11/module/FetchContent.html
配置時`ExternalProject_Add`的解決方案靈感來自Craig Scott，博客文章:https://crascit.com/2015/07/25/cgtest/

