# 5.4 构建时运行自定义命令:Ⅱ. 使用add_custom_target

**NOTE**:*此示例代码可以在 https://github.com/dev-cafe/cmake-cookbook/tree/v1.0/chapter-5/recipe-04 中找到，其中包含一个C++例子。该示例在CMake 3.5版(或更高版本)中是有效的，并且已经在GNU/Linux、macOS和Windows上进行过测试。*

我们在前面的示例，讨论了`add_custom_command`有一些限制，可以通过`add_custom_target`绕过这些限制。这个CMake命令将引入新的目标，与`add_custom_command`相反，这些目标依次执行不返回输出。可以将`add_custom_target`和`add_custom_command`结合使用。使用这种方法，可以与其依赖项所在目录不同的目录指定自定义目标，CMake基础设施对项目设计模块化非常有用。

## 准备工作

我们将重用前一节示例，对源码进行简单的修改。特别是，将把压缩后的`tar`打包文件放在名为`deps`的子目录中，而不是存储在主目录中。这个子目录包含它自己的`CMakeLists.txt`，将由主`CMakeLists.txt`调用。

## 具体实施

我们将从主`CMakeLists.txt`开始，然后讨论`deps/CMakeLists.txt`:

1. 声明启用C++11：

   ```cmake
   cmake_minimum_required(VERSION 3.5 FATAL_ERROR)
   
   project(recipe-04 LANGUAGES CXX Fortran)
   
   set(CMAKE_CXX_STANDARD 11)
   set(CMAKE_CXX_EXTENSIONS OFF)
   set(CMAKE_CXX_STANDARD_REQUIRED ON)
   ```

2. 现在，继续讨论`deps/CMakeLists.txt`。这通过`add_subdirectory`命令实现:

   ```cmake
   add_subdirectory(deps)
   ```

3. `deps/CMakeLists.txt`中，我们首先定位必要的库(BLAS和LAPACK):

   ```cmake
   find_package(BLAS REQUIRED)
   find_package(LAPACK REQUIRED)
   ```

4. 然后，我们将`tar`包的内容汇集到一个变量`MATH_SRCS`中:

   ```cmake
   set(MATH_SRCS
     ${CMAKE_CURRENT_BINARY_DIR}/wrap_BLAS_LAPACK/CxxBLAS.cpp
     ${CMAKE_CURRENT_BINARY_DIR}/wrap_BLAS_LAPACK/CxxLAPACK.cpp
     ${CMAKE_CURRENT_BINARY_DIR}/wrap_BLAS_LAPACK/CxxBLAS.hpp
     ${CMAKE_CURRENT_BINARY_DIR}/wrap_BLAS_LAPACK/CxxLAPACK.hpp
     )
   ```

5. 列出要打包的源之后，定义一个目标和一个命令。这个组合用于提取`${CMAKE_CURRENT_BINARY_DIR}`中的包。但是，这里我们在一个不同的范围内，引用`deps/CMakeLists.txt`，因此`tar`包将存放在到主项目构建目录下的`deps`子目录中:

   ```cmake
   add_custom_target(BLAS_LAPACK_wrappers
     WORKING_DIRECTORY
     	${CMAKE_CURRENT_BINARY_DIR}
     DEPENDS
     	${MATH_SRCS}
     COMMENT
     	"Intermediate BLAS_LAPACK_wrappers target"
     VERBATIM
     )
   
   add_custom_command(
     OUTPUT
     	${MATH_SRCS}
     COMMAND
     	${CMAKE_COMMAND} -E tar xzf ${CMAKE_CURRENT_SOURCE_DIR}/wrap_BLAS_LAPACK.tar.gz
     WORKING_DIRECTORY
     	${CMAKE_CURRENT_BINARY_DIR}
     DEPENDS
     	${CMAKE_CURRENT_SOURCE_DIR}/wrap_BLAS_LAPACK.tar.gz
     COMMENT
     	"Unpacking C++ wrappers for BLAS/LAPACK"
     )
   ```

6. 添加数学库作为目标，并指定相应的源，包括目录和链接库:

   ```cmake
   add_library(math "")
   
   target_sources(math
     PRIVATE
     	${MATH_SRCS}
     )
   
   target_include_directories(math
     INTERFACE
     	${CMAKE_CURRENT_BINARY_DIR}/wrap_BLAS_LAPACK
     )
   
   # BLAS_LIBRARIES are included in LAPACK_LIBRARIES
   target_link_libraries(math
     PUBLIC
     	${LAPACK_LIBRARIES}
     )
   ```

7. 执行完`deps/CMakeLists.txt`中的命令，返回到父范围，定义可执行目标，并将其链接到另一个目录的数学库:

   ```cmake
   add_executable(linear-algebra linear-algebra.cpp)
   
   target_link_libraries(linear-algebra
     PRIVATE
     	math
     )
   ```

## 工作原理

用户可以使用`add_custom_target`，在目标中执行定制命令。这与我们前面讨论的`add_custom_command`略有不同。`add_custom_target`添加的目标没有输出，因此总会执行。因此，可以在子目录中引入自定义目标，并且仍然能够在主`CMakeLists.txt`中引用它。

本例中，使用`add_custom_target`和`add_custom_command`提取了源文件的包。这些源文件稍后用于编译另一个库，我们设法在另一个(父)目录范围内链接这个库。构建`CMakeLists.txt`文件的过程中，`tar`包是在`deps`下，`deps`是项目构建目录下的一个子目录。这是因为在CMake中，构建树的结构与源树的层次结构相同。

这个示例中有一个值得注意的细节，就是我们把数学库的源标记为`PRIVATE`:

```cmake
set(MATH_SRCS
  ${CMAKE_CURRENT_BINARY_DIR}/wrap_BLAS_LAPACK/CxxBLAS.cpp
  ${CMAKE_CURRENT_BINARY_DIR}/wrap_BLAS_LAPACK/CxxLAPACK.cpp
  ${CMAKE_CURRENT_BINARY_DIR}/wrap_BLAS_LAPACK/CxxBLAS.hpp
  ${CMAKE_CURRENT_BINARY_DIR}/wrap_BLAS_LAPACK/CxxLAPACK.hpp
  )

# ...

add_library(math "")

target_sources(math
  PRIVATE
  	${MATH_SRCS}
  )

# ...
```

虽然这些源代码是`PRIVATE`，但我们在父范围内编译了`linear-algebra.cpp`，并且这个源代码包括`CxxBLAS.hpp`和`CxxLAPACK.hpp`。为什么这里使用`PRIVATE`，以及如何编译`linear-algebra.cpp`，并构建可执行文件呢？如果将头文件标记为`PUBLIC`, CMake就会在创建时停止，并出现一个错误，“无法找到源文件”，因为要生成(提取)还不存在于文件树中的源文件。

这是一个已知的限制(参见https://gitlab.kitware.com/cmake/cmake/issues/1633 ，以及相关的博客文章:https://samthursfield.wordpress.com/2015/11/21/cmake-depende-ncies-targets-and-files-and-custom-commands )。我们通过声明源代码为`PRIVATE`来解决这个限制。这样CMake时，没有获得对不存在源文件的依赖。但是，CMake内置的C/C++文件依赖关系扫描器在构建时获取它们，并编译和链接源代码。

