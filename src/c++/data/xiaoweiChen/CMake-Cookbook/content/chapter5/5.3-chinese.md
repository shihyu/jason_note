# 5.3 構建時運行自定義命令:Ⅰ. 使用add_custom_command

**NOTE**:*此示例代碼可以在 https://github.com/dev-cafe/cmake-cookbook/tree/v1.0/chapter-5/recipe-03 中找到，其中包含一個C++例子。該示例在CMake 3.5版(或更高版本)中是有效的，並且已經在GNU/Linux、macOS和Windows上進行過測試。*

項目的構建目標取決於命令的結果，這些命令只能在構建系統生成完成後的構建執行。CMake提供了三個選項來在構建時執行自定義命令:

1. 使用`add_custom_command`編譯目標，生成輸出文件。
2. `add_custom_target`的執行沒有輸出。
3. 構建目標前後，`add_custom_command`的執行可以沒有輸出。

這三個選項強制執行特定的語義，並且不可互換。接下來的三個示例將演示具體的用法。

## 準備工作

我們將重用第3章第4節中的C++示例，以說明如何使用`add_custom_command`的第一個選項。代碼示例中，我們瞭解了現有的BLAS和LAPACK庫，並編譯了一個很小的C++包裝器庫，以調用線性代數的Fortran實現。

我們將把代碼分成兩部分。` linear-algebra.cpp `的源文件與第3章、第4章沒有區別，並且將包含線性代數包裝器庫的頭文件和針對編譯庫的鏈接。源代碼將打包到一個壓縮的tar存檔文件中，該存檔文件隨示例項目一起提供。存檔文件將在構建時提取，並在可執行文件生成之前，編譯線性代數的包裝器庫。

## 具體實施

`CMakeLists.txt`必須包含一個自定義命令，來提取線性代數包裝器庫的源代碼：

1. 從CMake最低版本、項目名稱和支持語言的定義開始:

   ```cmake
   cmake_minimum_required(VERSION 3.5 FATAL_ERROR)
   project(recipe-03 LANGUAGES CXX Fortran)
   ```

2. 選擇C++11標準:

   ```cmake
   set(CMAKE_CXX_STANDARD 11)
   set(CMAKE_CXX_EXTENSIONS OFF)
   set(CMAKE_CXX_STANDARD_REQUIRED ON)
   ```

3. 然後，在系統上查找BLAS和LAPACK庫:

   ```cmake
   find_package(BLAS REQUIRED)
   find_package(LAPACK REQUIRED)
   ```

4. 聲明一個變量`wrap_BLAS_LAPACK_sources`來保存`wrap_BLAS_LAPACK.tar.gz`壓縮包文件的名稱:

   ```cmake
   set(wrap_BLAS_LAPACK_sources
     ${CMAKE_CURRENT_BINARY_DIR}/wrap_BLAS_LAPACK/CxxBLAS.hpp
     ${CMAKE_CURRENT_BINARY_DIR}/wrap_BLAS_LAPACK/CxxBLAS.cpp
     ${CMAKE_CURRENT_BINARY_DIR}/wrap_BLAS_LAPACK/CxxLAPACK.hpp
     ${CMAKE_CURRENT_BINARY_DIR}/wrap_BLAS_LAPACK/CxxLAPACK.cpp
     )
   ```

5. 聲明自定義命令來提取`wrap_BLAS_LAPACK.tar.gz`壓縮包，並更新提取文件的時間戳。注意這個`wrap_BLAS_LAPACK_sources`變量的預期輸出:

   ```cmake
   add_custom_command(
     OUTPUT
     	${wrap_BLAS_LAPACK_sources}
     COMMAND
     	${CMAKE_COMMAND} -E tar xzf ${CMAKE_CURRENT_SOURCE_DIR}/wrap_BLAS_LAPACK.tar.gz
     COMMAND
     	${CMAKE_COMMAND} -E touch ${wrap_BLAS_LAPACK_sources}
     WORKING_DIRECTORY
     	${CMAKE_CURRENT_BINARY_DIR}
     DEPENDS
     	${CMAKE_CURRENT_SOURCE_DIR}/wrap_BLAS_LAPACK.tar.gz
     COMMENT
     	"Unpacking C++ wrappers for BLAS/LAPACK"
     VERBATIM
     )
   ```

6. 接下來，添加一個庫目標，源文件是新解壓出來的:

   ```cmake
   add_library(math "")
   
   target_sources(math
     PRIVATE
     	${CMAKE_CURRENT_BINARY_DIR}/wrap_BLAS_LAPACK/CxxBLAS.cpp
     	${CMAKE_CURRENT_BINARY_DIR}/wrap_BLAS_LAPACK/CxxLAPACK.cpp
     PUBLIC
     	${CMAKE_CURRENT_BINARY_DIR}/wrap_BLAS_LAPACK/CxxBLAS.hpp
     	${CMAKE_CURRENT_BINARY_DIR}/wrap_BLAS_LAPACK/CxxLAPACK.hpp
     )
     
   target_include_directories(math
     INTERFACE
     	${CMAKE_CURRENT_BINARY_DIR}/wrap_BLAS_LAPACK
     )
     
   target_link_libraries(math
     PUBLIC
     	${LAPACK_LIBRARIES}
     )
   ```

7. 最後，添加`linear-algebra`可執行目標。可執行目標鏈接到庫:

   ```cmake
   add_executable(linear-algebra linear-algebra.cpp)
   
   target_link_libraries(linear-algebra
     PRIVATE
     	math
     )
   ```

8. 我們配置、構建和執行示例:

   ```shell
   $ mkdir -p build
   $ cd build
   $ cmake ..
   $ cmake --build .
   $ ./linear-algebra 1000
   
   C_DSCAL done
   C_DGESV done
   info is 0
   check is 4.35597e-10
   ```

## 工作原理

讓我們來瞭解一下`add_custom_command`的使用:

```cmake
add_custom_command(
  OUTPUT
  	${wrap_BLAS_LAPACK_sources}
  COMMAND
  	${CMAKE_COMMAND} -E tar xzf ${CMAKE_CURRENT_SOURCE_DIR}/wrap_BLAS_LAPACK.tar.gz
  COMMAND
  	${CMAKE_COMMAND} -E touch ${wrap_BLAS_LAPACK_sources}
  WORKING_DIRECTORY
  	${CMAKE_CURRENT_BINARY_DIR}
  DEPENDS
  	${CMAKE_CURRENT_SOURCE_DIR}/wrap_BLAS_LAPACK.tar.gz
  COMMENT
  	"Unpacking C++ wrappers for BLAS/LAPACK"
  VERBATIM
  )
```

`add_custom_command`向目標添加規則，並通過執行命令生成輸出。`add_custom_command`中聲明的任何目標，即在相同的`CMakeLists.txt`中聲明的任何目標，使用輸出的任何文件作為源文件的目標，在構建時會有規則生成這些文件。因此，源文件生成在構建時，目標和自定義命令在構建系統生成時，將自動處理依賴關係。

我們的例子中，輸出是壓縮`tar`包，其中包含有源文件。要檢測和使用這些文件，必須在構建時提取打包文件。通過使用帶有`-E`標誌的CMake命令，以實現平臺獨立性。下一個命令會更新提取文件的時間戳。這樣做是為了確保沒有處理陳舊文件。`WORKING_DIRECTORY`可以指定在何處執行命令。示例中，`CMAKE_CURRENT_BINARY_DIR`是當前正在處理的構建目錄。`DEPENDS`參數列出了自定義命令的依賴項。例子中，壓縮的`tar`是一個依賴項。CMake使用`COMMENT`字段在構建時打印狀態消息。最後，`VERBATIM`告訴CMake為生成器和平臺生成正確的命令，從而確保完全獨立。

我們來仔細看看這用使用方式和打包庫的創建：

```cmake
add_library(math "")

target_sources(math
  PRIVATE
    ${CMAKE_CURRENT_BINARY_DIR}/wrap_BLAS_LAPACK/CxxBLAS.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/wrap_BLAS_LAPACK/CxxLAPACK.cpp
  PUBLIC
    ${CMAKE_CURRENT_BINARY_DIR}/wrap_BLAS_LAPACK/CxxBLAS.hpp
    ${CMAKE_CURRENT_BINARY_DIR}/wrap_BLAS_LAPACK/CxxLAPACK.hpp
  )
  
target_include_directories(math
  INTERFACE
  	${CMAKE_CURRENT_BINARY_DIR}/wrap_BLAS_LAPACK
  )
  
target_link_libraries(math
  PUBLIC
  	${LAPACK_LIBRARIES}
  )
```

我們聲明一個沒有源的庫目標，是因為後續使用`target_sources`填充目標的源。這裡實現了一個非常重要的目標，即讓依賴於此目標的目標，瞭解需要哪些目錄和頭文件，以便成功地使用庫。C++源文件的目標是`PRIVATE`，因此只用於構建庫。因為目標及其依賴項都需要使用它們來成功編譯，所以頭文件是`PUBLIC`。包含目錄使用`target_include_categories`指定，其中`wrap_BLAS_LAPACK`聲明為`INTERFACE`，因為只有依賴於`math`目標的目標需要它。

`add_custom_command`有兩個限制:

* 只有在相同的`CMakeLists.txt`中，指定了所有依賴於其輸出的目標時才有效。
* 對於不同的獨立目標，使用`add_custom_command`的輸出可以重新執行定製命令。這可能會導致衝突，應該避免這種情況的發生。

第二個限制，可以使用`add_dependencies`來避免。不過，規避這兩個限制的正確方法是使用`add_custom_target`命令，我們將在下一節的示例中詳細介紹。