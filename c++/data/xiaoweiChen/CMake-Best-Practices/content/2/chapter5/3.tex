
若正在认真地编写软件，那么项目迟早会依赖于项目外部的库。而不是寻找单独的库文件或头文件，CMake社区和本书作者推荐的方法是使用\texttt{find\_package}，在CMake中查找依赖项的首选方法是使用包。

包提供了一组关于CMake和生成构建系统依赖关系的信息，可以以两种形式集成到项目中。可以通过上游项目提供的配置细节(也称为配置文件包)来实现，也可以通过所谓的查找模块包来实现，查找模块包通常定义在与包无关的地方，由CMake本身或使用包的项目来实现。这两种类型都可以通过使用\texttt{find\_package}找到，结果是一组导入的目标和/或一组变量，其中包含与构建系统相关的信息。

findPkgConfig模块使用find-pkg查找依赖项的相关元信息，还间接支持包的方式。

通常，find模块用于定位依赖项，例如：上游没有为包配置提供必要的信息时。不要将它们与CMake实用程序模块混淆，后者与\texttt{include()}一起使用。只要可能，就应该使用上游提供的包而不是find模块，修复上游项目以提供必要的信息要优于编写find模块。

注意，\texttt{find\_package}有两个签名:一个基本签名或短签名，一个完整签名或长签名。通常，使用短签名就足以找到正在寻找的包，因为它更容易维护，应该是首选。短格式同时支持模块和配置包，而长格式只支持配置模式。

短模式的签名如下:

\begin{lstlisting}[style=styleCMake]
find_package(<PackageName> [version] [EXACT] [QUIET] [MODULE]
	[REQUIRED] [[COMPONENTS] [components...]]
	[OPTIONAL_COMPONENTS components...]
	[NO_POLICY_SCOPE])
\end{lstlisting}

假设想要编写一个程序，通过OpenSSL库的适当功能将字符串转换为sha256哈希码。为了编译和链接这个示例，必须通知CMake该项目需要OpenSSL库，然后将其附加到目标中。现在，假设必要的库已经安装在系统上的默认位置;例如：Linux使用apt、RPM或类似的包管理器，Windows使用chocoley，macOS使用brew。

CMakeLists.txt文件可能是这样的:

\begin{lstlisting}[style=styleCMake]
find_package(OpenSSL REQUIRED COMPONENTS SSL)
add_executable(find_package_example)
target_link_libraries(find_package_example PRIVATE OpenSSL::SSL)
\end{lstlisting}

上面的例子做了以下事情:

\begin{enumerate}
\item 
第一行\texttt{find\_package(OpenSSL REQUIRED COMPONENTS SSL)}，正在为OpenSSL寻找一组库和头文件。具体来说，正在寻找SSL组件，而忽略加密组件。REQUIRED关键字说明必须使用其来构建这个项目，若没有找到库，CMake将停止配置过程，并出现错误。

\item 
找到包后，使用\texttt{target\_link\_library}将库链接到目标，并链接OpenSSL包提供的OpenSSL::SSL目标。
\end{enumerate}

若依赖指定了版本，则可以将其指定为major[.minor[.patch[.tweak]]]，或使用versionMin..[<]versionMax的方式作为版本划定范围。对于版本范围，versionMin和versionMax应该具有相同的格式，通过指定小于号，将排除versionMax。

但从3.21版本起，CMake无法查询可用组件的模块。因此，必须依赖模块或库提供者的文档，找出哪些组件是可用的。可用的模块可以通过以下命令查询:

\begin{tcblisting}{commandshell={}}
cmake --help-module-list #< lists all available modules
cmake --help-module <mod> #< prints the documentation for
  module <mod>
cmake --help-modules #< lists all modules and their
  documentation
\end{tcblisting}

可以在以下网站找到CMake的模块列表\url{https://cmake.org/cmake/help/latest/manual/cmake-modules.7.html}。

\begin{tcolorbox}[colback=blue!5!white,colframe=blue!75!black,title=查找单个库和文件]
可以查找单独的库和文件，但首选的方法是包。查找单独的文件，将它们提供给CMake将在编写查找模块的章节中介绍。
\end{tcolorbox}

在模块模式下运行时，\texttt{find\_package}会搜索名为Find<PackageName>.cmake的文件;首先会在CMAKE\_MODULE\_PATH指定的路径中搜索，然后是在外部提供的路径中查找模块。

在配置模式下运行时，\texttt{find\_package}会搜索以下的文件:

\begin{itemize}
\item 
<lowercasePackageName>-config.cmake

\item 
<PackageName>Config.cmake

\item 
<lowercasePackageName>-config-version.cmake (若指定了版本详细信息)

\item 
<PackageName>ConfigVersion.cmake (若指定了版本详细信息)
\end{itemize}

所有的搜索将按照明确的顺序进行，可以通过将相应的选项传递给CMake来跳过一些位置。\texttt{find\_package}比其他\texttt{find\_}指令包含更多的选项。下表展示了搜索顺序:

\begin{table}[H]
	\centering
	\begin{tabular}{|l|l|}
		\hline
		\textbf{位置}                     & \textbf{跳过命令中的选项}  \\ \hline
		包的根变量                & NO\_PACKAGE\_ROOT\_PATH              \\ \hline
		特定于CMake的缓存变量        & NO\_CMAKE\_PATH                      \\ \hline
		特定于CMake的环境变量  & NO\_CMAKE\_ENVIRONMENT\_PATH         \\ \hline
		HINTS选项中指定的路径  &                                      \\ \hline
		系统环境变量 & NO\_SYSTEM\_ENVIRONMENT\_PATH        \\ \hline
		用户包的注册表                 & NO\_CMAKE\_PACKAGE\_REGISTRY         \\ \hline
		特定于系统的缓存变量       & NO\_CMAKE\_SYSTEM\_PATH              \\ \hline
		系统包的注册表               & NO\_CMAKE\_SYSTEM\_PACKAGE\_REGISTRY \\ \hline
		PATHS选项中指定的路径   &                                      \\ \hline
	\end{tabular}
\end{table}

\begin{itemize}
\item 
包的根变量:每个\texttt{find\_package}包的根变量存储在名为<PackageName>\_ROOT中，是搜索包的首选。包的根变量与CMAKE\_PREFIX\_PATH相同，不仅用于\texttt{find\_package}，也可用于其他\texttt{find\_}指令。

\item 
特定于CMake的缓存变量:从CMAKE\_PREFIX\_PATH派生的位置。对于macOS, CMAKE\_FRAMEWORK\_PATH变量也是一个搜索位置。

\item 
通过设置CMAKE\_FIND\_USE\_CMAKE\_PATH变量为false，将跳过CMake特定缓存变量的位置。

\item 
特定于CMake的环境变量:除了指定CMAKE\_PREFIX\_PATH和CMAKE\_FRAMEWORK\_PATH作为缓存变量外，若设置为环境变量，CMake也会搜索它们。

\item 
将CMAKE\_FIND\_USE\_ENVIRONMENT\_PATH设置为false将禁用此行为。

\item 
find\_package的HINTS:这些是传递给\texttt{find\_package}的可选路径。

\item 
特定于系统的环境变量:PATH环境变量用于查找包和文件，并删除尾随的bin和sbin目录。每个系统的默认位置，例如/usr、/lib和类似的位置。

\item 
用户包的注册表:包要么在标准位置中找到，要么在通过CMAKE\_PREFIX\_PATH选项传递给CMake的位置中找到。包注册表是告诉CMake在哪里查找依赖项的另一种方法。用户注册表对当前用户帐户有效，而系统包注册表在系统范围内有效。Windows上，用户包的注册表位置存储在下面位置:

\item 
HKEY\_CURRENT\_USER\verb|\|Software\verb|\|Kitware\verb|\|CMake\verb|\|Packages\verb|\|<packageName>\

\item 
在Unix平台上，存储在用户的主目录中:~/.cmake/packages/<PackageName>

\item 
特定于平台的缓存变量:对于\texttt{find\_package}，特定于平台的缓存变量CMAKE\_SYSTEM\_PREFIX\_PATH，CMAKE\_SYSTEM\_FRAMEWORK\_PATH和CMAKE\_SYSTEM\_APPBUNDLE\_PATH的工作方式与其他\texttt{find}类似。它们由CMake设置，不应该由项目更改。

\item 
系统包的注册表:与用户包注册表类似，这是CMake查找包的位置。在Windows上，存储在 HKEY\_LOCAL\_MACHINE\verb|\|Software\verb|\|itware\verb|\|CMake\verb|\|Packages\verb|\|<packageName>\verb|\|。

\item 
Unix系统不提供系统包的注册表。

\item 
来自\texttt{find\_package}的路径:是传递给\texttt{find\_package}的可选路径。通常，HINTS选项是从其他值计算出来的，或者依赖于变量，而PATHS选项是固定路径。
\end{itemize}

具体来说，当在配置模式下查找包时，CMake将在各种前缀下查找以下文件结构:

\begin{lstlisting}[style=styleCMake]
<prefix>/
<prefix>/(cmake|CMake)/
<prefix>/<packageName>*/
<prefix>/<packageName>*/(cmake|CMake)/
<prefix>/(lib/<arch>|lib*|share)/cmake/<packageName>*/
<prefix>/(lib/<arch>|lib*|share)/<packageName>*/
<prefix>/(lib/<arch>|lib*|share)/<packageName>*/(cmake|CMake)/
<prefix>/<packageName>*/(lib/<arch>|lib*|share)/cmake/
  <packageName>*/
<prefix>/<packageName>*/(lib/<arch>|lib*|share)/<packageName>*/
<prefix>/<packageName>*/(lib/<arch>|lib*|share)/<packageName>*/
  (cmake|CMake)/
\end{lstlisting}

macOS平台上，还会搜索以下文件夹:

\begin{lstlisting}[style=styleCMake]
<prefix>/<packageName>.framework/Resources/
<prefix>/<packageName>.framework/Resources/CMake/
<prefix>/<packageName>.framework/Versions/*/Resources/
<prefix>/<packageName>.framework/Versions/*/Resources/CMake/
<prefix>/<packageName>.app/Contents/Resources/
<prefix>/<packageName>.app/Contents/Resources/CMake/
\end{lstlisting}

可以在官方的CMake文档中找到更多关于包的信息\url{https://cmake.org/cmake/help/latest/manual/cmake-packages.7.html}。

就模块而言，我们只讨论了如何查找现有的模块。但若想要寻找既没有集成到CMake中，也没有在标准位置中，或者没有为CMake提供配置说明的依赖项，会发生什么呢?让我们在下一节中寻找答案吧。

\subsubsubsection{5.3.1\hspace{0.2cm}编写查找模块}

目前仍然有很多库没有使用CMake管理，或者不导出CMake包。若可以将它们安装在系统的默认位置，找到这些库通常也不是问题。当使用仅为某个项目所需的专有第三方库，或者使用从系统包管理器安装的库构建的不同版本的库时，使用对应版本的库就成了件麻烦事。

若正在并行地开发多个项目，可能希望在本地处理每个项目的依赖关系。无论哪种方式，都应该在本地管理依赖项，而不是过多依赖于系统安装的东西，这是一种很好的实践方式。

若依赖项不存在模块和配置文件，解决方案就是编写find模块。我们的目标是提供足够的信息，以便可以使用\texttt{find\_package}找到。

find模块如何找到必要的头文件和二进制文件，以及为CMake创建导入目标的指令。如本章前面所述，当使用\texttt{find\_package}时，CMake在CMAKE\_MODULE\_PATH中搜索名为Find<PackageName>.cmake的文件。

假设正在构建一个项目，已经下载或构建了依赖项，并在使用之前将其放置在一个名为dep的文件夹中。所以，项目目录结构看起来可能是这样:

\begin{tcblisting}{commandshell={}}
├── dep <-- The folder where we locally keep dependencies
├── cmake
│     └── FindLibImagePipeline.cmake <-- This is what we need to write
├── CMakeLists.txt <-- Main CmakeLists.txt
├── src
│     ├── *.cpp files
\end{tcblisting}

要做的第一件事是将cmake文件夹添加到CMAKE\_MODULE\_PATH中，这是一个列表。因此，可在CMakeLists.txt文件中添加以下行:

\begin{lstlisting}[style=styleCMake]
list(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_SOURCE_DIR}/cmake")
\end{lstlisting}

CMake会在cmake文件夹中查找find模块。通常，find模块按以下顺序执行:

\begin{enumerate}
\item 
查找属于包的文件。
	
\item 
为包设置包含include路径和库目录的变量。

\item 
为导入的包设置目标。

\item 
为目标设置属性。
\end{enumerate}

一个简单的FindModules.cmake看起来像这样:

\begin{lstlisting}[style=styleCMake]
cmake_minimum_required(VERSION 3.21)
find_library(
	OBSCURE_LIBRARY
	NAMES obscure
	HINTS ${PROJECT_SOURCE_DIR}/dep/
	PATH_SUFFIXES lib bin build/Release build/Debug
)

find_path(
	OBSCURE_INCLUDE_DIR
	NAMES obscure/obscure.hpp
	HINTS ${PROJECT_SOURCE_DIR}/dep/include/
)

include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(
	Obscure
	DEFAULT_MSG
	OBSCURE_LIBRARY
	OBSCURE_INCLUDE_DIR
)

mark_as_advanced(OBSCURE_LIBRARY OBSCURE_INCLUDE_DIR)

if(NOT TARGET Obscure::Obscure)
	add_library(Obscure::Obscure UNKNOWN IMPORTED)
	set_target_properties(Obscure::Obscure PROPERTIES
		IMPORTED_LOCATION "${OBSCURE_LIBRARY}"
		INTERFACE_INCLUDE_DIRECTORIES "${OBSCURE_INCLUDE_DIR}"
		IMPORTED_LINK_INTERFACE_LANGUAGES "CXX"
	)
endif()
\end{lstlisting}

例子中，可以观察到:

\begin{enumerate}
\item 
首先，使用\texttt{find\_library}指令搜索属于依赖项的实际库文件。若找到了，其路径会包括实际的文件名，并存储在OBSCURE\_LIBRARY变量中，通常称为<PACKAGENAME>\_LIBRARY。NAMES参数是库的可能名称列表，名称会自动扩展为通用前缀和扩展名。前面的示例中，我们寻找名为“obscure”的文件libobscure，则会找到libobscure.so或obsile.dll。关于搜索顺序、提示和路径的更多细节将在本节后面介绍。

\item 
接下来，Find模块尝试定位包含路径。通过查找库的已知路径模式来实现，通常是公共头文件。结果会存储在OBSCURE\_INCLUDE\_DIR变量中，通常这个变量名为<PACKAGENAME>\_INCLUDE\_DIR。

\item 
由于处理find模块的所有需求可能非常繁琐，而且会有重复，所以CMake提供了FindPackageHandleStandardArgs模块，该模块提供了一个函数find\_package\_handle\_standard\_args来处理常见的情况，该函数处理REQUIRED、QUIET和\texttt{find\_package}的版本相关参数。find\_package\_handle\_standard\_args有一个短签名版本和一个长签名版本。本例中使用短签名版本:

\begin{lstlisting}[style=styleCMake]
find_package_handle_standard_args(<PackageName>
	(DEFAULT_MSG|<custom-failure-message>)
	<required-var>...
	)
\end{lstlisting}

\item 
对于大多数情况，find\_package\_handle\_standard\_args的简短形式就足够了。其中，find\_package\_handle\_standard\_args函数将包名作为第一个参数和包所需的变量列表。DEFAULT\_MSG参数表明在成功或失败的情况下打印默认消息，这取决于\texttt{find\_package}是用REQUIRED，还是QUIET。消息可以自定义，但建议尽可能使用默认消息。这样，所有\texttt{find\_package}的消息就一致了。前面的例子中，find\_package\_handle\_standard\_args检查已传递的OBSCURE\_LIBRARY和OBSCURE\_INCLUDE\_DIR变量是否有效，从而会设置<PACKAGENAME>\_FOUND变量。

\item 
若一切顺利，find模块将定义目标，这对于检查目标是否存在是很有帮助(以避免在有多个调用为相同的依赖时，覆盖目标的情况)。使用\texttt{add\_library}创建目标，因为还不能确定它是静态库还是动态库，所以类型为UNKNOWN，并设置了IMPORTED标志。

\item 
最后，设置库的属性。推荐的最小设置是MPORTED\_LOCATION属性，和包含文件所在的位置INTERFACE\_INCLUDE\_DIR。
\end{enumerate}

若一切正常，可以像这样使用库:

\begin{lstlisting}[style=styleCMake]
find_package(Obscure PRIVATE REQUIRED)
...
target_link_libraries(find_module_example Obscure::Obscure)
\end{lstlisting}

现在我们了解了如何将其他库添加到您的项目中(已经可以使用的话)。但如何在一开始就将这些库引入系统呢?让我们在下一节中找出答案。

