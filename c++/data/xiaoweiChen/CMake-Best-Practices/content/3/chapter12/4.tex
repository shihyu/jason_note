
能够毫不费力地为不同的架构交叉编译二进制文件，这为相关人员的开发人员工作带来了很多便利。但这些工作并不只构建二进制文件，还包括运行测试。若软件也在主机工具链上编译，并且测试足够通用，那么在主机上运行测试可能是测试软件的最简单方法。尽管在切换工具链和频繁地重新构建时，可能会花费一些时间。若这不可能或太耗时，一种替代方法就是在真正的目标硬件上运行测试，但这取决于硬件的可用性和在硬件上设置测试的工作量。因此，中间方案是在目标平台的模拟器中运行测试(若这可用)。

定义一个运行测试的模拟器，需要使用CROSSCOMPILING\_EMULATOR目标属性，既可以为单个目标设置，也可以通过设置CMAKE\_CROSSCOMPILING\_EMULATOR缓存变量全局设置，该缓存变量包含一个分号分隔的命令和参数列表，用于运行模拟器。若全局设置，该命令将添加到\texttt{add\_test()}、\texttt{add\_custom\_command()}和\texttt{add\_custom\_target()}中指定命令的前缀处，将用于运行\texttt{try\_run()}生成的可执行文件，所以用于构建的所有定制命令也必须在模拟器中访问和运行。CROSSCOMPILING\_EMULATOR属性并不一定是实际的模拟器——可以是任何程序，比如将二进制文件复制到目标计算机上，并在那里执行的脚本。

设置CMAKE\_CROSSCOMPILING\_EMULATOR应该通过工具链文件、命令行或配置的前缀进行。用于为ARM交叉编译C++代码的工具链文件示例，其中使用了开源模拟器QEMU来运行测试，如下所示:

\begin{lstlisting}[style=styleCMake]
set(CMAKE_SYSTEM_NAME Linux)
set(CMAKE_SYSTEM_PROCESSOR arm)

set(CMAKE_SYSROOT /path/to/arm/sysroot/)
set(CMAKE_CXX_COMPILER /usr/bin/clang++)
set(CMAKE_CXX_COMPILER_TARGET arm-linux-gnueabihf)

set(CMAKE_CROSSCOMPILING_EMULATOR "qemu-arm;-L;${CMAKE_	SYSROOT}")
\end{lstlisting}

除了设置目标系统的信息和交叉编译示例中最后一行的工具链之外，还将模拟器命令设置为qemu-arm -L/path/to/arm/sysroot。假设CMakeLists.txt文件包含如下测试:

\begin{lstlisting}[style=styleCMake]
add_test(NAME exampleTest COMMAND exampleExe)
\end{lstlisting}

当运行CTest时，测试命令转换为以下内容:

\begin{tcblisting}{commandshell={}}
qemu-arm "-L" "/path/to/arm/sysroot/" "/path/to/build-dir/exampleExe"
\end{tcblisting}

模拟器中运行测试可以加快开发人员的工作流程，因为它消除了主机工具链和目标工具链之间切换的必要，并且不需要为每个表面测试将构建构件移动到目标硬件。在持续集成(CI)构建中，使用这样的仿真器也很方便。

关于CMAKE\_CROSSCOMPILING\_EMULATOR，还可以用来将测试封装在调试工具中，如Valgrind或类似的调试工具。由于运行指定的模拟器可执行文件不依赖于CMAKE\_CROSSCOMPILING变量，CMAKE\_CROSSCOMPILING\_EMULATOR变量的缺点是，设置CMAKE\_CROSSCOMPILING\_EMULATOR变量将影响\texttt{try\_run()}，该指令通常用于测试工具链支持的特性或依赖，并且作为调试实用程序，可能导致编译器测试失败。可能有必要在已经缓存的构建上运行，其中\texttt{try\_run()}的结果已经缓存。正因为如此，不应该永久地使用CMAKE\_CROSSCOMPILING\_EMULATOR变量来运行调试程序执行，而应该在查找问题时，在特定的开发场景下执行。

本节中，提到了CMake的\texttt{try\_run()}指令，该指令与密切相关的\texttt{try\_compile()}指令一起使用，可以检查编译器或工具链中某些特性的可用性。下一节中，我们将更深入地研究这两个指令和测试工具链的特性。

\subsubsubsection{12.4.1\hspace{0.2cm}测试工具链支持的功能}

当CMake第一次在项目树上运行时，就会为编译器和语言特性执行各种测试。对\texttt{project()}或\texttt{enable\_language()}的使用都会触发测试，但结果可能在上一次运行中缓存了，这也是不建议在现有构建中切换工具链的原因。

大多数检查将在内部使用\texttt{try\_compile()}指令来执行这些测试。这个命令实际上是由用户进行检测，或工具链构建一个小的二进制文件。所有相关的全局变量，如CMAKE\_<LANG>\_FLAGS将转发至\texttt{try\_compile()}。

与其密切相关的是\texttt{try\_run()}，该指令在内部调用\texttt{try\_compile()}，若成功，将尝试运行程序。对于常规编译器检查，不使用\texttt{try\_run()}，对它的使用通常都会在项目中定义。

要编写自定义检查，而不是直接调用\texttt{try\_compile()}和\texttt{try\_run()}，建议使用CheckSourceCompiles或CheckSourceRuns模块的\texttt{check\_source\_compiles()}或\texttt{check\_source\_runs()}，这些指令自CMake 3.19起就已经可用了。大多数情况下，足以生成所需的信息，而不需要处理更复杂的\texttt{try\_compile()}或\texttt{try\_run()}。这两个指令的签名非常相似:

\begin{lstlisting}[style=styleCMake]
check_source_compiles(<lang> <code> <resultVar>
	[FAIL_REGEX <regex1> [<regex2>...]] [SRC_EXT <extension>])
check_source_runs(<lang> <code> <resultVar>
[SRC_EXT <extension>])
\end{lstlisting}

<lang>参数指定CMake支持的一种语言，例如对于C++来说是C或CXX。<code>是将作为可执行文件链接的字符串的代码，因此必须包含main()函数。编译的结果将作为布尔值存储在<resultvar>缓存变量中。若为\texttt{check\_source\_compililes}提供FAIL\_REGEX，编译的输出将根据提供的表达式进行检查。代码将保存在一个临时文件中，扩展名与所选语言匹配;若文件的扩展名与默认扩展名不同，可以通过SRC\_EXT选项指定。

也有一些特定于语言的模块，称为Check<LANG>SourceCompiles和Check<LANG>SourceRuns，提供了各自的指令:

\begin{lstlisting}[style=styleCMake]
include(CheckCSourceCompiles)
check_c_source_compiles(code resultVar
	[FAIL_REGEX regexes...]
)

include(CheckCXXSourceCompiles)
check_cxx_source_compiles(code resultVar
	[FAIL_REGEX regexes...]
)
\end{lstlisting}

假设C++项目可以使用标准库的原子功能，若不支持，则退回到不同的实现。编译器对此的检查如下所示:

\begin{lstlisting}[style=styleCMake]
include(CheckSourceCompiles)

check_source_compiles(CXX "
#include <atomic>
int main(){
	std::atomic<unsigned int> x;
	x.fetch_add(1);
	x.fetch_sub(1);
}" HAS_STD_ATOMIC)
\end{lstlisting}

包含模块后，小程序使用\texttt{check\_source\_compiles()}，在该程序中使用要检查的功能。若代码编译成功，HAS\_STD\_ATOMIC将设置为true;否则，设置为false。该测试在项目配置期间执行，会将打印如下状态消息:

\begin{tcblisting}{commandshell={}}
[cmake] -- Performing Test HAS_STD_ATOMIC
[cmake] -- Performing Test HAS_STD_ATOMIC - Success
\end{tcblisting}

结果将缓存，这样CMake后续运行不会再次执行测试。很多情况下，检查程序是否编译已经提供了关于工具链的某个特性的足够信息，但有时必须运行底层程序才能获得所需的信息。为此，\texttt{check\_source\_runs()}类似于\texttt{check\_source\_compiles()}。\texttt{check\_source\_runs()}的使用注意事项是:若设置了CMAKE\_CROSSCOMPILING但没有设置模拟器命令，测试将只编译测试而不运行，除非设置了CMAKE\_CROSSCOMPILING\_EMULATOR。

有许多形式的CMAKE\_REQUIRED\_变量来控制检查如何编译代码。注意，这些变量缺少特定于语言的部分，若在运行针对不同语言的测试的项目上工作，则需要特别注意这一点。以下是对其中一些变量的解释:

\begin{itemize}
\item 
CMAKE\_REQUIRED\_FLAGS用于在CMAKE\_<LANG>\_FLAGS或CMAKE\_<LANG>\_FLAGS\_<CONFIG>变量中指定的任何标志后，传递额外的标志给编译器。

\item 
CMAKE\_REQUIRED\_DEFINITIONS指定格式为-DFOO=bar的编译器宏定义。

\item 
CMAKE\_REQUIRED\_INCLUDES指定要搜索的头文件目录列表。

\item 
CMAKE\_REQUIRED\_LIBRARIES指定链接程序时要添加的库列表。这些可以是库的文件名，也可以是导入的CMake目标。

\item 
CMAKE\_REQUIRED\_LINK\_OPTIONS指定链接器标志的列表。

\item
CMAKE\_REQUIRED\_QUIET可设置为true，来静默自检查的状态消息。
\end{itemize}

检查需要相互隔离的情况下，CMakePushCheckState模块提供了cmake\_push\_check\_state()，cmake\_pop\_check\_state()和cmake\_reset\_check\_state()来存储配置，恢复之前的配置，并重置配置:

\begin{lstlisting}[style=styleCMake]
include(CMakePushCheckState)
cmake_push_check_state()
# Push the state and clean it to start with a clean check state
cmake_reset_check_state()

include(CheckCompilerFlag)
check_compiler_flag(CXX -Wall WALL_FLAG_SUPPORTED)

if(WALL_FLAG_SUPPORTED)
	set(CMAKE_REQUIRED_FLAGS -Wall)
	
	# Preserve -Wall and add more things for extra checks
	cmake_push_check_state()
	set(CMAKE_REQUIRED_INCLUDES ${CMAKE_CURRENT_SOURCE_DIR}/include)

	include(CheckSymbolExists)
	check_symbol_exists(hello "hello.hpp" HAVE_HELLO_SYMBOL)
	
	cmake_pop_check_state()
endif()
# restore all CMAKE_REQUIRED_VARIABLEs to original state
cmake_pop_check_state()
\end{lstlisting}

检查编译或运行测试程序的更复杂的\texttt{try\_compile()}和\texttt{try\_run()}。虽然可以外部使用，但它们主要用于内部使用，因此建议参考命令的官方文档，就不在这里解释它们了。

通过编译和运行程序来检查编译器特性，是检查工具链特性的一种通用方法。有些检查非常常见，以至于CMake为它们提供了专门的模块和功能。

\subsubsubsection{12.4.2\hspace{0.2cm}工具链和语言特性的常规检查}

对于一些常规的特性检查，例如检查是否支持编译器标志或是否存在头文件，CMake提供了自己的模块。从CMake 3.19起，以该语言作为参数的通用模块就存在了，但相应的Check<LANG>…仍然可以使用于特定的语言模块。

CheckLanguage模块是检查某一语言的编译器是否可用的测试，若没有设置CMAKE\_<LANG>\_COMPILER变量，可用来检查某一语言的编译器是否可用。检查Fortran是否可用的示例如下:

\begin{lstlisting}[style=styleCMake]
Include(CheckLanguage)
check_language(Fortran)
if(CMAKE_Fortran_COMPILER)
	enable_language(Fortran)
else()
	message(STATUS "No Fortran support")
endif()
\end{lstlisting}

若检查成功，则设置相应的CMAKE\_<LANG>\_COMPILER变量。若变量是在检查之前设置的，则没有效果。

CheckCompilerFlag提供了check\_compiler\_flag()来检查当前编译器是否支持某个标志。在内部，将编译一个非常简单的程序，并解析输出以获得诊断消息。检查编译器支持CMAKE\_<LANG>\_FLAGS的标志将成功运行;否则，check\_compiler\_flag()将失败。下面的例子就在检查C++编译器是否支持-Wall标志:

\begin{lstlisting}[style=styleCMake]
include(CheckCompilerFlag)
check_compiler_flag(CXX -Wall WALL_FLAG_SUPPORTED)
\end{lstlisting}

若支持-Wall标志，WALL\_FLAG\_SUPPORTED缓存变量为true;否则，为false。

检查链接器标志的相应模块称为CheckLinkerFlag，其工作原理类似于检查编译器标志，但链接器标志不会直接传递给链接器。由于通常会通过编译器调用链接器，因此向链接器传递附加标志可以使用-Wl或-Xlinker这样的前缀来告诉编译器将标志传递过去。由于该标志是特定于编译器的，CMake提供了LINKER:前缀来自动替代该命令。例如，要向链接器传递标志，来生成关于执行时间和内存消耗的统计信息:

\begin{lstlisting}[style=styleCMake]
include(CheckLinkerFlag)
check_linker_flag(CXX LINKER:-stats LINKER_STATS_FLAG_SUPPORTED)
\end{lstlisting}

若链接器支持-stats标志，则LINKER\_STATS\_FLAG\_SUPPORTED变量则为true。

比较常用的检查模块有CheckLibraryExists、CheckIncludeFile和CheckIncludeFileCXX，用于检查某些库或包含文件是否存在于某些位置。

CMake提供了更详细的检查，可能非常特定于项目;例如，CheckSymbolExists和CheckSymbolExistsCXX模块检查某个符号是否作为预处理器定义、变量或函数存在。CheckStructHasMember将检查结构是否有某个成员，而CheckTypeSize可以使用CheckPrototypeDefinition检查非用户类型的大小，以及C和C++函数原型的定义。

CMake提供了相当多的检查，而且可用的检查列表可能会随着CMake的发展而增长。虽然检查在某些情况下有用，但应该注意不要进行过多的测试。检查的数量和复杂性将对配置步骤的速度产生相当大的影响，但有时并没有提供太多的好处。项目中进行大量的检查，也可能暗示项目有着不必要的复杂性。
