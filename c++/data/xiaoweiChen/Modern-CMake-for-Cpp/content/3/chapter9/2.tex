专业开发人员通常遵循规则，高级开发人员知道什么时候打破规则(他们可以证明需要这样做)。另一方面，据说非常资深的开发人员不会违反规则，因为不断向他人解释自己就是浪费时间。我的看法是，选择你认可的方式即可，专注于真正重要的事情，对产品有实际影响的事情。

当谈到编码风格和格式时，开发者面临着无数的选择:我们应该使用制表符。还是空格来缩进?若是空格，有多少?一列的字符限制是多少?在文件中呢?大多数情况下，这样的选择不会影响程序的行为，但确实会产生很多杂声，并开始冗长的讨论，而这些讨论不会给产品带来太多价值。

有些做法是公认的，但大多数时候是在争论个人偏好。毕竟，在超过120的列中强制执行80个字符是一种随意的选择。选择什么并不重要，因为它会影响软件代码的可读性，所以风格一致就好。

避免这种情况的最好方法是使用格式化工具，如clang-format。若代码的格式不正确我们会得到提醒，甚至可以进行修复。

下面是一个格式化代码的命令示例:

\begin{tcblisting}{commandshell={}}
clang-format -i --style=LLVM filename1.cpp filename2.cpp
\end{tcblisting}

-i选项告诉clang-format就地编辑文件。-{}-style选择应该使用的支持的格式化样式:LLVM，Google，Chromium, Mozilla, WebKit，或自定义(以文件方式提供)(扩展阅读部分有详细的链接)。

当然，我们不希望每次进行更改时都手动执行此命令，CMake应该将此作为构建过程的一部分来处理。我们已经知道如何在系统中找到clang-format(需要手动安装)。

还没有讨论如何将外部工具应用到所有源文件的过程。为此，我们会创建一个函数，可以放在cmake目录中:

\begin{lstlisting}[style=styleCMake]
# chapter09/01-formatting/cmake/Format.cmake

function(Format target directory)
	find_program(CLANG-FORMAT_PATH clang-format REQUIRED)
	set(EXPRESSION h hpp hh c cc cxx cpp)
	list(TRANSFORM EXPRESSION PREPEND "${directory}/*.")
	file(GLOB_RECURSE SOURCE_FILES FOLLOW_SYMLINKS
		LIST_DIRECTORIES false ${EXPRESSION}
	)
	add_custom_command(TARGET ${target} PRE_BUILD COMMAND
		${CLANG-FORMAT_PATH} -i --style=file ${SOURCE_FILES}
	)
endfunction()
\end{lstlisting}

Format函数接受两个参数:target和directory，在目标构建之前，其将格式化目录中的所有源文件。

从技术上讲，并非目录中的所有文件都必须属于目标(而且目标源可能位于多个目录中)。然而，查找属于目标(以及可能依赖的目标)的所有源文件和头文件是一个复杂的过程，特别是需要过滤掉外部库，且是不应该格式化的头文件。这种情况下，处理目录更容易管理，所以可以为格式化目标的每个目录调用函数。

这个函数有以下步骤:

\begin{enumerate}
\item 
找到系统中安装的clang-format二进制文件。若没有找到二进制文件，REQUIRED关键字将停止配置并报错。

\item 
创建要格式化的文件扩展名列表(用作globbing表达式)。

\item 
每个表达式前面加上到目录的路径。

\item 
递归地搜索源和头文件(使用前面创建的列表)，跳过目录，并将其路径放入SOURCE\_FILES变量中。

\item 
将格式化命令作为目标的PRE\_BUILD步骤。
\end{enumerate}

该命令将很好地用于中小型代码库。对于大量的文件，需要将绝对文件路径转换为相对路径，并使用目录作为工作目录执行格式化(list(transform)在这里很有用)。这可能是必要的，因为传递给shell的命令有长度限制(通常约为13000个字符)，太长的路径根本不适合。

来看看如何在实践中使用这个函数，将使用以下项目结构:

\begin{tcblisting}{commandshell={}}
- CMakeLists.txt
- .clang-format
- cmake
    |- Format.cmake
- src
    |- CMakeLists.txt
    |- header.h
    |- main.cpp
\end{tcblisting}

首先，需要设置项目，并将cmake目录添加到模块路径中:

\begin{lstlisting}[style=styleCMake]
# chapter09/01-formatting/CMakeLists.txt

cmake_minimum_required(VERSION 3.20.0)
project(Formatting CXX)
enable_testing()
list(APPEND CMAKE_MODULE_PATH "${CMAKE_SOURCE_DIR}/cmake")
add_subdirectory(src bin)
\end{lstlisting}

设置好之后，为src目录完成列表文件:

\begin{lstlisting}[style=styleCMake]
# chapter09/01-formatting/src/CMakeLists.txt

add_executable(main main.cpp)
include(Format)
Format(main .)
\end{lstlisting}

我们已经创建了一个可执行的目标主文件，包括Format.cmake模块，并为当前目录(src)中的主目标使用Format()函数。

现在，需要一些未格式化的源文件。头文件中只是一个简单的函数:

\begin{lstlisting}[style=styleCXX]
// chapter09/01-formatting/src/header.h

int unused() { return 2 + 2; }
\end{lstlisting}

我们还将添加一个有很多空格的源文件:

\begin{lstlisting}[style=styleCXX]
// chapter09/01-formatting/src/main.cpp

#include <iostream>
	using namespace std;
		int main() {
			
			cout << "Hello, world!" << endl;
		}
\end{lstlisting}

差不多准备好了。剩下的就是格式化器的配置文件(通过命令行的-{}style=file参数启用):

\begin{lstlisting}[style=stylePython]
# chapter09/01-formatting/.clang-format

BasedOnStyle: Google
ColumnLimit: 140
UseTab: Never
AllowShortLoopsOnASingleLine: false
AllowShortFunctionsOnASingleLine: false
AllowShortIfStatementsOnASingleLine: false
\end{lstlisting}

Clang Format将扫描父目录以找到.clang-format文件，该文件指定了确切的格式化规则。这允许指定每个小细节，或者定制前面提到的标准。这个例子中，选择从Google的编码风格开始，并进行了一些调整:将列限制为140个字符，删除制表符，并允许短循环、函数和if语句。

来看看在构建这个项目之后文件是什么样的(编译前会自动格式化):

\begin{lstlisting}[style=styleCXX]
// chapter09/01-formatting/src/header.h (formatted)

int unused() {
	return 2 + 2;
}
\end{lstlisting}

头文件格式化了，即使没有目标使用，短函数也不允许在一行中出现。正如预期的那样，格式化程序添加了新行。main.cpp文件现在看起来也很舒服:

\begin{lstlisting}[style=styleCXX]
// chapter09/01-formatting/src/main.cpp (formatted)

#include <iostream>
using namespace std;
int main() {
	cout << "Hello, world!" << endl;
}
\end{lstlisting}

删除不必要的空白，标准化缩进。

添加自动格式化器并不需要花费太多精力，而且会为代码检查节省大量时间。若曾经手动修改过一些空白，就会了解这种感觉，一致的格式会使代码更简洁。

\begin{tcolorbox}[colback=blue!5!white,colframe=blue!75!black,title=Note]
将格式化应用于现有的代码库，很可能会对存储库中的大多数文件引入很大的一次性更改。若(或您的团队成员)有一些正在进行的工作，这可能会导致许多合并冲突。最好是在完成所有挂起的修改之后协调这些工作。若不能这样做，则考虑逐步进行，也许是在每个目录的基础上。您的开发同事会感谢您的。
\end{tcolorbox}

格式化器是一个伟大而简单的工具，可以将代码的可视化方面整合在一起，但不是一个成熟的程序分析工具(主要关注空白)。要处理更高级的场景，需要对编译程序的源码进行代码静态分析。