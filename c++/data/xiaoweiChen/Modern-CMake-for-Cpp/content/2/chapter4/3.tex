
使用自定义目标有一个缺点——添加到ALL目标或开始为其他目标依赖于它们，每次都进行构建(可以在if块中启用它们以限制)。有时，这是你想要的，但定制行为是必要的，以产生不应该无故重新创建的文件:

\begin{itemize}
\item 
生成另一个目标所依赖的源代码文件

\item 
将另一种语言翻译成C++

\item 
紧接在构建另一个目标之前或之后执行自定义操作
\end{itemize}

自定义命令有两个签名。第一个是add\_custom\_target()的扩展版本:

\begin{lstlisting}[style=styleCMake]
add_custom_command(OUTPUT output1 [output2 ...]
					COMMAND command1 [ARGS] [args1...]
					[COMMAND command2 [ARGS] [args2...] ...]
					[MAIN_DEPENDENCY depend]
					[DEPENDS [depends...]]
					[BYPRODUCTS [files...]]
					[IMPLICIT_DEPENDS <lang1> depend1
									[<lang2> depend2] ...]
					[WORKING_DIRECTORY dir]
					[COMMENT comment]
					[DEPFILE depfile]
					[JOB_POOL job_pool]
					[VERBATIM] [APPEND] [USES_TERMINAL]
					[COMMAND_EXPAND_LISTS])
\end{lstlisting}

自定义命令并不创建逻辑目标，但与自定义目标一样，其必须添加到依赖关系图中。有两种方法可以做到这一点——使用其输出工件作为可执行文件(或库)的源，或者显式地将其添加到自定义目标的DEPENDS列表中。

\subsubsubsection{4.3.1\hspace{0.2cm}使用自定义命令作为生成器}

诚然，不是每个项目都需要从其他文件生成C++代码，也可能是通过Google的协议缓冲区(Protobuf).proto文件的编译生成的。protobuf是一个平台无关的结构化数据二进制序列化器。为了跨平台和执行效率，Google工程师发明了他们自己的protobuf格式，可以在.proto文件中定义模型，例如:

\begin{lstlisting}[style=styleCMake]
message Person {
	required string name = 1;
	required int32 id = 2;
	optional string email = 3;
}
\end{lstlisting}

这样的文件可以跨多种语言共享——C++、Ruby、Go、Python、Java等。Google提供了读取.proto文件并输出对所选语言有效的结构和序列化代码的编译器。聪明的工程师不会将这些编译好的文件检入存储库，而是会使用原始的protobuf格式，并将其添加到构建链中。

我们还不知道如何检测protobuf编译器在目标主机上是否可用(以及在哪里可用)。因此，现在假设编译器的protoc命令位于系统已知的位置。我们知道protobuf编译器将输出person.pb.h和person.pb.cc文件。下面是如何定义一个自定义命令进行编译:

\begin{lstlisting}[style=styleCMake]
add_custom_command(OUTPUT person.pb.h person.pb.cc
		COMMAND protoc ARGS person.proto
		DEPENDS person.proto
)
\end{lstlisting}

然后，为了在可执行文件中序列化，只需向源文件添加输出文件:

\begin{lstlisting}[style=styleCMake]
add_executable(serializer serializer.cpp person.pb.cc)
\end{lstlisting}

假设正确地处理了头文件的包含和protobuf库的链接，当对.proto文件进行更改时，所有内容都将自动编译和更新。

一个简单的(而且不太实用的)例子是从另一个位置复制创建必要的头文件:

\begin{lstlisting}[style=styleCMake]
# chapter04/03-command/CMakeLists.txt

add_executable(main main.cpp constants.h)
target_include_directories(main PRIVATE
	${CMAKE_BINARY_DIR})
add_custom_command(OUTPUT constants.h
	COMMAND cp
	ARGS "${CMAKE_SOURCE_DIR}/template.xyz" constants.h)
\end{lstlisting}

本例中，“编译器”是cp命令，通过在构建树根中创建constants.h文件来实现对主目标的依赖，只需从源树中复制即可。

\subsubsubsection{4.3.2\hspace{0.2cm}使用自定义命令作为目标钩子}

add\_custom\_command()指令的第二个版本引入了在构建目标之前或之后执行命令的机制:

\begin{lstlisting}[style=styleCMake]
add_custom_command(TARGET <target>
					PRE_BUILD | PRE_LINK | POST_BUILD
					COMMAND command1 [ARGS] [args1...]
					[COMMAND command2 [ARGS] [args2...] ...]
					[BYPRODUCTS [files...]]
					[WORKING_DIRECTORY dir]
					[COMMENT comment]
					[VERBATIM] [USES_TERMINAL]
					[COMMAND_EXPAND_LISTS])
\end{lstlisting}

第一个参数中指定使用新行为“增强”的目标，并在以下条件下执行:

\begin{itemize}
\item 
PRE\_BUILD将在此目标的任何其他规则之前运行(仅Visual Studio生成器;对于其他的生成器，其行为像PRE\_LINK)。

\item 
PRE\_LINK绑定要在编译所有源之后但在链接(或存档)目标之前运行的命令。不适用于自定义目标。

\item 
POST\_BUILD将在为该目标执行所有其他规则之后运行。
\end{itemize}

使用这个版本的add\_custom\_command()，可以从前面的BankApp例子中复制校验和的生成方式:

\begin{lstlisting}[style=styleCMake]
# chapter04/04-command/CMakeLists.txt

cmake_minimum_required(VERSION 3.20.0)
project(Command CXX)

add_executable(main main.cpp)
add_custom_command(TARGET main POST_BUILD
					COMMAND cksum
					ARGS "$<TARGET_FILE:main>" > "main.ck")
\end{lstlisting}

主可执行文件的构建完成后，CMake将使用提供的参数执行cksum。第一个参数中发生了什么?它不是一个变量，因为不是包装在花括号(\$\{\})中，而是在尖括号(\$<>)中，而这是一个生成器表达式，计算到目标二进制文件的完整路径。这种机制在许多目标属性的上下文中非常有用。


