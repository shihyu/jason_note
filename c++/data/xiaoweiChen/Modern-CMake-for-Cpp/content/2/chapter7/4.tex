极少数情况下，在项目中会使用的库不提供config文件或PkgConfig文件，而且CMake中也没有现成的查找模块。可以为该库编写一个自定义查找模块，并将其与项目一起发布。这种情况并不理想，但为了照顾项目的用户，有时必须这样做。

上一节中已经熟悉了libpqxx，所以让我们编写一个漂亮的查找模块吧。首先编写一个新的FindPQXX.cmake文件，将它存储在项目源代码树的cmake/module目录中。需要确保当使用find\_package()时，CMake可以找到这个查找模块，所以会在CMakeLists.txt中将该目录追加到CMAKE\_MODULE\_PATH变量中。整个文件应该是这样的:

\begin{lstlisting}[style=styleCMake]
# chapter07/04-find-package-custom/CMakeLists.txt

cmake_minimum_required(VERSION 3.20.0) project(FindPackageCustom CXX) 

list(APPEND CMAKE_MODULE_PATH 
	"${CMAKE_SOURCE_DIR}/cmake/module/")
find_package(PQXX REQUIRED)
add_executable(main main.cpp)
target_link_libraries(main PRIVATE PQXX::PQXX)
\end{lstlisting}

现在，需要编写查找模块。若FindPQXX.cmake文件为空:若某些特定的变量没有设置(包括PQXX\_FOUND)， CMake不会报错，即使用户使用REQUIRED调用find\_package()。这取决于查找模块的作者是否遵守CMake文档中列出的约定:

\begin{itemize}
\item 
使用find\_package(<PKG\_NAME> REQUIRED)时，CMake将提供一个<PKG\_NAME>\_FIND\_REQUIRED变量，并设置为1。当没有找到库时，查找模块应该使用message(FATAL\_ERROR)。

\item 
使用find\_package(<PKG\_NAME> QUIET)时，CMake将提供一个<PKG\_NAME>\_FIND\_QUIETLY变量，并设置为1。查找模块应该跳过打印诊断消息(前面提到的除外)。

\item 
CMake将提供<PKG\_NAME>\_FIND\_VERSION变量，设置为调用列表文件所需的版本。查找模块应该找到适当的版本或发出FATAL\_ERROR信息。
\end{itemize}

当然，为了与其他查找模块保持一致，最好遵循前面的规则。为PQXX创建一个优雅的查找模块所需的步骤:

\begin{enumerate}
\item 
若库和头文件的路径已知(由用户提供，或来自前一次运行的缓存)，则使用这些路径并创建导入的目标。结束。

\item 
否则，找到嵌套依赖的库和头文件 —— PostgreSQL。

\item 
已知的路径中搜索二进制版本的PostgreSQL客户端库。

\item 
搜索PostgreSQL客户端包含头文件的已知路径。

\item 
检查是否找到库和include头文件。是的话，创建一个IMPORTED目标。
\end{enumerate}

IMPORTED目标的创建会发生两次——若用户从命令行提供库的路径，或者自动找到。首先编写一个函数来处理搜索过程的结果并保持代码的DRY。

要创建IMPORTED目标，只需要一个IMPORTED关键字的库(在CMakeLists.txt中的target\_link\_libraries()指令中使用它)。库必须提供一个类型——将其标记为UNKNOWN表示我们不想检测所找到的库是静态的，还是动态的，只是想为链接器提供一个参数。

接下来，IMPORTED\_LOCATION和INTERFACE\_INCLUDE\_DIRECTORIES导入目标的必需属性设置为调用函数时使用的参数。还可以指定其他属性(如COMPILE\_DEFINITIONS)，这些对PQXX来说不是必要的。

之后，将把路径存储在缓存变量中，这样就不需要再次执行搜索了。PQXX\_FOUND是在缓存中显式设置的，因此在全局变量作用域中是可见的(因此可以由用户的CMakeLists.txt访问)。

最后，将缓存变量标记为高级，除非启用了“高级”选项，否则它们在CMake GUI中是不可见的。这是这些变量的常见做法，我们也应该遵循这个惯例:

\begin{lstlisting}[style=styleCMake]
# chapter07/04-find-package-custom/cmake/module/FindPQXX.cmake

function(add_imported_library library headers)
	add_library(PQXX::PQXX UNKNOWN IMPORTED)
	set_target_properties(PQXX::PQXX PROPERTIES
		IMPORTED_LOCATION ${library}
		INTERFACE_INCLUDE_DIRECTORIES ${headers}
	)
	set(PQXX_FOUND 1 CACHE INTERNAL "PQXX found" FORCE)
	set(PQXX_LIBRARIES ${library}
		CACHE STRING "Path to pqxx library" FORCE)
	set(PQXX_INCLUDES ${headers}
		CACHE STRING "Path to pqxx headers" FORCE)
	mark_as_advanced(FORCE PQXX_LIBRARIES)
	mark_as_advanced(FORCE PQXX_INCLUDES)
endfunction()
\end{lstlisting}

接下来，介绍第一种情况——将PQXX安装在非标准位置的用户可以使用-D参数通过命令行提供必要的路径。若是这种情况，只需调用刚刚定义的函数，并通过使用return()进行转义来放弃搜索。相信用户最清楚，并提供库及其依赖项(PostgreSQL)的正确路径。

若配置阶段在过去执行过，这个条件也为true，因为PQXX\_LIBRARIES和PQXX\_INCLUDES变量缓存了。

\begin{lstlisting}[style=styleCMake]
if (PQXX_LIBRARIES AND PQXX_INCLUDES)
	add_imported_library(${PQXX_LIBRARIES} ${PQXX_INCLUDES})
	return()
endif()
\end{lstlisting}

现在是时候找到一些嵌套依赖项了。要使用PQXX，主机还需要PostgreSQL。查找模块中使用另一个查找模块没问题，但应该将REQUIRED和QUIET标志进行转发给(这样嵌套的搜索行为与外部的搜索行为一致)。这不是复杂的逻辑，但应该尽量避免不必要的代码。

CMake有一个内置的帮助宏，可以做这件事——find\_dependency()。有趣的是，文档指出它并不适合查找模块，因为若没有找到依赖项，会调用return()。因为这是一个宏(而不是函数)，所以return()将退出调用者的作用域FindPQXX.cmake文件，停止外部查找模块的执行。可能在某些情况下，这是不可取的，但在这个例子中，这正是我们想要做的——防止CMake掉进坑里，在已知PostgreSQL不可用时寻找PQXX的组件:

\begin{lstlisting}[style=styleCMake]
# deliberately used in mind-module against the
	documentation
include(CMakeFindDependencyMacro)
find_dependency(PostgreSQL)
\end{lstlisting}

要查找PQXX库，将设置一个\_PQXX\_DIR帮助变量(转换为cmake样式的路径)，并使用find\_library()扫描路径列表，在PATHS关键字后面提供路径。该指令将检查是否存在与另一个关键字NAMES后面提供的名称相匹配的库二进制文件。若找到匹配的文件，路径将存储在PQXX\_LIBRARY\_PATH变量中。否则，该变量将设置为<var>-NOTFOUND，或是本例中的PQXX\_HEADER\_PATH-NOTFOUND。

NO\_DEFAULT\_PATH关键字禁用默认行为，该行为将扫描CMake为该主机环境提供的一长串默认路径:

\begin{lstlisting}[style=styleCMake]
file(TO_CMAKE_PATH "$ENV{PQXX_DIR}" _PQXX_DIR)
find_library(PQXX_LIBRARY_PATH NAMES libpqxx pqxx
	PATHS
		${_PQXX_DIR}/lib/${CMAKE_LIBRARY_ARCHITECTURE}
		# (...) many other paths - removed for brevity
		/usr/lib
	NO_DEFAULT_PATH
)
\end{lstlisting}

接下来，将使用find\_path()指令搜索所有已知的头文件，该命令的工作原理与find\_library()非常相似。主要的区别是find\_library()知道库的特定于系统的扩展名，并将根据需要隐式附加这些扩展名，而对于find\_path()，我们需要提供确切的名称。

另外，不要在这里与pqxx/pqxx混淆。它是一个实际的头文件，但是库作者故意省略了扩展名，以遵循C++风格的\#include指令(而不是遵循C风格的.h扩展名):

\begin{lstlisting}[style=styleCXX]
#include <pqxx/pqxx>
\end{lstlisting}

\begin{lstlisting}[style=styleCMake]
find_path(PQXX_HEADER_PATH NAMES pqxx/pqxx
	PATHS
		${_PQXX_DIR}/include
		# (...) many other paths - removed for brevity
		/usr/include
	NO_DEFAULT_PATH
)
\end{lstlisting}

现在是时候检查PQXX\_LIBRARY\_PATH和PQXX\_HEADER\_PATH变量是否包含-NOTFOUND值了。同样，可以手动完成此操作，然后根据约定打印诊断消息或终止构建执行，或者可以使用CMake提供的FindPackageHandleStandardArgs列表文件中的find\_package\_handle\_standard\_args()助手函数。它是一个辅助指令，若填充路径变量，会将<PKG\_NAME>\_FOUND变量设置为1，并提供关于成功和失败的正确诊断消息(考虑QUIET关键字)。当REQUIRED关键字传递给查找模块时，若提供的路径变量中的一个为空，其还将使用FATAL\_ERROR终止执行。

若找到了库，将调用函数来定义导入的目标，并将路径存储在缓存中:

\begin{lstlisting}[style=styleCMake]
include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(
	PQXX DEFAULT_MSG PQXX_LIBRARY_PATH PQXX_HEADER_PATH
)
if (PQXX_FOUND)
	add_imported_library(
		"${PQXX_LIBRARY_PATH};${POSTGRES_LIBRARIES}"
		"${PQXX_HEADER_PATH};${POSTGRES_INCLUDE_DIRECTORIES}"
	)
endif()
\end{lstlisting}

这个查找模块将查找PQXX并创建适当的PQXX::PQXX目标。可以在本书示例库中找到这个文件: chapter07/04find-package-custom/cmake/module/FindPQXX.cmake.

若一个库很流行并且很可能已经安装在系统中，那么这个方法就非常有效。然而，并不是所有的库都是随时可用的。那我们能用CMake让用户更容易获取和构建这些依赖关系吗?



























